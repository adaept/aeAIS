Attribute VB_Name = "Gen"
' Template for formatting RTF files produced by Paratext or other programs,
' using USFM.sty version 2.35, released 2012-02-02
' August 2005, by Jim Henderson and Kim Blewett

' Global variables
'
Public Const UseUserTemplates As Boolean = True
Public Const GenSilasVersion As String = "2.35"
Public Const GenSilasBuild As String = "1.22"
Public Const PageOrPrintView As Integer = 3 ' because different versions of Word use different names.
Public Const HiddenColor = wdWhite
' Public Const HiddenColor = wdYellow
Public Const PageHeaderChapterNumbersSimple As String = "no"
Public Const DefaultBottomMargin = 0.9  ' inches, for A4/letter pages
Public Const SpaceBetweenQuotesScaling As Long = 5  ' 20% scaling between " and '

Public Const StdChapterStyle = "c - Chapter Number"
Public Const StdVerseStyle = "v - Verse Number"
Public Const StdParaStyle = "p - Paragraph - Normal - First Line Indent"
Public Const ThinSpaceScaling = 50

Public Const Wd2000 = 9
Public Const WdXP = 10
Public Const Wd2003 = 11
Public Const Wd2007 = 12
Public Const Wd2010 = 14        ' They didn't do a version 13!

Dim LanguageName As String      ' to control language-specific things
Dim LanguageProvince As String  ' for front matter
Public IniFile As String        ' settings filename + path

' These are set by LoadLanguageData from the ID line
' and "C:\My Paratext Projects\Languages.ini"

Public PtSettingsPathFromReg As String  ' Path to Pt settings folder in Registry
                                        ' (actually put there when Tr Notes Editor installed.)
Public PtSettingsFolderFromIni As String    ' Paratext settings folder path with \ at end.
Public PtSettingsDir As String              ' derived from either of the above

Public InstallFolder As String  ' default: C:\Program Files\Silas
Public Const IniPTname As String = "ParatextPath.ini"
Public IniPT As String
Public Const LangIniFile As String = "Languages.ini"
Dim ChaptersMadeDropCap As Boolean
Dim HeadingStyles(5) As String
Dim HeadingFound(5) As Boolean
Dim fs As Object, f As Object

' User's preference for smart quotes: initially null,
' set to "true" or "false"
' by EnableSmartQuotes. Used for restoring user's preference.
'
Dim SmartQuotesUserPreference As String
Dim SmartCutAndPasteUserPreference As String    ' & for Smart cut & Paste.

Dim FindNextMessageShown As Boolean

Public StyleToChange As String
Public CancelFormatting As Boolean
Public EditCheckingTable As Boolean
Public FormattingInProgress As Boolean  ' To reduce over-use of OptionalLineBreaks macro

Const ChangeEqualsToNoBreakSpace As Boolean = False

' Constants used if ReplaceAlmostEverywhere needs to do particular jobs this time.
Public Const iFixQuotes As Integer = 1      ' Change < characters etc into smart quotes
Public Const iNoBreakHyphens As Integer = 2 ' Change hyphens into no-break hyphens
Public Const iHyphens2Spaces As Integer = 4 ' Change hyphens into thin no-break spaces
Public Const iBrackets2HalfBrackets As Integer = 8
Public Const iSpecialCharacters As Integer = 16 ' " -- " to emdash, "!$" or "~" to no-break space
                                    ' and hyphens between numbers, as in John 1:1B-4 or \v 7-9
Public Const iResetFont As Integer = 32        ' remove any explicit font settings.
Public Const iSetDirectFormatting As Integer = 64   ' Apply direct formatting to fix default.

Option Compare Text     ' so language names will match better
Option Explicit
Sub AddCaption()
'
' AddCaption Macro
' Macro recorded 11/12/2009 by Jim Henderson
'
    On Error GoTo NoPictureSelected
    If Selection.InlineShapes(1).Type = wdInlineShapePicture Then
        Selection.MoveRight Unit:=wdCharacter, Count:=1
        Selection.TypeParagraph
        Selection.Style = ActiveDocument.Styles("_Caption")
        Exit Sub
    End If
    
NoPictureSelected:
    On Error GoTo 0
        MsgBox "You need to have a picture selected before you run this macro." & vbCr & vbCr & _
            "Please click inside a picture, then run this macro again." & vbCr & _
            "This will add the caption line, in the style '_Caption'," & vbCr & _
            "so you can paste or type in the caption for the picture.", _
            vbCritical, "ERROR -- No picture selected"
End Sub
Sub AddChapterCodesToVerseNumbers()
' Adds the current chapter number to each verse number in the invisible style
'   _CurrentChapter, so the page headers can do 4,5 on the page where chapter 5's code is.
'
    Dim CurrentChapter As String
    Dim Found As Boolean
    Dim FoundVerseNumber As Boolean
    Dim SomeVerseCodeFound As Boolean
    Dim FoundAnother As Boolean
    Dim FoundContent As Boolean
    Dim FoundIDLine As Boolean
    Dim rCurrentChapterNumber As Range
    Dim rThisChapter As Range
    Dim FoundVerseStyle As Boolean
    Dim StyleName As String
    Dim EOFReached As Boolean
    Dim NrVerseStylesFound As Long
    Dim StyleArrayIndex As Long
    Dim StyleArraySize As Long
    Dim ChapterLength As Long
    Dim i As Long
    
    CurrentChapter = " "        ' in case there isn't any chapter code.
    SomeVerseCodeFound = False
    NrVerseStylesFound = 0
    StyleArrayIndex = 1
    StyleArraySize = 3
    ReDim VerseStyles(StyleArraySize) As String
    StyleName = StdVerseStyle
    
    SetStandardFindOptions
    Selection.HomeKey Unit:=wdStory
    
    Set rThisChapter = Selection.Range.Duplicate
    ActiveDocument.ActiveWindow.View.Type = wdNormalView
    Application.StatusBar = "               Adding chapter codes to verse numbers -- please wait."
    Application.ScreenUpdating = False
    
    ' First, in case this file came from WordSend, we'll remove the numbers it put in.
    '
    SetStandardFindOptions
    Selection.Find.Style = ActiveDocument.Styles("cvmarker")
    
    With Selection.Find
        .Text = ""
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindContinue
        .Format = True
    End With
    Selection.Find.Execute Replace:=wdReplaceAll
    
    ' Now to make our own chapter numbers.
    '
    Selection.HomeKey Unit:=wdStory

    ' Find out which v, vp and va verse styles are used in this document
    '
    SetStandardFindOptions
    
    With Selection.Find
        .Wrap = wdFindStop
        .Format = True
    End With

    Do
        Selection.HomeKey Unit:=wdStory
        Selection.Find.Style = ActiveDocument.Styles(StyleName)
        Found = Selection.Find.Execute
        
        If Found Then
            NrVerseStylesFound = NrVerseStylesFound + 1
            
            If NrVerseStylesFound > StyleArraySize Then
                ReDim Preserve VerseStyles(NrVerseStylesFound)
            End If
            
            VerseStyles(NrVerseStylesFound) = ActiveDocument.Styles(StyleName)
        End If
        
        If StyleName = StdVerseStyle Then
            StyleName = "va...va* - Verse Number - Alternate"
        ElseIf StyleName = "va...va* - Verse Number - Alternate" Then
            StyleName = "vp...vp* - Verse Number - Publishing Alternate"
        ElseIf StyleName = "vp...vp* - Verse Number - Publishing Alternate" Then
            StyleName = ""
        End If
    Loop While StyleName <> ""
    
    Selection.HomeKey Unit:=wdStory
    SetStandardFindOptions
    
    ' for each chapter,
    '   get the chapter number in CurrentChapter
    '   select the text of the chapter
    '   if nothing but verse numbers, hide the chapter
    '   else
    '       put CurrentChapter in _CurrentChapter style before its last paragraph mark
    '       and for each verse number in the selection,
    '           put CurrentChapter and a fixed space in _CurrentChapter style before it.
    '
    Do
        Selection.Find.ClearFormatting
        Selection.Find.Replacement.ClearFormatting
        Selection.Find.Style = ActiveDocument.Styles(StdChapterStyle)
        
        With Selection.Find
            ' .Text = "([0-9]{1,})"
            .Text = "([0-9]{1" & Application.International(wdListSeparator) & "})"
            .Replacement.Text = ""
            .Forward = True
            .Wrap = wdFindStop
            .Format = True
            .MatchWildcards = True
        End With
        Found = Selection.Find.Execute
        
        If Found Then
            CurrentChapter = Selection.Text
            
            ' In case there is no text in the chapter
            ' find the next chapter code.
            
            ' skip down if this paragraph contains verse numbers.
            '
            Set rCurrentChapterNumber = Selection.Range.Duplicate
            Set rThisChapter = Nothing
            Set rThisChapter = Selection.Range.Duplicate
            
            Selection.Paragraphs(1).Range.Select
            Selection.Start = Selection.End
            
            ' Now to find the end of the chapter
            '
            Selection.Find.Style = ActiveDocument.Styles(StdChapterStyle)
            
            With Selection.Find
                ' .Text = "([0-9]{1,})"
                .Text = "([0-9]{1" & Application.International(wdListSeparator) & "})"
            End With
            FoundAnother = Selection.Find.Execute
            
            If FoundAnother Then
                rThisChapter.End = Selection.Start
            Else
                rThisChapter.End = ActiveDocument.Range.End
                EOFReached = True
            End If
            
            ' rThisChapter is the current chapter,
            ' either to next chapter code or to EOF.
            
            ' if only numbers and fixed spaces in this chapter,
            '   hide the whole chapter.
            '
            rThisChapter.Select     ' Selection is the current chapter, & maybe more.
            
            ' Now see if there is an ID line in the selection.
            '
            Selection.Find.ClearFormatting
            Selection.Find.Style = ActiveDocument.Styles("id - File - Identification")
            
            With Selection.Find
                .Text = ""
                .Replacement.Text = ""
                .Forward = True
                .Wrap = wdFindStop
                .Format = True
                .MatchWildcards = False
            End With
            FoundIDLine = Selection.Find.Execute
            
            If FoundIDLine Then
                If Selection.Start < rThisChapter.End Then
                    rThisChapter.End = Selection.Start
                End If
            End If
            
            ' Now to look for any content in the chapter.
            '
            rThisChapter.Select
            Selection.Find.ClearFormatting
            Selection.Find.Replacement.ClearFormatting
            
            With Selection.Find
'               .Text = "([!0-9a-dA-D^13^s -]{1,})"
                .Text = "([!0-9a-dA-D^13^s -]{1" & Application.International(wdListSeparator) & "})"
                .Replacement.Text = ""
                .Forward = True
                .Wrap = wdFindStop
                .Format = False
                .MatchWildcards = True
            End With
            FoundContent = Selection.Find.Execute
            
            If Not FoundContent Then
                rThisChapter.Delete
            Else
                ' Put current chapter number before final paragraph mark of the chapter
                '
                rThisChapter.Select     ' Selection is the current chapter,
                
                If Selection.End - Selection.Start > 1 Then
                    Selection.Start = Selection.End - 1
                    If Selection.Text = vbCr Then
                        Selection.InsertBefore CurrentChapter
                        Selection.End = Selection.End - 1
                        Selection.Style = ActiveDocument.Styles("_CurrentChapter")
                    End If
                End If
                
                ' Now put current chapter before each verse number.
                '
                For i = 1 To NrVerseStylesFound
                    StyleName = VerseStyles(i)
                    rThisChapter.Select     ' Selection is the current chapter,
                                            ' either to next chapter code or to EOF.
    '                Application.StatusBar = "       Adding chapter codes to verse numbers in chapter " & _
    '                    CurrentChapter & " -- please wait."
        
                    
                    ' Word 2000 doesn't return True for wdReplaceAll even though it changes them,
                    ' so we need to remember how big the chapter is at the beginning,
                    ' and see if the size has changed.
                    '
                    ChapterLength = rThisChapter.Characters.Count
        
                    ' The brute force approach -- add a chapter code and fixed space
                    '   in each possible place,
                    ' then reduce any multiple ones to single occurrances.
                    '
                    Selection.Find.ClearFormatting
                    ' Selection.Find.Style = ActiveDocument.Styles(i)
                    ' Selection.Find.Style = ActiveDocument.Styles(StyleName)
                    Selection.Find.Style = StyleName
                    Selection.Find.Replacement.ClearFormatting
                    With Selection.Find
        '               .Text = "([0-9-]{1,}^s)"
                        .Text = "([0-9a-d-]{1" & Application.International(wdListSeparator) & "}^s)"
                        ' .Replacement.Text = "\1;" & CurrentChapter
                        .Replacement.Text = CurrentChapter & ";\1"
                        .Forward = True
                        .Wrap = wdFindStop
                        .Format = True
                        .MatchWildcards = True
                    End With
                    FoundVerseNumber = Selection.Find.Execute(Replace:=wdReplaceAll)
                    
    '                If FoundVerseNumber Then
                    If rThisChapter.Characters.Count > ChapterLength Then
                        SomeVerseCodeFound = True
                        ' Set the style for Current Chapter codes.
                        '
                        ' Now apply the invisible style _CurrentChapter to the chapter codes.
                        '
                        Selection.Find.ClearFormatting
                        Selection.Find.Style = StyleName
                        Selection.Find.Replacement.ClearFormatting
                        Selection.Find.Replacement.Style = ActiveDocument.Styles("_CurrentChapter")
                        
                        With Selection.Find
                            ' .Text = ";(" & CurrentChapter & ")"
                            .Text = "(" & CurrentChapter & ");"
                            ' .Text = ";([0-9]{1" & Application.International(wdListSeparator) & "})"
                            .Replacement.Text = "\1^s"
                            .Forward = True
                            .Wrap = wdFindStop
                            .Format = True
                            .MatchWildcards = True
                        End With
                        FoundVerseNumber = Selection.Find.Execute(Replace:=wdReplaceAll)
                    End If
                Next i
            End If
        End If
        
        ' Now set up to do the next chapter.
        '
SkipThisChapter:
        If Found Then
            If Not EOFReached Then Selection.Start = rThisChapter.End
        End If
        Set rThisChapter = Nothing
    Loop While Found And Not EOFReached
    
    If SomeVerseCodeFound Then
        ' Now to make sure there aren't too many chapter codes.
        '
        Selection.HomeKey Unit:=wdStory
        Selection.Find.ClearFormatting
        Selection.Find.Style = "_CurrentChapter"
        Selection.Find.Replacement.ClearFormatting
        With Selection.Find
'               .Text = "([0-9-]{1,}^s)([0-9-]{1,}^s)"
                .Text = "([0-9]{1" & Application.International(wdListSeparator) & "}^s)" & _
                        "([0-9]{1" & Application.International(wdListSeparator) & "}^s)"
'               .Replacement.Text = CurrentChapter & "\1"
                .Replacement.Text = "\1"
            .Forward = True
            .Wrap = wdFindStop
            .Format = True
            .MatchCase = False
            .MatchWholeWord = False
            .MatchWildcards = True
        End With
        Selection.Find.Execute Replace:=wdReplaceAll
    End If
    SetStandardFindOptions
End Sub
Sub AddCheckedByTable()
' Include the checking table from a file in the Paratext settings folder
' so the user can have a modified version of it.
' If that fails, do it the old way, generating the table from scratch.
'
Dim CheckingTable As String
    
    CheckingTable = VariableGet("CheckingTable")
     
    If CheckingTable = "" Then
        CheckingTable = "Checking table.doc"
    End If
     
    If PtSettingsDir <> "" Then
        On Error GoTo -1: On Error GoTo IncludeError
        
        Selection.InsertFile _
            FileName:=PtSettingsDir & CheckingTable, _
            Range:="", _
            ConfirmConversions:=False, Link:=False, Attachment:=False
        On Error GoTo 0
        Selection.TypeBackspace
    Else
IncludeError:
        On Error GoTo 0
        With Options
            .DefaultBorderLineStyle = wdLineStyleSingle
            .DefaultBorderLineWidth = wdLineWidth050pt
            .DefaultBorderColorIndex = wdAuto
        End With
        
        ActiveDocument.Tables.Add Range:=Selection.Range, NumRows:=1, NumColumns:=4
        Selection.Tables(1).Select
        Selection.Style = ActiveDocument.Styles("_CheckingDetails")
        Selection.Collapse
        
        Selection.TypeText Text:="[_]"
        Selection.InsertSymbol CharacterNumber:=160, Unicode:=True, Bias:=0
        Selection.TypeText Text:="Checked"
        
        Selection.TypeParagraph
        Selection.TypeText Text:="[_]"
        Selection.InsertSymbol CharacterNumber:=160, Unicode:=True, Bias:=0
        Selection.TypeText Text:="Corrected in PT"
        
        Selection.MoveRight Unit:=wdCell
        Selection.TypeParagraph
        Selection.TypeText Text:="Date: __________"
        Selection.MoveRight Unit:=wdCell
        Selection.TypeParagraph
        Selection.TypeText Text:="By: ______________"
        Selection.TypeParagraph
        Selection.Font.Size = 7
        Selection.TypeText Text:="(Name of checker)"
        Selection.MoveRight Unit:=wdCell
        Selection.TypeParagraph
        Selection.TypeText Text:="In: _______________"
        Selection.TypeParagraph
        Selection.Font.Size = 7
        Selection.TypeText Text:="(Name of village)"
        Selection.Tables(1).Select
    
        With Selection.Tables(1)
            With .Borders(wdBorderLeft)
                .LineStyle = wdLineStyleSingle
                .LineWidth = wdLineWidth050pt
                '.Color = wdAuto
            End With
            With .Borders(wdBorderRight)
                .LineStyle = wdLineStyleSingle
                .LineWidth = wdLineWidth050pt
            End With
            With .Borders(wdBorderTop)
                .LineStyle = wdLineStyleSingle
                .LineWidth = wdLineWidth050pt
                '.Color = wdColorAutomatic
            End With
            With .Borders(wdBorderBottom)
                .LineStyle = wdLineStyleSingle
                .LineWidth = wdLineWidth050pt
                '.Color = wdColorAutomatic
            End With
            With .Borders(wdBorderVertical)
                .LineStyle = wdLineStyleSingle
                .LineWidth = wdLineWidth050pt
                '.Color = wdColorAutomatic
            End With
            .Borders.Shadow = False
        End With
    
    End If
    Selection.Tables(1).Select
    Selection.Cells.AutoFit
    
    On Error GoTo 0
End Sub
Sub AddFixedSpaces2VerseNumbers()
    Application.StatusBar = "               Adding fixed spaces after verse numbers -- please wait."
    Application.ScreenUpdating = False
    SetStandardFindOptions
    
    AddFixedSpaces2VerseNumbersInternal "v - Verse Number"
    AddFixedSpaces2VerseNumbersInternal "va...va* - Verse Number - Alternate"
    AddFixedSpaces2VerseNumbersInternal "vp...vp* - Verse Number - Publishing Alternate"
    
    ActiveDocument.StoryRanges(wdMainTextStory).Select
    SetStandardFindOptions
    Selection.HomeKey Unit:=wdStory
    Application.StatusBar = "                                                     "
    Application.ScreenUpdating = True
End Sub
Sub AddFixedSpaces2VerseNumbersInternal(ByVal StyleName As String)
'
' For each run of text in a verse number style
'   add a fixed space in the same style after it,,
'   provided there is not one there already.
' Also convert hyphens in verse numbers to no-break ones,
'   and remove spaces from the beginnings of lines.
'
    Dim FoundVerseNumber As Boolean
    Dim StyleManifested As Boolean

    ActiveDocument.ActiveWindow.View.Type = wdNormalView
        
    StyleManifested = False
    
    ' Wildcard searching is broken in Word 2003, and perhaps in other versions,
    ' in that
    '   .Text = "([0-9a-d-]{1,})"
    ' doesn't match if the character after the run of the style
    ' is within the range being matched (at least the a-d range).
    ' So we'll have to do it the slow way, and search for each run
    ' of any text in the verse number style, and add a fixed space
    ' after it.
    ' Why not use
    '   .Text = "(?{1,})"
    '   .Replacement.Text = "\1^s"
    ' That's broken too -- it replaces all characters in the style with
    ' fixed spaces :-(
    ' Shoddy Microsoft junk!
    On Error GoTo -1: On Error GoTo EndOfMacro
    Selection.HomeKey Unit:=wdStory
    Selection.Find.ClearFormatting
    Selection.Find.Style = ActiveDocument.Styles(StyleName)
    Selection.Find.Replacement.ClearFormatting
    On Error GoTo 0
    
    With Selection.Find
        .MatchWildcards = False
        .Text = ""
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindContinue
        .Format = True
        .MatchCase = False
        .MatchWholeWord = False
    End With
    
    Do
        FoundVerseNumber = Selection.Find.Execute
        
        If FoundVerseNumber Then
            If InStr(Selection.Text, vbCr) = 0 Then ' if not paragraph mark
                StyleManifested = True
                Selection.Font.Reset
                Selection.Style = ActiveDocument.Styles(StyleName)
                Selection.Collapse Direction:=wdCollapseEnd
'                    Selection.InsertAfter Chr$(160)    ' Problem in Japanese & Korean
                Selection.InsertAfter " "
                Selection.Style = ActiveDocument.Styles(StyleName)
            End If
            Selection.Collapse Direction:=wdCollapseEnd
        End If
    Loop While FoundVerseNumber _
         And Selection.Range.End < (ActiveDocument.Range.End - 1)
'            Loop While FoundVerseNumber    ' Infinite loop if no text after verse number.
    
    If Not StyleManifested Then GoTo EndOfMacro
    
    ' Now replace space in versenumber style with no-break space
    '
    Selection.HomeKey Unit:=wdStory
    
    With Selection.Find
        .Text = " "
        .Replacement.Text = "^s"
        .Format = True
        .MatchWildcards = False
    End With
    Selection.Find.Execute Replace:=wdReplaceAll
    
    ' Now see if there are any multiple fixed spaces in verse numbers,
    ' and reduce them to single ones.
    '
ReduceDoubleFixedSpaces:
    Selection.HomeKey Unit:=wdStory
    Selection.Find.Style = ActiveDocument.Styles(StyleName)
    With Selection.Find
        .Text = "^s^s"
        .Replacement.Text = "^s"
        .Format = True
        .MatchWildcards = False
    End With
    Selection.Find.Execute Replace:=wdReplaceAll
    
    If Selection.Find.Execute Then GoTo ReduceDoubleFixedSpaces
    
    Selection.HomeKey Unit:=wdStory
    
EndOfMacro:
    On Error GoTo 0

    SetStandardFindOptions
End Sub
Sub AddSectionBreaks(Optional SeparateIntro As Boolean)
'
' AddSectionBreaks Macro
' Macro created 6/06/2005 by Jim Henderson
'
' For each ID line,
'   if it starts a book different from the previous one,
'       put a section-break before it.
'       and a space formatted in invisible _CurrentChapter style
'       at the end of the ID line, so page headers won't have chapter numbers
'       from the previous book.
'       Then go down and put a section-break (continuous)
'           between main title/intro stuff and Scripture text,
'               in case the user wants to do double-columns.
'
    Dim BookCode As String
    Dim PreviousBookCode As String
    Dim Found As Boolean
    Dim BookIDLen As Long
    Dim EndOfStyle As Long
    Dim PreviousStyleName As String
    Dim StyleName As String
    Dim StartOfBook As Boolean
    Dim rIDField As Range
    
    PreviousStyleName = ""
    ActiveWindow.ActivePane.View.Type = wdNormalView
    Selection.HomeKey Unit:=wdStory

    Application.StatusBar = "               Adding section breaks -- please wait."
    Application.ScreenUpdating = False
    
    Selection.Find.ClearFormatting
    Selection.Find.Style = ActiveDocument.Styles("id - File - Identification ")

    With Selection.Find
        .Text = ""
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindStop
        .Format = True
    End With

    Do
        Found = Selection.Find.Execute

        If Found Then
            If (ActiveDocument.Range.End - Selection.End) < 2 Then GoTo Finished
            
            EndOfStyle = Selection.End

            ' step back off the paragraph mark
            '
            Selection.MoveEnd Unit:=wdCharacter, Count:=-1
            Set rIDField = Selection.Range.Duplicate
            rIDField.Text = Trim(rIDField.Text)
            rIDField.Select
            BookIDLen = InStr(1, Selection.Text, " ", vbTextCompare) - 1

            If BookIDLen = 3 Then       ' this is the start of a book
                Selection.End = Selection.Start + BookIDLen
                BookCode = Selection.Text

                If PreviousBookCode <> "" _
                And PreviousBookCode <> BookCode Then   ' Non-initial book beginning.
                    Selection.Collapse
                    Selection.InsertBreak Type:=wdSectionBreakNextPage
                    StartOfBook = True
                End If
                
                ' if this is a new book, put a space in _CurrentChapter
                ' at the end of the ID line.
                '
                If PreviousBookCode <> BookCode Then
                    rIDField.InsertAfter " "
                    rIDField.Select
                    Selection.Start = Selection.End - 1
                    Selection.Style = ActiveDocument.Styles("_CurrentChapter")
                End If
                
                ' step off ID line
                '
                rIDField.Select
                Selection.Start = Selection.End + 1
                
                ' If this is the start of a book
                ' and we are making double columns,
                '
                If PreviousBookCode <> BookCode _
                And SeparateIntro Then
                    '
                    ' Look for the change from Main Title or Intro material
                    ' to Scripture text, and put a section-break (continuous) between them.
                    '
                    Do
                        PreviousStyleName = StyleName
                        StyleName = Selection.Paragraphs(1).Style
                        If StyleName Like "[hi]*" _
                        Or StyleName Like "toc*" _
                        Or StyleName Like "OBSOLETE toc *" _
                        Or StyleName Like "periph *" _
                        Or StyleName Like "*pref *" _
                        Or StyleName Like "*intro *" _
                        Or StyleName Like "*glo *" _
                        Or StyleName Like "*conc *" _
                        Or StyleName Like "*idx *" _
                        Or StyleName Like "nidx *" _
                        Or StyleName Like "*chron *" _
                        Or StyleName Like "wm *" _
                        Or StyleName Like "*cov *" _
                        Or StyleName Like "*spine *" _
                        Or StyleName Like "re*" _
                        Or StyleName Like "_sts - Status" _
                        Or StyleName Like "rem - File - Status" _
                        Or StyleName Like "tr *" _
                        Or StyleName = "_SpaceAbove" _
                        Or StyleName Like "mt*" Then
                            If Selection.Information(wdWithInTable) Then
                                Selection.MoveDown Unit:=wdLine, Count:=1
                            Else
                                Selection.MoveDown Unit:=wdParagraph, Count:=1
                            End If
                            
                            If (ActiveDocument.Range.End - Selection.Start) < 2 Then
                                Exit Do
                            End If
                        Else
                            Exit Do
                        End If
                    Loop While True
                        
                    ' If the previous paragraph is "_SpaceAbove",
                    '   insert the section break before it.
                    '
                    If PreviousStyleName = "_SpaceAbove" Then
                        Selection.MoveUp Unit:=wdParagraph, Count:=1
                    End If
                    
                    ' Insert the section break.
                    '
                    Selection.InsertBreak Type:=wdSectionBreakContinuous
                    Selection.MoveLeft Unit:=wdCharacter, Count:=1
                    Selection.TypeParagraph
                    Selection.MoveLeft Unit:=wdCharacter, Count:=1, Extend:=wdExtend
                    Selection.Style = ActiveDocument.Styles("_ibr - Introduction - Blank Line - Rule")
                    Selection.Font.Reset    ' Should work in other languages.
                    Selection.Start = Selection.End
                End If
                
                PreviousBookCode = BookCode
            Else
                PreviousBookCode = ""
                Selection.Start = EndOfStyle
            End If
        End If
    Loop While Found

Finished:
    SetStandardFindOptions
    Selection.HomeKey Unit:=wdStory
    
    ' Now to make sure there aren't too many blank chapter codes.
    '
    Selection.Find.ClearFormatting
    Selection.Find.Style = ActiveDocument.Styles("_CurrentChapter")
    Selection.Find.Replacement.ClearFormatting
    With Selection.Find
'       .Text = " {1,}^s)"
        .Text = " {1" & Application.International(wdListSeparator) & "}"
        .Replacement.Text = " "
        .Forward = True
        .Wrap = wdFindStop
        .Format = True
        .MatchWildcards = True
    End With
    Selection.Find.Execute Replace:=wdReplaceAll

    Selection.EndKey Unit:=wdStory
    Selection.HomeKey Unit:=wdStory
    Application.StatusBar = "                                                "
    Application.ScreenUpdating = True
End Sub
Sub BookletMacroInstall()
' Open template for reading instructions
    Dim WordMacrosDoc As String

    If SetPathVariables = False Then Exit Sub
    
    WordMacrosDoc = InstallFolder & _
        "Booklet_Macros\Word Booklet Macros" & InstallingBookletVersion & ".doc"
    On Error GoTo -1: On Error GoTo OpenBookletFileFailed
    MsgBox _
        Title:="Opening file to install booklet macros", _
        Prompt:="I'm about to open the file with instructions on using the booklet macros." & _
            vbCr & vbCr & _
            "Please read these instructions, and print them if you can, then close that file.                        " & _
            vbCr & vbCr & _
            "To copy the macros to your Normal template, click the 'Install' button."
    Documents.Open _
        FileName:=WordMacrosDoc, _
        ReadOnly:=True
    On Error GoTo 0
    Documents(WordMacrosDoc).Activate
    Exit Sub

OpenBookletFileFailed:
    On Error GoTo 0
    MsgBox "Sorry, I wasn't able to open the file" & vbCr & _
            vbTab & "Booklet_Macros\Word Booklet Macros" & InstallingBookletVersion & ".doc" & vbCr & _
            "in the folder" & vbCr & _
            vbTab & ThisDocument.Path & vbCr & _
            "Please write down that path and filename," & vbCr & _
            "and try to open the file some other time.", _
        vbOKOnly, _
        "Error opening file to install booklet macros."
End Sub
Sub Brackets2HalfBrackets(ByRef rngStory As Range)
'
' Brackets2HalfBrackets Macro
' Macro recorded 3/24/2008 by Jim Henderson
'
    Dim rngThisStory As Range
    
    Application.StatusBar = "         Changing brackets to half brackets -- please wait."
    Application.ScreenUpdating = False
    SetStandardFindOptions
    
    Set rngThisStory = rngStory.Duplicate
    rngThisStory.Select

    With Selection.Find
        .Text = "["
        .Replacement.Text = ChrW(763)
    End With
    Selection.Find.Execute Replace:=wdReplaceAll
    
    rngThisStory.Select
    With Selection.Find
        .Text = "]"
        .Replacement.Text = ChrW(764)
    End With
    Selection.Find.Execute Replace:=wdReplaceAll
End Sub
Sub CallMakeHeaders()
    MakeHeaders MakeSectionBreaks:=False
End Sub
Sub ChangeDefaultPaperSize()
    ShowDbPaperSize
    If VariableGet("NoLanguageSet") = "True" Then Exit Sub
    SetDefaultPaperSize
        
    If Not FormattingInProgress Then
        OptionalLineBreaks
    End If
End Sub
Sub ChapterLabels()
    ' To handle \cl paragraphs, as described in USFM documentation
    '
    ' For each \cl
    '   if the previous paragraph is in \c style
    '       make \c style very small and white
    '   else if the next paragraph is a \c field
    '       put text after \cl in sChapterLabel
    '       make \cl style very small and white
    '       do for each \c
    '           if chapter number < previous and previous paragraph is \cl style
    '               replace sChapterLabel with its contents
    '           else clear sChapterLabel
    '           put contents of sChapterLabel & " " before chapter number.
    '
    Dim Found_cl As Boolean
    Dim Found_c As Boolean
    Dim sChapterLabel As String
    Dim rChapterLabel As Range
    Dim rChapterNumber As Range
    Dim iChapterSize As Integer
    Dim iPreviousChapterNumber As Integer
    Dim iThisChapterNumber As Integer
    
    iPreviousChapterNumber = 0
    iThisChapterNumber = 0
    ActiveDocument.ActiveWindow.View.Type = wdNormalView
    Application.StatusBar = "               Handling Chapter Label (\cl) codes -- please wait."
    Application.ScreenUpdating = False
    SetStandardFindOptions
    Selection.HomeKey Unit:=wdStory
    
    With Selection.Find
        .Wrap = wdFindStop
        .Format = True
    End With
    
    Do  ' for each \cl field
        Selection.Find.Style = ActiveDocument.Styles("cl - Chapter - Publishing Label")
        Found_cl = False
        Found_cl = Selection.Find.Execute
        
        If Not Found_cl Then Exit Do
        
        If Selection.Characters(Selection.Characters.Count) = vbCr Then
            Selection.End = Selection.End - 1
        End If
        
        Set rChapterLabel = Selection.Range.Duplicate
        
        ' Look at previous paragraph.
        '
        Selection.Collapse
        Selection.MoveUp Unit:=wdParagraph, Extend:=wdExtend

        If Selection.ParagraphFormat.Style = ActiveDocument.Styles(StdChapterStyle) Then
            PseudoHideSelectedParagraph StdChapterStyle
        Else    ' look at paragraph after \cl field
            rChapterLabel.Select
            Selection.Paragraphs(1).Range.Select
            Selection.MoveRight

            Selection.Paragraphs(1).Range.Select
            
            If Selection.ParagraphFormat.Style = ActiveDocument.Styles(StdChapterStyle) Then
                If Selection.Characters(Selection.Characters.Count) = vbCr Then
                    Selection.End = Selection.End - 1
                End If
                
                sChapterLabel = Trim(rChapterLabel.Text)
                rChapterLabel.Select
                PseudoHideSelectedParagraph "cl - Chapter - Publishing Label"
                Selection.MoveRight
                
                Do  ' for each \c field
                    Selection.Find.Style = ActiveDocument.Styles(StdChapterStyle)
                    Found_c = False
                    Found_c = Selection.Find.Execute
                    
                    If Not Found_c Then Exit Do
                    
                    Set rChapterNumber = Selection.Range.Duplicate
                    ' Look at chapter number value & perhaps at previous paragraph.
                    ' There might be a ca number after the chapter number,
                    '   so don't look beyond the space between them.
                    '
                    iChapterSize = InStr(rChapterNumber.Text, " ")
                    
                    If iChapterSize > 0 Then
                        iThisChapterNumber = CInt(Left(rChapterNumber.Text, iChapterSize - 1))
                    Else
                        iThisChapterNumber = CInt(rChapterNumber.Text)
                    End If
                    
                    If iThisChapterNumber < iPreviousChapterNumber Then
                        Selection.Collapse
                        Selection.MoveUp Unit:=wdParagraph, Extend:=wdExtend
                
                        If Selection.ParagraphFormat.Style = ActiveDocument.Styles("cl - Chapter - Publishing Label") Then
                            If Selection.Characters(Selection.Characters.Count) = vbCr Then
                                Selection.End = Selection.End - 1
                            End If
                            sChapterLabel = Trim(Selection.Text)
                        Else
                            sChapterLabel = ""      ' so \cl doesn't carry on to the next book
                        End If
                        
                        rChapterNumber.Select
                    End If
                    
                    ' Select the chapter number again, and put \cl field before it.
                    '
                    If sChapterLabel <> "" Then
                        Selection.InsertBefore sChapterLabel & " "
                    End If
                    
                    Selection.MoveDown
                    iPreviousChapterNumber = iThisChapterNumber
                Loop While Found_c
            End If
        End If
    Loop While Found_cl
    
    SetStandardFindOptions
    Selection.HomeKey Unit:=wdStory
    Application.StatusBar = "                                                     "
    Application.ScreenUpdating = True
End Sub
Sub CheckForChapterNumberRunningTooFar()
' Check that the user hasn't left out a paragraph code
' or something similar following a chapter number,
' or else a whole paragraph will be formatted as a chapter number,
' and setting dropcaps will make a big mess.
'
' For each run of text in chapter style
'   if it is an empty chapter paragraph
'       delete it
'   if there is text after those digits
'       complain and format it as paragraph style.
'
    Dim Found As Boolean
    Dim FoundVerseNumber As Boolean
    Dim FoundDigits As Boolean
    Dim rChapterText As Range
    Dim rChapterNumber As Range
    Dim NoMoreNotifications As Boolean
    
    Dim InsertCrAfter As Boolean
    Dim ParagraphMarkFound As Boolean
    Dim CharacterNextToChapter As Range
    Dim Answer As Integer
    Dim NrChecked As Long
    
    ActiveDocument.StoryRanges(wdMainTextStory).Select
    Selection.HomeKey Unit:=wdStory
    SetStandardFindOptions
    Application.ScreenUpdating = True
    ActiveDocument.ActiveWindow.View.Type = wdNormalView
    
    Do While ActiveDocument.ActiveWindow.Panes.Count > 1
        On Error Resume Next
        ActiveWindow.Panes(2).Close
        On Error GoTo 0
    Loop
    
    Application.ScreenUpdating = True
    Application.StatusBar = "    Checking for chapter codes running too far -- please wait."
    
    Do
FindAgain:              ' to come back here when the style of text has been changed.
        Found = False   ' to cope with a bug in Word
        Selection.Find.ClearFormatting
        Selection.Find.Style = ActiveDocument.Styles(StdChapterStyle)
        With Selection.Find
            .Text = ""
            .Replacement.Text = ""
            .Forward = True
            .Wrap = wdFindStop
            .Format = True
            .MatchWildcards = False
        End With
        
        Found = Selection.Find.Execute
        
        If Not Found Then GoTo EndOfMacro
        
        ' delete empty chapter lines
        '
        If Selection.Text = vbCr Then
            Selection.Delete
            
            If ActiveDocument.Range.End - Selection.End < 2 Then
                GoTo EndOfMacro
            Else
                GoTo FindAgain
            End If
        End If
        
        Set rChapterText = Selection.Range.Duplicate
        Set rChapterNumber = Selection.Range.Duplicate
        
        ' Decide if this file came from Pt6 or Pt7.
        ' If \p or similar missing after \c N, text will be like this:
        ' Pt6: <C-num><space><verse><^s><verse><^s> . . . <paragraph mark>
        ' Pt7: <verse><^s><verse><^s> . . . <C-num><paragraph mark>
        '
        ' if first char in selection is a chapter number,
        '   either it came from Pt6 or there is a \p code after the \c N
        ' else it came from Pt7.
        '
        If Selection.Characters(1).Style = ActiveDocument.Styles(StdChapterStyle) Then
            ' valid text or it came from Pt6, so step over the chapter number
            '
            ParagraphMarkFound = False
            InsertCrAfter = True
            rChapterNumber.Collapse
            
            Do
                Selection.Start = Selection.Start + 1
                Selection.End = Selection.Start + 1
                
                If Selection.Text = " " Then Selection.Delete
                
                If Selection.Text = vbCr Then
                    ParagraphMarkFound = True
                    rChapterNumber.End = Selection.Start
                End If
                    
                Selection.End = Selection.Start + 1
            Loop While Selection.Range.Style = ActiveDocument.Styles(StdChapterStyle)
            
            If rChapterNumber.Start = rChapterNumber.End Then
                rChapterNumber.End = Selection.Start
            End If

            Selection.End = Selection.Start + 1
        Else
            ' it came from Pt7,
            ' so separate the previous chapter's verses from the chapter number
            '
            ' Step before the final paragraph.
            '
            InsertCrAfter = False
            Selection.Start = Selection.End - 1
            rChapterNumber.Start = rChapterNumber.End
            
            If Selection.Text = vbCr Then
                rChapterNumber.End = Selection.Start
            End If
            
            ParagraphMarkFound = False
            
            Do
                Selection.End = Selection.End - 1
                Selection.Start = Selection.End - 1
                
                If Selection.Text = vbCr Then
                    ParagraphMarkFound = True
                End If
                
                If Selection.Text = " " Then Selection.Delete
                
            Loop While Selection.Range.Style = ActiveDocument.Styles(StdChapterStyle)
            
            Selection.Start = Selection.End - 1
            
            If rChapterNumber.Start = rChapterNumber.End Then
                rChapterNumber.Start = Selection.End
            End If
        End If

        ' Now to look at the adjacent character not in chapter style.
        '
        Set CharacterNextToChapter = Selection.Range.Duplicate
        ' Pt6: <C-num><space>[<verse>]<^s><verse><^s> . . . <paragraph mark>
        ' Pt7: <verse><^s><verse><^s> . . . <verse>[<^s>]<C-num><paragraph mark>
        '
        If Selection.Paragraphs(1).Style = ActiveDocument.Styles(StdChapterStyle) Then
            If Selection.Style = ActiveDocument.Styles(StdVerseStyle) Then
                If Not NoMoreNotifications Then
                    rChapterText.Select
                    
                    Answer = MsgBox( _
                        Prompt:="Oh dear, you have a verse number in a chapter paragraph." & vbCr & vbCr & _
                            "Every chapter number must be followed by a section heading, " & _
                            "or by a paragraph or something like that." & vbCr & vbCr & _
                            "I'll format this paragraph with the style '" & _
                            StdParaStyle & "'," & vbCr & vbCr & _
                            "If you want to see this message for other paragraphs, choose 'Yes'." & vbCr & _
                            "If you don't want to see this message for other paragraphs, choose 'No'." & vbCr & _
                            "If you don't want to fix any more paragraphs, choose 'Cancel'.", _
                        Buttons:=vbYesNoCancel, _
                        Title:="SILAS: No \p or \s etc between \c and \v")
                    
                    Select Case Answer
                        Case vbNo
                            NoMoreNotifications = True
                            Application.ScreenUpdating = False
                        Case vbCancel
                            GoTo EndOfMacro
                    End Select
                    
                    If NrChecked < 10 Then
                        NrChecked = NrChecked + 1
                    Else
                        If NrChecked < 11 Then
                            NrChecked = NrChecked + 1
                            Application.ScreenUpdating = False
                        End If
                    End If
                End If
            End If
        End If  ' chapter style has ended.
        
        rChapterNumber.Select

        If Not ParagraphMarkFound Then
            If InsertCrAfter Then
                Selection.InsertAfter vbCr
            Else
                Selection.InsertBefore vbCr
            End If
            
            CharacterNextToChapter.Select
            Selection.Start = Selection.End - 1
            Selection.Paragraphs(1).Range.Select
            Selection.Paragraphs(1).Style = ActiveDocument.Styles(StdParaStyle)
            rChapterNumber.Select
        End If

        Selection.MoveDown Unit:=wdParagraph, Count:=1
    Loop While Found

EndOfMacro:
    ' If the file came from Pt7,
    '   the last verses of a book can be marked in ID style.
    '   so scan the file for this situation & fix it.
    '
    SetStandardFindOptions
    Selection.HomeKey Unit:=wdStory
    Selection.Find.Style = ActiveDocument.Styles("v - Verse Number")
    With Selection.Find
        .Format = True
        .Wrap = wdFindStop
    End With
    
    Do
ReTryVerseCheck:
        Found = Selection.Find.Execute
        
        If Found Then
            Selection.Start = Selection.End - 1
            
            If Selection.Text = vbCr Then
                Selection.Font.Reset
                Selection.MoveUp Unit:=wdParagraph, Count:=1
                GoTo ReTryVerseCheck
            End If
            
            If Selection.ParagraphFormat.Style = "id - File - Identification" Then
                Selection.MoveRight Unit:=wdCharacter, Count:=1
                Selection.TypeParagraph
                Selection.MoveLeft Unit:=wdCharacter, Count:=1
                Selection.Style = ActiveDocument.Styles(StdParaStyle)
                Selection.MoveRight Unit:=wdCharacter, Count:=1
            Else
                Selection.Start = Selection.End
            End If
        End If
    Loop While Found

    Selection.HomeKey Unit:=wdStory
    SetStandardFindOptions
    Application.ScreenUpdating = True
End Sub
Sub CheckForIdIncludingVerseNumbers()
    ' Pt7 beta currently puts out multiple books
    ' without starting a new paragraph when an ID line comes out,
    ' so we'll put in paragraph breaks where necessary.
    Dim Found As Boolean
    Dim sParaStyle As Style
    Dim rIDline As Range
    
    Selection.HomeKey Unit:=wdStory
    SetStandardFindOptions
    
    Selection.Find.ClearFormatting
    Selection.Find.Style = ActiveDocument.Styles("id - File - Identification")
    With Selection.Find
        .Text = ""
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindStop
        .Format = True
        .MatchWildcards = False
    End With
    
    Do
        Found = False   ' to cope with a bug in Word
        Found = Selection.Find.Execute
        
        If Found Then
            Set rIDline = Selection.Range.Duplicate
            Selection.Paragraphs(1).Range.Select
            Set sParaStyle = Selection.Paragraphs(1).Range.Style
            
            If Selection.Characters(1).Style <> sParaStyle Then
                Selection.Collapse
                Do
                    Selection.End = Selection.End + 1
                    Selection.Start = Selection.End - 1
                Loop While Selection.Characters(1).Style <> sParaStyle
                
                Selection.InsertBefore Chr(13)
                rIDline.Select
                Selection.Collapse
                Selection.Style = ActiveDocument.Styles(StdParaStyle)
                rIDline.Select
                Selection.Start = Selection.End
            End If
        End If
    Loop While Found
    
    Selection.HomeKey Unit:=wdStory
    SetStandardFindOptions
End Sub
Public Function CheckForNewVersionB() As Boolean
' checks version of Silas.dot
' Always called after SetPathVariables succeeds, so we can count on them.
'
    Dim SilasBuildStr As String
    Dim OldVersionStr As String
    Dim NewVersion As Double, OldVersion As Double
    Dim NewBuild As Double, OldBuild As Double
    Dim fs As Object
    
    If IniFile = "" Then SetIniFile
    Set fs = CreateObject("Scripting.FileSystemObject")
    
    'Check previous template version from Inifile setting
    '
    If fs.FileExists(IniFile) Then
        NewVersion = InternationalCDbl(GenSilasVersion) ' the version number from the attached template
        OldVersionStr = System.PrivateProfileString(FileName:=IniFile, _
            Section:="TemplateVersionNumber", Key:="SVersion")
        If OldVersionStr = "" Or OldVersionStr = " " Then OldVersionStr = "0"
        OldVersion = InternationalCDbl(OldVersionStr)
        
        ' If the version number in the template is older than the ini file,
        '
        If NewVersion < OldVersion Then
            CheckForNewVersionB = False
        ElseIf NewVersion > OldVersion Then
            CheckForNewVersionB = True
        ElseIf NewVersion = OldVersion Then
            ' Check the build number
            '
            SilasBuildStr = System.PrivateProfileString(FileName:=IniFile, _
                Section:="TemplateVersionNumber", Key:="Build")
            If SilasBuildStr = "" Or SilasBuildStr = " " Then SilasBuildStr = "0"
            
            NewBuild = InternationalCDbl(GenSilasBuild)
            OldBuild = InternationalCDbl(SilasBuildStr)

            If NewBuild <= OldBuild Then
                CheckForNewVersionB = False
            ElseIf NewBuild > OldBuild Then
                CheckForNewVersionB = True
            End If
        End If
    Else: CheckForNewVersionB = True
    End If
End Function
Sub CheckPath(ByRef Path)
' Make sure the path variable has a path separator at the end.
'
    If Path <> "" _
    And Right(Path, 1) <> Application.PathSeparator Then
        Path = Path & Application.PathSeparator
    End If
End Sub
Static Function CheckPTPath() As String
' If SILAS.dot needs to be in the user templates folder,
'   check that it's there, set PtSettingsDir and return
' else
'   check that it's in Pt settings folder,
'   set PtSettingsDir and return.
'
Dim CurrentPath As String
Dim InstallMsgMain As String

    InstallMsgMain = _
        "If you know you've installed it already, be sure to " & vbCrLf & _
        "attach the template to your Scripture file as follows:" & vbCrLf & _
        "Hold down the Alt key and type TF to open the 'Tools' menu and do the job, " & vbCrLf & _
        vbTab & "'Attach Silas Formatting Template'. " & vbCrLf & vbCrLf & _
        "To read about how to do this, open the QuickLaunch bar, " & vbCrLf & _
        "and select the item, 'Silas Scripture Template Instructions,'" & vbCrLf & _
        "or do the job, " & vbCrLf & _
        "       Start Button -> (All) Programs -> Silas -> How to use Silas Scripture Template" & vbCrLf & _
        "Please ask for technical help if you need it."
    
    CurrentPath = ActiveDocument.AttachedTemplate.Path & Application.PathSeparator
    
    If UseUserTemplates Then
        If PtSettingsPathFromReg = "" _
        And PtSettingsFolderFromIni = "" Then
            If SetPathVariables = False Then
                MsgBox _
                    Title:="SILAS: not installed", _
                    Prompt:="It looks as if you haven't installed the SILAS package properly." & vbCrLf & vbCrLf & _
                        "Please open the START menu, then (All) Programs the SILAS then Install Silas.doc" & vbCrLf & _
                        "and operate the Install Template button in that file." & vbCrLf & vbCrLf & _
                        InstallMsgMain, _
                    Buttons:=vbCritical
                CheckPTPath = "bad"
                Exit Function
            End If
        End If
        
        If PtSettingsFolderFromIni <> "" Then
            PtSettingsDir = PtSettingsFolderFromIni
            CheckPath PtSettingsDir
            GoTo EndGood
        End If
        
        If PtSettingsPathFromReg <> "" Then
            PtSettingsDir = PtSettingsPathFromReg
            CheckPath PtSettingsDir
            GoTo EndGood
        End If
        
    Else
        If PtSettingsPathFromReg = "" Then
            If PtSettingsFolderFromIni = "" Then
                GoTo InstallMsg
            ElseIf CurrentPath = PtSettingsFolderFromIni Then
                PtSettingsDir = PtSettingsFolderFromIni
                CheckPath PtSettingsDir
                GoTo EndGood
            ElseIf CurrentPath <> PtSettingsFolderFromIni Then
                GoTo InstallMsg
            End If
            
        ElseIf CurrentPath = PtSettingsPathFromReg Then
            GoTo EndGood
        ElseIf CurrentPath <> PtSettingsPathFromReg Then
            GoTo InstallMsg
        End If
    
InstallMsg:
        MsgBox _
            "It looks as if you haven't installed this Scripture template correctly," & vbCr & _
            "or else you have opened another copy of the template file" & vbCrLf & _
            "that is not in your Paratext settings folder." & vbCrLf & vbCrLf & _
            "Please close this file, then open the file, " & _
            Chr(34) & "Install Silas.doc" & Chr(34) & "." & _
            vbCrLf & "Click on the 'Install Template' " & _
            "button in that file to install the template." & vbCrLf & vbCrLf & _
            InstallMsgMain, vbOKOnly, _
            "Template Not Installed"
            CheckPTPath = "bad"
            Exit Function
    End If

EndGood:
    CheckPTPath = "good"
End Function
Sub ClearDirectFormatting(rngStory As Word.Range, sStatusMessage As String)
'
' ClearDirectFormatting Macro
' Macro recorded 09-Oct-2018 by Jim
'
' For each Character Style,
'   Search for text marked with that style and reapply it,
'   to remove any direct formatting.
'
    Dim sCharacterStyleName As String
    Dim yStyle As Style
    
    Application.ScreenUpdating = True
    Application.StatusBar = sStatusMessage
    Application.ScreenUpdating = False
    SetStandardFindOptions
    
    ' rngStory.StoryType
    
    For Each yStyle In ActiveDocument.Styles
        If yStyle.Type = wdStyleTypeCharacter Then
            sCharacterStyleName = yStyle.NameLocal
            rngStory.Find.ClearFormatting
            rngStory.Find.Style = ActiveDocument.Styles(sCharacterStyleName)
            rngStory.Find.Replacement.ClearFormatting
            rngStory.Find.Replacement.Style = ActiveDocument.Styles(sCharacterStyleName)
            With rngStory.Find
                .Format = True
            End With
            rngStory.Find.Execute Replace:=wdReplaceAll
        End If
    Next
End Sub
Sub ConvertFootnotesToInline()
Dim bFindResult As Boolean
Dim oFeets As Footnotes
Dim oFoot As Footnote
Dim oRange As Range
Dim szFootNoteText As String

SetStandardFindOptions

' Grabs the collection of FootNotes
Set oFeets = Word.ActiveDocument.Footnotes

' Iterates through each footnote
For Each oFoot In oFeets
    
    szFootNoteText = oFoot.Range.Text
    
    'Start search from beginning of document
    Selection.HomeKey Unit:=wdStory
    ' Selection.Find.ClearFormatting
  
    With Selection.Find
        .Text = "^f" ' Looks for all footnotes
        .Forward = True
        .Wrap = wdFindStop
    End With
    
    bFindResult = Selection.Find.Execute
    
    ' Keep a place marker just before the footnote
    Set oRange = Selection.Range
    oRange.Select
    oRange.Collapse
    oRange.Start = oRange.Start - 10
    oRange.Select
    
    ' Delete the footnote
    oFoot.Delete
    oRange.Select
    oRange.End = Selection.Paragraphs(1).Range.End
    oRange.Start = oRange.End
    oRange.Select
    Selection.TypeParagraph
    
    'Insert the footnote text
    'Here you do whatever format tickles your fancy
'    Selection.Text = " [Note: " + szFootNoteText + "] "
    Selection.Style = ActiveDocument.Styles("xsl - Cross Reference Section Left")
    Selection.Text = szFootNoteText

    'CHANGE COLOR HERE. Color code is below.
    ' Selection.Font.Color = 6299648

    'Disables undo to save memory on very large documents.
    ' ActiveDocument.UndoClear
Next
End Sub
Sub ConvertTextIntoFields(NrFields As Integer)
    Dim n As Integer
    
    WordBasic.EditFind "}", Direction:=0, Format:=0
    n = 0
    While WordBasic.EditFindFound()
        n = n + 1
        If n > NrFields Then NrFields = n
        WordBasic.PrintStatusBar "Wait. Creating chapter field: " + String(n, Chr(135)) + String(NrFields - n, Chr(134))
        WordBasic.CharLeft
        WordBasic.ExtendSelection
        WordBasic.EditFind "{", Direction:=1, Format:=0
        If WordBasic.EditFindFound() Then
            WordBasic.CharRight 1, 1
            WordBasic.EditCut
            WordBasic.CharLeft 1, 1
            WordBasic.WW6_EditClear
            WordBasic.CharRight 1, 1
            
            ' Also delete spurious spaces.
            '
            While Not Selection.Text Like "*}"
                WordBasic.CharRight 1, 1
            Wend
            
            WordBasic.WW6_EditClear
            WordBasic.InsertFieldChars
            WordBasic.EditPaste
        Else
            ' Disable selection
            WordBasic.Cancel
        End If
        If WordBasic.EditFindFound() Then WordBasic.EditFind "}", Direction:=0, Format:=0
    Wend

End Sub
Sub CopyStyle(StyleName As String)
' If the style named does not exist in the document,
'   copy it from the attached template.
'
Dim ThisStyle As String
    
    On Error GoTo -1: On Error GoTo CopyTheSTyle
    
    ThisStyle = ActiveDocument.Styles(StyleName).Font.Name
    GoTo EndOfSub
    
CopyTheSTyle:
    On Error GoTo -1: On Error GoTo EndOfSub
    
    Application.OrganizerCopy _
        Source:=ActiveDocument.AttachedTemplate.FullName, _
        Destination:=ActiveDocument.FullName, _
        Name:=StyleName, _
        Object:=wdOrganizerObjectStyles
EndOfSub:
    On Error GoTo 0
End Sub
Sub CreateIDFooters()
    SetDefaultPaperSize
    MoveChapterAfterIDLines     ' unless done already
    
    CreateIDFootersInternal _
        IncludeTable:=True, MakeSectionBreaks:=True
    
    ActiveWindow.ActivePane.View.Type = PageOrPrintView
    ActiveWindow.ActivePane.View.Zoom.PageFit = wdPageFitBestFit
    SetStandardFindOptions
End Sub
Sub CreateIDFootersInternal(IncludeTable As Boolean, MakeSectionBreaks As Boolean)
' Create ID Footers macro
' Creates running footers which show the Identification field
' B Wussow PNG Feb96, mod J Henderson PNG May05

    Dim rIdent As Range
    Dim FooterStart As Long
    
    RemoveFrontMatter           ' unless done already
    RemoveSectionBreaks
    Selection.HomeKey Unit:=wdStory
    ActiveDocument.Styles("_Footer").Font.Italic = True
    
    HideIDetc
    
    ' Mark the Identification field with a bookmark
    WordBasic.ShowAll 1
    WordBasic.StartOfDocument
    Selection.Find.Style = ActiveDocument.Styles("id - File - Identification")
    WordBasic.EditFind Find:="", Direction:=0, MatchCase:=1, WholeWord:=0, _
        PatternMatch:=0, SoundsLike:=0, Format:=1, Wrap:=2
    WordBasic.CharLeft 1, 1     'must leave out the para mark
    WordBasic.EditBookmark Name:="Ident", SortBy:=0, Add:=1
    WordBasic.ShowAll 0
    
    ' point to the end of the document
    '
    Selection.EndKey Unit:=wdStory
    Selection.TypeParagraph
    FooterStart = Selection.Start
    
    On Error GoTo -1:  On Error GoTo error1
    
    ' Make sure there are enough pages in this section
    ' so we can make the Footers
    '
    Selection.InsertBreak Type:=wdPageBreak
    Selection.InsertBreak Type:=wdPageBreak
    
    ' Make sure each Footer contains only one line
    '
    Selection.Sections(1).Footers(wdHeaderFooterFirstPage).Range.Text = "first"
    Selection.Sections(1).Footers(wdHeaderFooterEvenPages).Range.Text = "even"
    Selection.Sections(1).Footers(wdHeaderFooterPrimary).Range.Text = "odd"

    ' Make sure we can view all footers.
    '
    With ActiveDocument.PageSetup
        .OddAndEvenPagesHeaderFooter = True
        .DifferentFirstPageHeaderFooter = True
    End With
    
    CreateIDFooterText wdSeekFirstPageFooter, wdSeekPrimaryFooter, IncludeTable
    ' CreateIDFooterText wdSeekPrimaryFooter, wdSeekFirstPageFooter, IncludeTable
    ' CreateIDFooterText wdSeekEvenPagesFooter, wdSeekPrimaryFooter, IncludeTable
    Selection.Sections(1).Footers(wdHeaderFooterFirstPage).Range.Select
    Selection.Sections(1).Footers(wdHeaderFooterFirstPage).Range.Copy
    Selection.Sections(1).Footers(wdHeaderFooterEvenPages).Range.Select
    Selection.Style = ActiveDocument.Styles("_Footer")
    Selection.Paste
    Selection.Sections(1).Footers(wdHeaderFooterEvenPages).Range.Select
    
    ' Remove unwanted paragraph mark
    '
    Selection.MoveRight Unit:=wdCharacter, Count:=1
    Selection.Delete Unit:=wdCharacter, Count:=1
    
    Selection.Sections(1).Footers(wdHeaderFooterPrimary).Range.Select
    Selection.Style = ActiveDocument.Styles("_Footer")
    Selection.Paste
    Selection.Sections(1).Footers(wdHeaderFooterPrimary).Range.Select
    Selection.MoveRight Unit:=wdCharacter, Count:=1
    Selection.Delete Unit:=wdCharacter, Count:=1
    
    With ActiveWindow
        If .Panes.Count > 1 Then .ActivePane.Close
    End With

    GoTo end1

error1:
    MsgBox "Error happened making page footers: " & Err.Description & vbCr & _
        "We'll continue anyway."
    On Error GoTo 0
    ' fallthrough to remove extra pages added to this section.
    '
end1:
    On Error GoTo 0
    ' Remove the things we added
    '
    Selection.EndKey Unit:=wdStory
    Selection.Start = FooterStart
    Selection.Delete
    Selection.EndKey Unit:=wdLine, Extend:=wdExtend
    Selection.Delete Unit:=wdCharacter, Count:=1
    
    WordBasic.ToolsOptionsView Hidden:=0, ShowAll:=0, FieldCodes:=0
    Selection.HomeKey Unit:=wdStory
        
    With ActiveDocument.PageSetup
        .OddAndEvenPagesHeaderFooter = False
        .DifferentFirstPageHeaderFooter = True
        
        If IncludeTable = True Then
            .MirrorMargins = False
        End If
    End With
    
    If MakeSectionBreaks Then
        If VariableGet("NrColumns") = "2" Then
            AddSectionBreaks SeparateIntro:=True
            StandardizeSections MakingNrColumns:="2"
        Else
            AddSectionBreaks
            StandardizeSections MakingNrColumns:="1"
        End If
    End If
End Sub
Sub CreateIDFooterText( _
        ThisFooter As Integer, _
        OtherFooter As Integer, _
        IncludeTable As Boolean)
    
    Dim WhenExported As String
    Dim rFooter As Range
    
    If IncludeTable Then
        ActiveDocument.PageSetup.BottomMargin = _
            InchesToPoints(DefaultBottomMargin) + 40    ' points
    Else
        ActiveDocument.PageSetup.BottomMargin = InchesToPoints(DefaultBottomMargin)
    End If
    
    ' Set the rFooter variable to the first page footer,
    ' and make an appropriate footer in it.
    '
    Set rFooter = Selection.Sections(1).Footers(wdHeaderFooterFirstPage).Range
    rFooter.Delete
    
    ' Insert a string of characters which will become fields later:
    '
    rFooter.Text = _
        "{ STYLEREF " & Chr(34) & "id - File - Identification" & Chr(34) & " \* MERGEFORMAT }"
    
    If VariableGet("StatusCode") = "done" Then
        rFooter.Text = rFooter.Text & _
            " { STYLEREF " & Chr(34) & "rem - File - Status" & Chr(34) & " \* MERGEFORMAT }"
    End If
    
    rFooter.Text = rFooter.Text & ", exported: "
        
    WhenExported = VariableGet("WhenExported")
    
    If WhenExported = "" Then
        rFooter.InsertAfter "{ SAVEDATE \@ " & Chr(34) & "d-MMM-yyyy at HH:mm" & Chr(34)
    Else
        rFooter.InsertAfter WhenExported
    End If
        
    rFooter.InsertAfter vbTab & "p. { Page \* MERGEFORMAT } "
    rFooter.Select
    Selection.Style = ActiveDocument.Styles("_Footer")
    
    ' Now convert these {...} strings into fields:
    '
    ConvertTextIntoFields NrFields:=2

    If IncludeTable Then
        rFooter.Select
        Selection.Collapse
        AddCheckedByTable
    End If
End Sub
Sub DecreaseRightMargin()
    ' With ActiveDocument.PageSetup
    Dim RightMargin As Single
    
    ' in case different sections have different margins,
    '   get the margin of the section where the selection is.
    '
    With Selection.Sections(1).PageSetup
        RightMargin = .RightMargin
    End With
    
    With ActiveDocument.PageSetup
        If RightMargin > InchesToPoints(1.2) Then
            .RightMargin = RightMargin - InchesToPoints(1#)
        End If
    End With
        
    If Not FormattingInProgress Then
        OptionalLineBreaks
    End If
End Sub
Function DefaultSettings()
    ' Some styles have been added to the template lately.
    ' In case this file was exported after these styles were added,
    ' make sure they are in the document now.
    '
    CopyStyle "_GlossaryHeadWord"
    CopyStyle "_ca"
    CopyStyle "_Caption"
    CopyStyle "_CheckingDetails"
    CopyStyle "_CurrentChapter"
    CopyStyle "_Chapter_Drop"
    CopyStyle "_Footnote_Reference"
    CopyStyle "_Paragraph_Centred"
    CopyStyle "_rq_Para"
    CopyStyle "_SpaceAbove"
    CopyStyle "_Spread_items"
    CopyStyle "_sts - Status"
    CopyStyle "rem - File - Status"
    CopyStyle "_OptionalLineBreak"
    
    If SetPathVariables <> True Then
        DefaultSettings = False
        Exit Function
    End If
    
    ' Make sure the language data for this file have been set.
    '
    InitAfterAttachingTemplate
    
    DisableSmartCutAndPaste "disable"
    FootnotesRestartingEachPage    ' make footnote references restart at 'a' each page, if requested
    
    If VariableGet("DefaultSettings") = "done" Then
        DefaultSettings = True
        Exit Function
    End If
    
    FormatStatusItems
    CheckForIdIncludingVerseNumbers     ' Pt7 beta causes this problem.
    ' CheckForChapterNumberRunningTooFar
    NoAutoStyles

    If Val(Application.Version) >= WdXP Then
        NoAutoCanvas
    End If
    
    If VariableGet("WhenExported") = "" Then
        If VariableGet("NoLanguageSet") = "True" Then GoTo EndSub
    End If
    
    ActiveWindow.ActivePane.View.Type = wdNormalView
    
    ' Change " -- " to emdash, "!$" or "~" to no-break space
    ' and hyphens between numbers, as in John 1:1B-4 or \v 7-9
    '
    ReplaceAlmostEverywhere iSpecialCharacters
    
    If VariableGet("FixedSpacesOnVerseNumbers") <> "done" Then
        DeleteParaInitialSpaces
        AddFixedSpaces2VerseNumbers
        AddChapterCodesToVerseNumbers
        FootnotesNoSpacesBefore
        ChapterLabels
        VariableSet "FixedSpacesOnVerseNumbers", "done"
    End If
    
    MoveChapterAfterIDLines     ' unless done already
    Format_rq_items
    ActiveWindow.ActivePane.View.Zoom.PageFit = wdPageFitBestFit
    
    SetStandardFindOptions
    FormatGlossaryEntries
    Table_ConvertRunsOfTableRows
    SetDefaultView
EndSub:
    VariableSet "DefaultSettings", "done"
    DefaultSettings = True
End Function
Sub DeleteDocumentVariables(Optional Confirm As Variant)
    Dim Msg As String
    Dim Style As Long
    Dim Title As String
    Dim Result As Integer
    Dim myvar As Variable
    
    Msg = "Are you sure you want to delete all the variables in " & ActiveDocument.Name & "?"
    Style = vbYesNo + vbQuestion
    Title = "Delete Variables in Active Document"
    
    If IsMissing(Confirm) Then
        Result = MsgBox(Msg, Style, Title)
        If Result = vbYes Then
            GoTo Delete
        Else: Exit Sub
        End If
    End If
Delete:
        For Each myvar In ActiveDocument.Variables
            myvar.Delete
        Next myvar
End Sub
Sub DeleteParaInitialSpaces()
'
' For each paragraph,
'   while the first character is a space
'       delete it
'
    Dim para As Paragraph
    
    For Each para In ActiveDocument.Paragraphs
        Do While para.Range.Characters(1) = " "
            para.Range.Characters(1).Delete
        Loop
    Next para
End Sub
Sub DeleteTablesInFooters()
    ZoomSet
    CreateIDFootersInternal _
        IncludeTable:=False, MakeSectionBreaks:=True
    SetDefaultView
End Sub
Sub DirectFormattingCludge()
'
' When complex text comes out of Paratext without a suitable font applied,
' Word seems to apply a suitable one with direct formatting,
' which stops the font and size macros from having any effect.
' This subroutine finds the font of the first blank line in the file and reports it.
'
Dim ProjectFont As String
Dim IsComplex As Boolean
Dim ComplexFontName As String
Dim HasInitialBlankParagraph As Boolean

    IsComplex = False
    ComplexFontName = ""

    Selection.HomeKey Unit:=wdStory
    If Selection.Characters(1) = vbCr _
    And Selection.Range.Style = "Normal" Then
        HasInitialBlankParagraph = True
        ProjectFont = Selection.Characters(1).Font.Name
        If Application.Version >= Wd2000 Then
            ' ask if Selection.Characters(1).Font.NameBi <> ""
            IsComplex = VBA9plus.ScriptIsComplex(ComplexFontName)
        End If
    End If
    MsgBox "ProjectFont is " & ProjectFont & vbCrLf _
        & "\p font is " _
        & ActiveDocument.Styles("_Vernacular_Base").Font.Name
    
    ' if direct formatting has not been removed yet,
    '   remove it
    '
    If VariableGet("DirectFormatting") = "" Then
        ReplaceAlmostEverywhere iResetFont  ' remove direct formatting
        VariableSet "DirectFormatting", "removed"
        
        If HasInitialBlankParagraph Then
            Selection.HomeKey Unit:=wdStory
            Selection.Paragraphs(1).Range.Delete
        End If
    End If
End Sub
Sub DisableSmartCutAndPaste(Optional TurnOff As Variant)
'
' Macro recorded 1-Dec-2005 by Jim Henderson
' If argument is given, apply it to turn smart cut & paste on or off.
' Remember original in global variable.
' If no argument, restore to user's preference.
'
    If IsMissing(TurnOff) Then   ' restore user's preference
        If SmartCutAndPasteUserPreference <> "" Then
            Options.SmartCutPaste = _
                SmartCutAndPasteUserPreference
        End If
    Else        ' remember user's preference & set to argument
        If SmartCutAndPasteUserPreference = "" Then
            SmartCutAndPasteUserPreference = _
                Options.SmartCutPaste
        End If
    
        Options.SmartCutPaste = False
    End If
End Sub
Sub EditMyCheckingTable()
' Work out a name for the checking table for this language/project
'
' If no such file exists already,
'   copy the default one to that name.
' endif
'
' Open the file for this language for editing.
'
    Dim CheckingTable As String
    Dim LanguageNumber As String
    Dim fs As Object
    Dim Path As String
    Dim StandardTable As String
    Dim CustomTable As String
    Dim FileToEdit As String
    
    If UseUserTemplates Then
        If CheckPTPath = "bad" Then
            Lib1.EndMacro
        End If
        Path = PtSettingsDir
    Else
        Path = ActiveDocument.AttachedTemplate.Path & Application.PathSeparator
    End If
    
    StandardTable = Path & "Checking Table.doc"
    CheckingTable = VariableGet("CheckingTable")

    Set fs = CreateObject("Scripting.FileSystemObject")
    
    If CheckingTable = "" _
    Or CheckingTable = " " Then
        MakeCheckingTableName CheckingTable
        
        VariableSet "CheckingTable", CheckingTable
     
        ' Write that variable in the Ini file for that language number.
        '
        If IniFile = "" Then SetIniFile
        LanguageNumber = VariableGet("LanguageNumber")
        
        System.PrivateProfileString( _
            FileName:=IniFile, _
            Section:=LanguageNumber, _
            Key:="CheckingTable") = CheckingTable
    End If
        
    ' If that file doesn't exist in the Pt settings folder,
    '    copy "Checking table.doc" to a file by that name.
    '
    CustomTable = Path & CheckingTable & ".doc"
    
    If fs.FileExists(CustomTable) <> True Then
        fs.CopyFile StandardTable, CustomTable
    End If
        
    FileToEdit = CustomTable
    
    'open Checking Table.doc for editing, if they've asked
    '
    If fs.FileExists(FileToEdit) <> True Then
        GoTo MissingDoc
    End If
    
    If MsgBox("The file" & vbCr & _
        vbTab & "'" & CheckingTable & ".doc'" & vbCr & _
        "will open for you to edit." & vbCrLf & vbCrLf & _
         "If your text makes the table box bigger, you may delete some underline characters; " & vbCrLf & _
         "for best results you should keep the boxes the same size. " & vbCrLf & vbCrLf & _
         "When you finish editing the table, " & vbCrLf & _
         "   *save* '" & CheckingTable & ".doc' and " & vbCrLf & _
         "   and then *close* it. " & vbCrLf & vbCrLf & _
         "Then start the 'Scripture--1. Format for Proof printouts' job again.", vbOKCancel) _
         = vbCancel Then
            Exit Sub
    Else
      Documents.Open FileName:=FileToEdit
      CancelFormatting = True
      Exit Sub
    End If
   
MissingDoc:
   MsgBox "I'm sorry, I can't find 'Checking Table.doc' in your Silas folder." & _
      "No problem, though, you may continue with your Proof printout."
End Sub
Sub EnableSmartQuotes(Optional TurnOn As Variant)
'
' Macro recorded 10/16/2005 by Jim Henderson
' If argument is given, apply it to turn smart quotes on or off.
' Remember original in global variable.
' If no argument, restore to user's preference.
'
    If IsMissing(TurnOn) Then   ' restore user's preference
        If SmartQuotesUserPreference <> "" Then
            Options.AutoFormatAsYouTypeReplaceQuotes _
                = SmartQuotesUserPreference
        End If
    Else        ' remember user's preference & set to argument
        If SmartQuotesUserPreference = "" Then
            SmartQuotesUserPreference = _
                Options.AutoFormatAsYouTypeReplaceQuotes
        End If
    
        With Options
            .AutoFormatAsYouTypeReplaceQuotes = TurnOn
        End With
    End If
End Sub
Sub ExportAllModules()
    Dim strPath As String
    Dim vbc As Object
    With Application.FileDialog(msoFileDialogFolderPicker)
        If .Show Then
            strPath = .SelectedItems(1)
            If Right(strPath, 1) <> "\" Then
                strPath = strPath & "\"
            End If
            For Each vbc In ActiveDocument.VBProject.VBComponents
                Select Case vbc.Type
                    Case 1  'vbext_ct_StdModule
                      vbc.Export strPath & vbc.Name & ".txt"
                    Case 100    ' vbext_ct_Document, vbext_ct_ClassModule
                      ' ThisDocument and class modules
                      vbc.Export strPath & vbc.Name & ".cls"
                    Case 3  ' vbext_ct_MSForm
                      vbc.Export strPath & vbc.Name & ".frm"
                    Case Else
                      vbc.Export strPath & vbc.Name
                    End Select
                ' vbc.Export strPath & vbc.Name & ".txt"
            Next vbc
        Else
            MsgBox "No folder specified...", vbExclamation
        End If
    End With
End Sub
Sub FindFigCodeText()
'
' FindFigCodeText Macro
' Macro recorded 6/8/2006 by Jim Henderson
'
    SetStandardFindOptions
    Selection.Find.ClearFormatting
    With Selection.Find
        .Wrap = wdFindAsk
    End With
    
    Selection.Find.Style = ActiveDocument.Styles( _
        "fig...fig* - Auxiliary - Figure/Illustration/Map")
    With Selection.Find
        .Format = True
    End With
    Selection.Find.Execute
End Sub
Sub FindNextChapter()
'
' FindNextChapter Macro
' Macro recorded 10/29/2006 by Jim Henderson
'
    SetStandardFindOptions
    
    Selection.Find.ClearFormatting
    Selection.Find.Style = ActiveDocument.Styles(StdChapterStyle)
    With Selection.Find
        .Text = ""
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindContinue
        .Format = True
    End With
    
    If Selection.Find.Execute Then
        If Not FindNextMessageShown Then
            MsgBox "Now that you have made a search, you can Find-Next" & vbCr & _
                    "by using the standard Word hotkey Shift-F4." & _
                    vbCr & vbCr & _
                    "(ie. hold down the Shift key and hit the key called F4.)"
            FindNextMessageShown = True
        End If
    End If
End Sub
Sub FixQuotes()
' Change angle brackets to smart quotes
' in the main text and in the footnotes.
'
   ReplaceAlmostEverywhere iFixQuotes
   ActiveWindow.View.Type = PageOrPrintView
End Sub
Sub FixQuotesInternal(ByRef rngStory As Range)
   ' Now change angle brackets to curly quotes.
   ' Also does em-dashes.
   '
Dim rngThisStory As Range
Dim Found As Boolean

   SetStandardFindOptions
   Application.StatusBar = "               Fixing quote characters --  please wait."
   Application.ScreenUpdating = False
   
   Set rngThisStory = rngStory.Duplicate
   rngThisStory.Select

   EnableSmartQuotes False     ' Turn off smart quote processing

   With Selection.Find
       .Text = "< <"
       .Replacement.Text = "<" & Chr(160) & "<"
       .Wrap = wdFindStop
   End With

   ' Shoddy Word 2003 crashes if you do wdReplaceAll in the footnotes.
   ' This is fixed in SP2, but that's a 477 Meg download,
   ' so we'll do it the slow way, looping for each change.
   '
   On Error GoTo e1A0

   Do
       Found = Selection.Find.Execute

       If Found = True Then
           Selection.Text = Selection.Find.Replacement.Text
           Selection.Start = Selection.End
       End If
   Loop While Found = True

e1A0:
   
   rngThisStory.Select
   
   With Selection.Find
       .Text = "> >"
       .Replacement.Text = ">" & Chr(160) & ">"
       .Wrap = wdFindStop
   End With

   On Error GoTo e1A1

   rngThisStory.Select
   
   Do
       Found = Selection.Find.Execute

       If Found = True Then
           Selection.Text = Selection.Find.Replacement.Text
           Selection.Start = Selection.End
       End If
   Loop While Found = True

e1A1:
   rngThisStory.Select
   
   With Selection.Find
       .Text = "<<<"
       .Replacement.Text = ChrW(8220) & Chr(160) & Chr(145)
       .Wrap = wdFindStop
       .Format = False
   End With

   rngThisStory.Select
   
   On Error GoTo e1A

   Do
       Found = Selection.Find.Execute

       If Found = True Then
           Selection.Text = Selection.Find.Replacement.Text
           Selection.Start = Selection.End
       End If
   Loop While Found = True

e1A:
   rngThisStory.Select

   With Selection.Find
       .Text = "<=>"
       .Replacement.Text = Chr(243)
   End With

   On Error GoTo e1B

   Do
       Found = Selection.Find.Execute

       If Found = True Then
           Selection.Text = Selection.Find.Replacement.Text
           Selection.Font.Name = "Wingdings"
           Selection.Start = Selection.End
       End If
   Loop While Found = True

e1B:

   rngThisStory.Select

   With Selection.Find
       .Text = "<<"
       .Replacement.Text = ChrW(8220)
   End With

   On Error GoTo e2

   Do
       Found = Selection.Find.Execute

       If Found = True Then
           Selection.Text = Selection.Find.Replacement.Text
           Selection.Start = Selection.End
       End If
   Loop While Found = True

e2:
   rngThisStory.Select

   With Selection.Find
       .Text = "<"
       .Replacement.Text = Chr(145)
   End With

   On Error GoTo e3

   Do
       Found = Selection.Find.Execute

       If Found = True Then
           Selection.Text = Selection.Find.Replacement.Text
           Selection.Start = Selection.End
       End If
   Loop While Found = True

e3:
   ' For closing quotes within quotes, ">>>" is the ordinary one.
   ' How about where a quote character
   ' comes at the end of a word, such as in Hebrew for "He created"
   ' <<bara'>>
   '
   rngThisStory.Select

   With Selection.Find
       .Text = "[\'\>]\>\>"
       .Replacement.Text = Chr(146) & Chr(160) & ChrW(8221)
       .MatchWildcards = True      ' Don't forget to turn wildcards off next search!
   End With

   On Error GoTo e4

   Do
       Found = Selection.Find.Execute

       If Found = True Then
           Selection.Text = Selection.Find.Replacement.Text
           Selection.Start = Selection.End
       End If
   Loop While Found = True

e4:
   rngThisStory.Select

   With Selection.Find
       .Text = ">>"
       .Replacement.Text = ChrW(8221)
       .MatchWildcards = False
   End With

   On Error GoTo e5

   Do
       Found = Selection.Find.Execute

       If Found = True Then
           Selection.Text = Selection.Find.Replacement.Text
           Selection.Start = Selection.End
       End If
   Loop While Found = True

e5:
   rngThisStory.Select

   With Selection.Find
       .Text = ">"
       .Replacement.Text = Chr(146)
   End With

   On Error GoTo e6

   Do
       Found = Selection.Find.Execute

       If Found = True Then
           Selection.Text = Selection.Find.Replacement.Text
           Selection.Start = Selection.End
       End If
   Loop While Found = True

e6:
MissingStoryRange:
   On Error GoTo 0
e7:
    ' Make the fixed spaces between curly quotes smaller.
    '
    ' Shoddy Wd 2010 crashes if we do this in footnotes in Normal view
    '
    If Val(Application.Version) >= Wd2010 Then
        ' ActiveWindow.ActivePane.View.Type = PageOrPrintView
        If ActiveWindow.View.SplitSpecial = wdPaneNone Then
            ActiveWindow.ActivePane.View.Type = PageOrPrintView
        Else
            ActiveWindow.View.Type = PageOrPrintView
        End If
    End If
    
    ' MsgBox "I'm about to make the fixed spaces between sloping quotes smaller."
    
    SetStandardFindOptions
    Selection.Find.ClearFormatting
    Selection.Find.Replacement.ClearFormatting
    rngThisStory.Select
    Selection.Collapse
    With Selection.Find
        ' << < or >> >
        ' .Text = ChrW(8220) & ChrW(160) & ChrW(145)
        .Text = """" & ChrW(160) & "'"
        .Wrap = wdFindStop
    End With
    
    Do While Selection.Find.Execute
        Selection.Characters(2).Font.Scaling = SpaceBetweenQuotesScaling
        Selection.Start = Selection.End
    Loop
    
    rngThisStory.Select
    Selection.Collapse
    ' Selection.HomeKey Unit:=wdStory
    
    With Selection.Find
        ' < << or > >>
        .Text = "'" & ChrW(160) & """"
    End With
    
    Do While Selection.Find.Execute
        Selection.Characters(2).Font.Scaling = SpaceBetweenQuotesScaling
        Selection.Start = Selection.End
    Loop
    
    If Val(Application.Version) >= Wd2010 Then
        If ActiveWindow.View.SplitSpecial = wdPaneNone Then
            ActiveWindow.ActivePane.View.Type = wdNormalView
        Else
            ActiveWindow.View.Type = wdNormalView
        End If
    End If
   
   EnableSmartQuotes           ' Restore user's preference.
   SetStandardFindOptions
End Sub
Sub FixSpecialCharacters(ByRef rngStory As Range)
' Macro to go through a storyrange and convert
'   " -- " to emdash and "!$" or "~" to no-break space
'   2009 THIN-SPACE to 0020 of small size and
'   202F NARROW-NO-BREAK-SPACE to 00A0 of small size
'
Dim rngThisStory As Range

    On Error GoTo MissingStoryRange
    
    Set rngThisStory = rngStory.Duplicate
    rngThisStory.Select
    
    Application.StatusBar = "               Fixing special characters --  please wait."
    Application.ScreenUpdating = False
    
    ' Now change " -- " into em-dash with a space each side.
    '
    With Selection.Find
        .Text = " -- "
        .Replacement.Text = " " & ChrW(8212) & " "
    End With

    On Error GoTo e7
    Selection.Find.Execute Replace:=wdReplaceAll

e7:
    ' Change !$ or ~ to fixed space
    '
    SetStandardFindOptions
    rngThisStory.Select
    Selection.Find.ClearFormatting
    Selection.Find.Replacement.ClearFormatting
    With Selection.Find
        .Text = "!$"
        .Replacement.Text = "^s"
        .Forward = True
        .Wrap = wdFindContinue
        .Format = False
    End With
    Selection.Find.Execute Replace:=wdReplaceAll
     
    rngThisStory.Select
    With Selection.Find
        .Text = "~"
        .Replacement.Text = "^s"
    End With
    Selection.Find.Execute Replace:=wdReplaceAll
    
    ' Now any hyphen between verse numbers becomes a non-breaking hyphen.
    '
    SetStandardFindOptions
    rngThisStory.Select
    Selection.Find.ClearFormatting
    Selection.Find.Replacement.ClearFormatting
    With Selection.Find
        .Text = "([0-9a-dA-D])-([0-9])"
        .Replacement.Text = "\1^~\2"
        .MatchWildcards = True
        .Forward = True
        .Wrap = wdFindContinue
        .Format = False
    End With
    Selection.Find.Execute Replace:=wdReplaceAll

    ' Shoddy Wd 2010 crashes if we do this in footnotes in Normal view
    '
    If Val(Application.Version) >= Wd2010 Then
        ActiveWindow.ActivePane.View.Type = PageOrPrintView
    End If
    
'   2009 THIN-SPACE to 0020 of small size and
    SetStandardFindOptions
    rngThisStory.Select
    Selection.Find.ClearFormatting
    Selection.Find.Replacement.ClearFormatting

    With Selection.Find
        .Text = ChrW(&H2009&)
        .Replacement.Text = " "
    End With

    Do While Selection.Find.Execute(Replace:=wdReplaceOne)
        Selection.Characters(1).Font.Scaling = ThinSpaceScaling
        Selection.Start = Selection.End
    Loop

'   202F NARROW-NO-BREAK-SPACE to 00A0 of small size
    SetStandardFindOptions
    rngThisStory.Select
    Selection.Find.ClearFormatting
    Selection.Find.Replacement.ClearFormatting

    With Selection.Find
        .Text = ChrW(&H202F&)
        .Replacement.Text = "^s"
    End With

    Do While Selection.Find.Execute(Replace:=wdReplaceOne)
        Selection.Characters(1).Font.Scaling = ThinSpaceScaling
        Selection.Start = Selection.End
    Loop
    
    Selection.WholeStory
    If ActiveWindow.View.SplitSpecial <> wdPaneNone Then
        If ActiveWindow.Panes.Count > 1 Then ActiveWindow.Panes(2).Close
    End If
MissingStoryRange:
End Sub
Sub FootnoteParaHangSet(ByVal ForceStyleSetting As Boolean)
    ' To set a hanging indent on footnote, endnote and Xref paragraphs,
    ' after the footnote reference letter and possible space.
    '
    ' Pt6 produces <ref-letter><footnote-text>
    ' Pt7 produces <space><ref-letter><footnote-text>
    ' SILAS makes  <ref-letter><space><footnote-text>
    '
    Dim Indent As Single  ' value in points for hanging indent
    Dim ThisPosition As Long
    Dim tmpIndent As Single
    
    If ActiveDocument.Footnotes.Count = 0 Then
        Exit Sub
    End If
    
    With ActiveWindow
        If .Panes.Count > 1 Then .ActivePane.Close
    End With
    
    Selection.HomeKey Unit:=wdStory
    
    If ActiveWindow.View.SplitSpecial = wdPaneNone Then
        ActiveWindow.ActivePane.View.Type = wdNormalView
    Else
        ActiveWindow.View.Type = wdNormalView
    End If
    
    ActiveWindow.View.SplitSpecial = wdPaneFootnotes
    Application.StatusBar = "       Adjusting hanging indent on footnotes --  please wait."
    
    ' Now cursor is positioned before the first footnote.
    ' Remove any leading spaces and step over reference letter.
    ' to standardize first footnote to <ref-letter(s)><space>
    '
    While Selection.Characters(1) = " "
        Selection.Delete
    Wend
    
    ThisPosition = Selection.Start
    
    While Selection.Characters(1).Font.Superscript
        Selection.Start = Selection.Start + 1
    Wend
    
    If Selection.Start > ThisPosition Then
        Selection.Start = ThisPosition
        Selection.Font.Reset
        Selection.Style = ActiveDocument.Styles("_Footnote_Reference")
    End If
    
    Selection.Start = Selection.End
    
    ' Get amount of space to leave for wide reference letters like "w"
    '
    tmpIndent = Selection.Information(wdHorizontalPositionRelativeToTextBoundary) * 0.3
    
    If Selection.Characters(1) = " " Then
        Selection.End = Selection.Start + 1
    ElseIf Selection.Characters(1) = vbTab Then
        Selection.Characters(1) = " "
        Selection.End = Selection.Start + 1
    Else
        Selection.InsertBefore " "
    End If
    
    Selection.Font.Reset
    Selection.Start = Selection.End
    
    ' Now get the amount of space used by <ref-letter(s)><space>
    ' and make hanging indent on each of the 3 footnote styles.
    '
    Indent = Selection.Information(wdHorizontalPositionRelativeToTextBoundary)
    Indent = Indent + tmpIndent
    FootnoteParaHangSetInternal _
        StyleName:="f...f* - Footnote", _
        Indent:=Indent
    
    FootnoteParaHangSetInternal _
        StyleName:="fe...fe* - Endnote", _
        Indent:=Indent
    
    FootnoteParaHangSetInternal _
        StyleName:="x...x* - Cross Reference", _
        Indent:=Indent
    
    If Selection.Characters(1) <> vbTab Then Selection.InsertBefore vbTab
    If VariableGet("FootnotesFormatted") = "yes" _
    And ForceStyleSetting <> True Then
        GoTo CloseFootnotePane:
    End If
    
    ' Now change following footnotes
    ' from <ref-letter>(<space>)
    ' into <ref-letter><tab>
    ' so hanging lines will line up properly.
    '
    Application.StatusBar = "    Formatting footnote references in footnotes -- please wait."
    Selection.HomeKey Unit:=wdLine
    Selection.MoveDown Unit:=wdParagraph, Count:=1
    
    Do
        ' Remove any leading spaces and step over reference letter(s).
        ' to standardize each footnote to <ref-letter(s)><tab>
        '
        While Selection.Characters(1) = " "
            Selection.Delete
        Wend
        
        ThisPosition = Selection.Start
        
        While Selection.Characters(1).Font.Superscript _
        And Selection.Characters(1) <> " " _
        And Selection.Characters(1) <> vbTab
            Selection.Start = Selection.Start + 1
        Wend
        
        ' Select footnote reference letter.
        '
        If Selection.End > ThisPosition Then
            Selection.Start = ThisPosition
            Selection.Font.Reset
            Selection.Style = ActiveDocument.Styles("_Footnote_Reference")
        
            Selection.Start = Selection.End
        
            If Selection.Characters(1) = " " Then
                Selection.Characters(1).Delete
            End If
            
            If Selection.Characters(1) <> vbTab Then
                Selection.InsertBefore vbTab
                Selection.Font.Reset
            End If
        End If
    
        Selection.Start = Selection.End
        
        While Selection.Characters(1) = " "
            Selection.Characters(1).Delete
        Wend
        
        ThisPosition = Selection.Start
        Selection.MoveDown Unit:=wdParagraph, Count:=1
    Loop While Selection.Start > ThisPosition
    VariableSet "FootnotesFormatted", "yes"
    ' fallthrough to CloseFootnotePane
CloseFootnotePane:
    With ActiveWindow
        If .Panes.Count > 1 Then .ActivePane.Close
    End With

    ActiveWindow.ActivePane.View.Type = PageOrPrintView
    Application.StatusBar = "                                                            ."
End Sub
Private Sub FootnoteParaHangSetInternal(ByVal StyleName As String, ByVal Indent As Single)
    ' Pass Indent in points
    '
    With ActiveDocument.Styles(StyleName).ParagraphFormat
        .LeftIndent = Indent
        .FirstLineIndent = 0 - Indent
    End With
End Sub
Sub FootnotesNoSpacesBefore()
    Dim NrFootnotes As Long
    Dim i As Long
    Dim iRefCode As Integer
    Dim Fnote As Footnote
    
    Selection.HomeKey Unit:=wdStory
    
    ' For each footnote marker,
    '   apply the style "_Footnote_Reference" to the reference letter
    '   remove any spaces before it.
    '
    NrFootnotes = ActiveDocument.Footnotes.Count
    
    If NrFootnotes > 0 Then
        ActiveDocument.ActiveWindow.View.Type = wdNormalView
        Application.StatusBar = "    Removing spaces before footnote references -- please wait."
        Application.ScreenUpdating = False
        
        For i = 1 To NrFootnotes
            Set Fnote = ActiveDocument.Footnotes(i)
            On Error GoTo -1: On Error GoTo InvalidFootnote
            
            ' Apply style to each footnote marker
            '
            Fnote.Reference.Select
            Selection.Font.Reset
            Selection.Style = "_Footnote_Reference"
            Selection.Collapse
            
            ' Now remove any preceding spaces
            '
            Do While Selection.Start > 0
                Selection.Start = Selection.Start - 1
                If Selection.Text = " " Then
                    Selection.Delete
                Else
                    Exit Do
                End If
            Loop
InvalidFootnote:
        On Error GoTo 0
        Next i
        
        ' Now set hanging indent of footnote paragraphs
        '
        FootnoteParaHangSet ForceStyleSetting:=False
    End If
End Sub
Sub FootnotesRestartingEachPage()
    Dim NrFootnotes As Long
    Dim i As Long
    Dim iRefCode As Integer
    Dim Fnote As Footnote
    Dim ThisPosition As Long
    Dim Marker As String
    Dim ChangeFootnote As Boolean
    
    If VariableGet("RestartFootnoteRefs") <> "yes" Then Exit Sub
    
    NrFootnotes = ActiveDocument.Footnotes.Count
    
    If NrFootnotes = 0 Then
        Exit Sub
    End If
    
    ' Paratext 7 sometimes puts out a space before the footnote marker, in the footnote,
    ' so first we'll remove any of those that exist.
    '
    
    If ActiveWindow.View.SplitSpecial = wdPaneNone Then
        ActiveWindow.ActivePane.View.Type = wdNormalView
    Else
        ActiveWindow.View.Type = wdNormalView
    End If
'
'    ActiveWindow.View.SplitSpecial = wdPaneFootnotes
    
    ActiveDocument.StoryRanges(wdFootnotesStory).Select
    Selection.Collapse
    
    ' For each footnote paragraph,
    '   delete any initial space(s)
    '
    Application.StatusBar = "    Removing spaces before footnote references in footnotes -- please wait."
    
    Do
        While Selection.Characters(1) = " "
            Selection.Characters(1).Delete
        Wend
        
        ThisPosition = Selection.Start
        Selection.MoveDown Unit:=wdParagraph, Count:=1
    Loop While Selection.Start > ThisPosition
    
    With ActiveWindow
        If .Panes.Count > 1 Then .ActivePane.Close
    End With

    If ActiveWindow.View.SplitSpecial = wdPaneNone Then
        ActiveWindow.ActivePane.View.Type = PageOrPrintView
    Else
        ActiveWindow.View.Type = PageOrPrintView
    End If

    Selection.HomeKey Unit:=wdStory
    
    ' For each footnote marker,
    '   change the explicit number that came from Paratext to an automatic number
    '   (unless the user put a marker outside the range [a-z])
    '   apply the style "_Footnote_Reference" to the reference letter
    '
    ActiveDocument.ActiveWindow.View.Type = wdNormalView
    Application.StatusBar = "    Making footnote references restart at 'a' each page -- please wait."
    Application.ScreenUpdating = False
    
     With ActiveDocument.Footnotes
        .Location = wdBottomOfPage
        .StartingNumber = 1
        .NumberStyle = wdNoteNumberStyleLowercaseLetter
        .NumberingRule = wdRestartPage
    End With
   
    For i = 1 To NrFootnotes
        Set Fnote = ActiveDocument.Footnotes(i)
        On Error GoTo -1: On Error GoTo InvalidFootnote
        Fnote.Reference.Select
        iRefCode = Asc(Selection.Text)
        ChangeFootnote = False
        
        ' If reference character is in the range a-z,
        '   replace Paratext's explicit number with auto.
        '
        If iRefCode >= 97 And iRefCode <= 122 Then
            Marker = ""
            ChangeFootnote = True
        End If
        
        If iRefCode = Asc("*") Then
            Marker = "*"
            ChangeFootnote = True
        End If
        
        If ChangeFootnote Then
            Selection.Footnotes.Add Range:=Selection.Range, Reference:=Marker
            ActiveDocument.Footnotes(i).Reference.Select
        End If
        
        ' Apply style to each footnote marker
        '
        Selection.Font.Reset
        Selection.Style = "_Footnote_Reference"
InvalidFootnote:
    On Error GoTo 0
    Next i
    
    VariableSet "RestartFootnoteRefs", "done"
    
    With ActiveDocument.Footnotes
        .NumberingRule = wdRestartPage
    End With

    With ActiveWindow
        If .Panes.Count > 1 Then .ActivePane.Close
    End With

    ActiveWindow.ActivePane.View.Type = PageOrPrintView
    FootnoteParaHangSet ForceStyleSetting:=True ' Set hanging indent again, and set style on refs
End Sub
Sub FootnotesRestartingOff()
'
' Macro recorded 4/18/2009 by Jim Henderson
'
    With ActiveDocument.Footnotes
        .Location = wdBottomOfPage
'        .NumberingRule = wdRestartContinuous
        .NumberingRule = wdRestartSection
        .StartingNumber = 1
        .NumberStyle = wdNoteNumberStyleLowercaseLetter
    End With

'    With ActiveDocument.Range(Start:=ActiveDocument.Content.Start, End:= _
'        ActiveDocument.Content.End).FootnoteOptions
'        .Location = wdBottomOfPage
'        .NumberingRule = wdRestartContinuous
'        .StartingNumber = 1
'        .NumberStyle = wdNoteNumberStyleLowercaseLetter
'    End With
End Sub
Sub FormatForHalfPageBooklets()
    Dim IsDoubleColumn As Boolean
    Dim NrSections As Long
    Dim SectionNr As Long
    Dim rng As Range
    
    ' Make sure the language data for this file have been set.
    '
    InitAfterAttachingTemplate
    CopyStyle "_Header"
    CopyStyle "_Footer"
    
    ' Get the parameters from the user first.
    '
    CancelFormatting = False
    dbBookletStyle.Show
    If CancelFormatting Then Exit Sub
    
    FormattingInProgress = True
    FormattingDoneReport Starting:=True

    ' Now show the results of the selections, for approval.
    '
    Dim Answer As String
    Dim BookMsgCut As String
    Dim BookMsgMargins As String
    Dim BookMsgJustified As String
    Dim BookMsgPassage As String
    Dim BookMsgVillage As String
    Dim BookMsgPaperSize As String
    
    ' cut or folded
    Answer = VariableGet("BookletStyle")
    If Answer = "cut" Then
        BookMsgCut = "Your booklet will have wider left margins so you can cut the pages apart and staple them along the side."
    ElseIf VariableGet("BookletStyle") = "folded" Then
        BookMsgCut = "Your booklet will be set up to fold the sheets in half and staple in the fold."
    End If

    ' margin trim
    Answer = VariableGet("MarginTrim")
    If Answer = "yes" Then
        BookMsgMargins = "Margins will allow extra space so books can be trimmed after binding."
    Else
        BookMsgMargins = "Margins will not allow extra space for trimming the books."
    End If
    
    ' justified text
    Answer = VariableGet("Justified")
    If Answer = "yes" Then
        BookMsgJustified = "Right margin of the text will be justified (even/square)."
    Else:
        BookMsgJustified = "Right margin will not be justified (ends of lines will be uneven)."
    End If
    
    BookMsgPassage = "The Scripture reference for this booklet will be " & Chr(34) & VariableGet("ScripturePassage") & Chr(34)
    BookMsgVillage = "This line will be included on the title page: " & Chr(34) & "Printed at " & VariableGet("VillageName") & Chr(34)
    If VariableGet("DefaultPaperSize") = "Letter" Then BookMsgPaperSize = "The paper size for this booklet is half-Letter"
    If VariableGet("DefaultPaperSize") = "A4" Then BookMsgPaperSize = "The paper size for this booklet is A5"
    
    Dim Result As Integer
        
    CancelFormatting = False   'first reset variable
    Result = MsgBox( _
                Title:="Settings for Current Booklet", _
                Prompt:=BookMsgCut & vbCrLf & BookMsgMargins & vbCrLf & BookMsgJustified & vbCrLf & _
                    BookMsgPassage & vbCrLf & BookMsgVillage & vbCrLf & BookMsgPaperSize & vbCrLf & vbCrLf & _
                    "Are these settings correct for this booklet? If so, choose OK." & vbCrLf & _
                    "If not, choose Cancel. Then open the menu 'Scripture' and restart the job called " & _
                    vbCrLf & vbTab & Chr(34) & "2. Format for Half-page Booklets." & Chr(34), _
                Buttons:=vbOKCancel)
    
    If Result = vbCancel Then
        CancelFormatting = True
        Exit Sub  'return to FormatForHalfPageBooklets
    End If
    
    If DefaultSettings <> True Then Exit Sub
    
    CancelFormatting = False
    PageSetupHalfPageBooklets

    If CancelFormatting Then
       Exit Sub
    End If
    
    If VariableGet("NrColumns") = "2" Then
        IsDoubleColumn = True
        SingleOrDoubleColumn.MAIN
    End If
    
    MakeHeaders MakeSectionBreaks:=True
    NrSections = ActiveDocument.Sections.Count
    
    ActiveWindow.ActivePane.View.Type = wdNormalView
    HideIDetc
    ReplaceAlmostEverywhere iFixQuotes
    
    If VariableGet("NoBreakSpaces") = "yes" Then
        ReplaceAlmostEverywhere iHyphens2Spaces
    End If
    
    If VariableGet("Brackets2HalfBrackets") = "yes" Then
        ReplaceAlmostEverywhere iBrackets2HalfBrackets
    End If
    
    If VariableGet("DropCapChapterNumbers") = "yes" Then
        If VariableGet("ChapterStyle") <> "drop" Then
           DropCapChapters.MAIN
        End If
        
        If VariableGet("HideNumberForEachVerse1") = "yes" Then
            DropCapChapters.HideNumberForEachVerse1Now
        Else
            DropCapChapters.RevealNumberForEachVerse1Now
        End If
    End If
        
    MakeFrontPages
    SetDropCapSize
    
    If IsDoubleColumn = True Then
        SingleOrDoubleColumn.MAIN
    End If

    For SectionNr = 1 To NrSections
        Set rng = ActiveDocument.Sections(SectionNr).Footers(wdHeaderFooterFirstPage).Range
        rng.Text = vbTab
        rng.Collapse wdCollapseEnd
        rng.Fields.Add rng, wdFieldPage
        rng.Style = ActiveDocument.Styles("_Footer")
        
        ' Now clear the other footers in the section.
        '
        ActiveDocument.Sections(SectionNr).Footers(wdHeaderFooterEvenPages).Range.Text = ""
        ActiveDocument.Sections(SectionNr).Footers(wdHeaderFooterEvenPages).Range.Style = "_Footer"
        ActiveDocument.Sections(SectionNr).Footers(wdHeaderFooterPrimary).Range.Text = ""
        ActiveDocument.Sections(SectionNr).Footers(wdHeaderFooterPrimary).Range.Style = "_Footer"
    Next SectionNr
    
    ' Remove page number from bottom of title page
    '
    ActiveDocument.Sections(1).Footers(wdHeaderFooterFirstPage).Range.Text = ""
    
    SetStandardFindOptions
    DisableSmartCutAndPaste
    Selection.HomeKey Unit:=wdStory
    SetDefaultView
    
    With ActiveWindow.ActivePane.View.Zoom
        .PageColumns = 2
        .PageRows = 1
    End With
    
    OptionalLineBreaks
    FormattingInProgress = False

    ResetParaMarks
    
    ' Save the file with a name including language name, passage, date & time.
    '
    F_FileSaveAs
    FormattingDoneReport Starting:=False
End Sub
Sub FormatForProofPrintouts()
    Dim RtMargin As String, Backed As String, LineSp As String
    Dim CkgTable As Boolean
    Dim RemakeFrontMatter As Boolean
   
    ' Make sure the language data for this file have been set.
    '
    InitAfterAttachingTemplate
    CopyStyle "_Header"
    CopyStyle "_Footer"

 ' Set options for Proofs
   CancelFormatting = False
   EditCheckingTable = False
   dbProofPrintStyle.Show
   
   If EditCheckingTable Then
        EditMyCheckingTable
        Exit Sub
   End If
   
   If CancelFormatting Then Exit Sub

   FormattingInProgress = True
   FormattingDoneReport Starting:=True
   
   RtMargin = VariableGet("ProofRtMargin")
   Backed = VariableGet("ProofBacked")
   CkgTable = VariableGet("ProofCkgTable")
   LineSp = VariableGet("ProofLineSp")
   
   If DefaultSettings <> True Then Exit Sub
   
   SetDefaultPaperSize
   
   ' Make justification left,
   '   so multiple spaces show up in proof printouts.
   '
   With ActiveDocument.Styles("_BodyText_Base")
       .ParagraphFormat.Alignment = wdAlignParagraphLeft
   End With

   ActiveWindow.ActivePane.View.Type = wdNormalView

   If VariableGet("FrontMatter") = "done" Then
       RemakeFrontMatter = True
       Gen.RemoveFrontMatter
   End If

   MakeHeaders MakeSectionBreaks:=False

   With ActiveDocument.PageSetup
       .OddAndEvenPagesHeaderFooter = False
       .DifferentFirstPageHeaderFooter = True
       .MirrorMargins = False
   End With

   ActiveWindow.ActivePane.View.Type = wdNormalView

   If VariableGet("NoBreakHyphens") = "yes" Then
       ReplaceAlmostEverywhere iNoBreakHyphens
   End If

   If VariableGet("Brackets2HalfBrackets") = "yes" Then
       ReplaceAlmostEverywhere iBrackets2HalfBrackets
   End If

   If VariableGet("QuotesInProofPrintouts") = "yes" Then
       ReplaceAlmostEverywhere iFixQuotes
   End If

   Selection.HomeKey Unit:=wdStory
'   DoubleSpacingNew  Enter spacing setting here
   If LineSp = "single" Then
      SingleSpacingNew
   ElseIf LineSp = "oneandhalf" Then
      OneAndHalfSpacingNew
   Else: DoubleSpacingNew
   End If
   
   HideIDetc
   ActiveWindow.ActivePane.View.Zoom.PageFit = wdPageFitBestFit

    If VariableGet("DropCapChapterNumbers") = "yes" Then
        If VariableGet("ChapterStyle") <> "drop" Then
           DropCapChapters.MAIN
        End If
        
        If VariableGet("HideNumberForEachVerse1") = "yes" Then
            DropCapChapters.HideNumberForEachVerse1Now
        Else
            DropCapChapters.RevealNumberForEachVerse1Now
        End If
   End If

   CreateIDFootersInternal _
        IncludeTable:=CkgTable, MakeSectionBreaks:=True
   SetDropCapSize
   
   'rest of the options here
   If RtMargin = "wide" Then IncreaseRightMargin
   If RtMargin = "extrawide" Then
      IncreaseRightMargin
      IncreaseRightMargin
   End If
   
   If Backed = "yes" Then LayOutForDoubleSidedPrinting
   
   SetStandardFindOptions
   ActiveWindow.ActivePane.View.Type = PageOrPrintView
   ActiveWindow.ActivePane.View.Zoom.PageFit = wdPageFitFullPage
   DisableSmartCutAndPaste
   Selection.HomeKey Unit:=wdStory
EndSub:
   OptionalLineBreaks
   FormattingInProgress = False
   ResetParaMarks
   SetDefaultView
   FormattingDoneReport Starting:=False
End Sub
Sub FormatGlossaryEntries()
'
' FormatGlossaryEntries Macro
' Macro recorded 2/15/2006 by Jim Henderson
'
' Expects \id GLO or \glo code near the start of the glossary,
' and glossary entries to look like this:
' \p \k Angel\k*
' \li A supernatural being who tells God's messages to people or protects those who belong to God.
' \p \k Council\k*
' \li 1) A group of leaders who meet and make decisions for their people.
' \li 2) The Old Testament refers to God's council as a group of angels who meet and talk with God in heaven.
'
' Subroutine algorithm:
'
' Quit if this job has been done already.
'
' If there is any text in the  style "OBSOLETE glo Peripherals - Back Matter Glossary",
'   change that style to small and yellow
'   step down through paragraphs until non-glossary code found then goto NoMoreParagraphs
'
' For each \m or \p paragraph
'   format it as "_GlossaryHeadWord"
'   so it can have space before it and keep-with-next.
'
'   check the following paragraphs
'   and while they are \li style,
'       if the paragraph begins with something like 2)
'           put a tab in place of the space after the 2)
'       else
'           put a tab before the text of the paragraph.
'
' NoMoreParagraphs:
' Remember that this job has been done.
'
Dim FoundGlossary As Boolean
Dim FoundGloStyle As Boolean
Dim FoundIDGlossary As Boolean
Dim SpacePosition As Long
Dim rKeyWord As Range
Dim FoundKeyWord As Boolean
Dim ThisStyle As String

    If VariableGet("GlossaryEntriesFormatted") = "done" Then Exit Sub
    
    Selection.HomeKey Unit:=wdStory
    Application.StatusBar = "               Formatting the glossary -- please wait."
    Application.ScreenUpdating = False
    SetStandardFindOptions
    
    Selection.Find.ClearFormatting
    Selection.Find.Style = ActiveDocument.Styles( _
        "id - File - Identification")
    With Selection.Find
        .Text = "GLO "
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindStop
        .Format = True
    End With
    
    FoundIDGlossary = Selection.Find.Execute
    
    Selection.HomeKey Unit:=wdStory
    Selection.Find.ClearFormatting
    Selection.Find.Style = ActiveDocument.Styles( _
        "OBSOLETE glo Peripherals - Back Matter Glossary")
    With Selection.Find
        .Text = ""
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindStop
        .Format = True
    End With
    
    FoundGloStyle = Selection.Find.Execute
    
    If FoundGloStyle Or FoundIDGlossary Then FoundGlossary = True
    
    If FoundGlossary Then
        With ActiveDocument.Styles("OBSOLETE glo Peripherals - Back Matter Glossary").Font
            .ColorIndex = HiddenColor
            .Size = 1
        End With
        
        Selection.Paragraphs(1).Next.Range.Select
    Else
        GoTo NoMoreParagraphs
    End If
    
    Do
        If Selection.Style = "k...k* - Special Text - Keyword" Then
            ' Change \p (or \m or whatever) paragraph style to _GlossaryHeadWord
            ' so it can have Keep with neXt.
            '
            Selection.Paragraphs(1).Style = "_GlossaryHeadWord"
            On Error GoTo -1:  On Error GoTo NoMoreParagraphs
            Selection.Paragraphs(1).Next.Range.Select
            On Error GoTo -1
        End If
                
        If Selection.Paragraphs(1).Style Like "li*- List Entry*" Then
            If Selection.Text Like "[0-9][).] *" _
            Or Selection.Text Like "[0-9][0-9][).] *" Then
                SpacePosition = InStr(1, Selection.Text, " ")
                
                If SpacePosition > 0 And SpacePosition < 10 Then
                    Selection.Characters(SpacePosition) = vbTab
                End If
            Else
                Selection.Characters(1).InsertBefore vbTab
            End If
        End If
        
        On Error GoTo -1:  On Error GoTo NoMoreParagraphs
        Selection.Paragraphs(1).Next.Range.Select
        On Error GoTo -1
        
        ' See if we have reached the end of the glossary yet.
        '
        ThisStyle = Selection.Paragraphs(1).Style
        
        If ThisStyle Like "*conc *" _
        Or ThisStyle Like "*idx *" _
        Or ThisStyle Like "*nidx *" _
        Or ThisStyle Like "*chron *" _
        Or ThisStyle Like "wm *" _
        Or ThisStyle Like "*maps *" _
        Or ThisStyle Like "*cov *" _
        Or ThisStyle Like "periph *" _
        Or ThisStyle Like "*spine *" Then
            FoundGlossary = False
        End If
        
        If ThisStyle = "id - File - Identification" _
        And Selection.Paragraphs(1).Range.Text Like "GLO *" <> True Then
            FoundGlossary = False
        End If
    Loop While FoundGlossary = True
    
NoMoreParagraphs:
    On Error GoTo -1
    Application.StatusBar = "                                                      "
    SetStandardFindOptions
    VariableSet "GlossaryEntriesFormatted", "done"
End Sub
Sub Format_ca_items()
    ' for each run of text in the style
    ' "ca...ca* - Chapter Number - Alternate"
    ' move it up into the previous paragraph in the style StdChapterStyle.
    '
    Dim Found As Boolean
    Dim rFound As Range
    Dim lFoundLen As Long
    Dim rChapter As Range
    
    If VariableGet("caMoved") = "done" Then Exit Sub
    
    SetStandardFindOptions
    Selection.HomeKey Unit:=wdStory
    
    Do
        With Selection.Find
            .Style = "ca...ca* - Chapter Number - Alternate"
            .Format = True
            .Forward = True     ' need to reset this each loop
        End With
        
        Found = False   'Cludge to handle bug with multiple searches
        Found = Selection.Find.Execute
    
        If Found Then
            ' Put parentheses around the ca number
            '
            Set rFound = Selection.Range.Duplicate
            lFoundLen = rFound.Characters.Count
            If rFound.Characters(1) <> "(" Then rFound.InsertBefore "("
            
            If rFound.Characters(lFoundLen) <> ")" Then
                rFound.InsertAfter ")"
                rFound.End = rFound.End + 1
            End If
            
            ' If the selection is not in a Chapter paragraph,
            '   mark its position in rFound
            '   search up for the nearest Chapter paragraph
            '   if char before para mark in Chapter para is not space
            '       put a space there
            '   copy ca text before para mark in Chapter para
            '   remove any character formatting from para mark
            '   select rFound again
            '   delete rFound text
            '
            If Selection.Paragraphs(1).Style <> StdChapterStyle Then
                Selection.Collapse

                With Selection.Find
                    .Style = StdChapterStyle
                    .Forward = False
                    .Wrap = wdFindStop
                End With

                If Selection.Find.Execute Then
                    Set rChapter = Selection.Range.Duplicate
                    Selection.Start = Selection.End - 2
                    Selection.End = Selection.End - 1
                    
                    If Selection.Text <> " " Then Selection.InsertAfter " "
                    
                    Selection.Start = Selection.End
                    rChapter.Start = Selection.Start
                    Selection.InsertAfter rFound.Text
                    ' rChapter.Select
                    rChapter.Style = "ca...ca* - Chapter Number - Alternate"
                    Selection.End = Selection.Paragraphs(1).Range.End
                    Selection.Start = Selection.End - 1
                    Selection.Font.Reset
                    rFound.Select
                    rFound.Delete
                    If Selection.Text = " " Then Selection.Delete
                End If
            Else
                Selection.Start = Selection.End
            End If
        End If
    Loop While Found
    
    VariableSet "caMoved", "done"
End Sub
Sub Format_rq_items()
    ' for each run of text in the style
    ' "rq...rq* - Cross Reference - Inline Quotation References"
    ' put a paragraph mark before it,
    ' and apply the style
    ' "_rq_Para"
    ' to the rq paragraph.
    Dim Found As Boolean
    Dim rFound As Range
    
    SetStandardFindOptions
    
    Selection.HomeKey Unit:=wdStory
    Selection.Find.Style = "rq...rq* - Cross Reference - Inline Quotation References"
        
    Do
        Found = False   'Cludge to handle bug with multiple searches
        Found = Selection.Find.Execute
    
        If Found Then
            Set rFound = Selection.Range.Duplicate
            Selection.Font.Reset    ' remove the character style
            ' rFound.InsertAfter vbCr
            
            ' If the selection is not at the start of a paragraph,
            '   put a paragraph mark before it.
            '
            Selection.Collapse
            
            Do While Selection.Start > 0
                Selection.Start = Selection.Start - 1
                If Selection.Range.Text = vbCr Then
                    Exit Do
                Else
                    Selection.Start = Selection.End
                    Selection.TypeParagraph
                    rFound.Start = Selection.Start
                    Selection.Start = Selection.Start - 1
                    Exit Do
                End If
            Loop
            
            Selection.Paragraphs(1).Format.KeepWithNext = True
            Selection.Paragraphs(1).Format.SpaceAfter = 0
            
            rFound.Select
            
            Selection.Paragraphs(1).Style = "_rq_Para"
            Selection.Style = "rq...rq* - Cross Reference - Inline Quotation References"
            ' At least in Word 2003 that also applies the chr style to the para mark,
            ' so we need to remove it again.
            '
            Selection.Paragraphs(1).Range.Select
            Selection.Start = Selection.End - 1
            Selection.Font.Reset
            rFound.Select
            Selection.Start = Selection.End
            Selection.MoveRight Unit:=wdCharacter, Count:=1
        End If
    Loop While Found
End Sub
Sub FormatStatusItems()
    ' To catch \sts N fields or text in style "rem - File - Status"
    ' and apply a small white paragraph style to them.
    '
    Dim FoundSTS As Boolean
    Dim SomeSTSCodeFound As Boolean
    Const StatusCodesCount As Integer = 4
    Dim StatusCodes(StatusCodesCount) As String
    Dim rStatusCode As Range
    Dim rStatusDigit As Range
    Dim StatusDigitFound As Boolean
    Dim NonDigitFound As Boolean   ' Set true if beyond any Status Digits
    
    If VariableGet("StatusCode") = "done" Then Exit Sub
    
    StatusCodes(1) = "first draft"
    StatusCodes(2) = "team draft"
    StatusCodes(3) = "reviewed draft"
    StatusCodes(4) = "clean text"

    SetStandardFindOptions
    Selection.HomeKey Unit:=wdStory
    
    ' First search for \sts code.
    '
    Selection.Find.ClearFormatting
    Selection.Find.Replacement.ClearFormatting
    With Selection.Find
        .Text = "\sts "
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindStop
    End With

    Do
        FoundSTS = Selection.Find.Execute
    
        If FoundSTS Then
            SomeSTSCodeFound = True
            Selection.Delete
            Set rStatusCode = Selection.Range.Duplicate
            StatusDigitFound = False
            
            ' Put the status item on a line by itself.
            '
            If Selection.Start > 1 Then
                Selection.Start = Selection.Start - 1
                
                If Selection.Text <> vbCr Then
                    Selection.Collapse Direction:=wdCollapseEnd
                    Selection.TypeText vbCrLf
                    rStatusCode.Start = Selection.Start
                End If
            End If
            
            Selection.Start = Selection.End
            
            ' look for a Status Digit or paragraph mark or character style
            ' If a char style found,
            '   put a CRLF before it.
            '
            Do While ActiveDocument.Range.End - Selection.End > 2
                Selection.End = Selection.End + 1
                
                If StatusDigitFound Then
                Else
                    If Selection.Text Like "[1-4]" Then
                        StatusDigitFound = True
                        Set rStatusDigit = Selection.Range.Duplicate
                    End If
                End If
                
                If Selection.Text = vbCr Then
                    Selection.End = Selection.Start
                    Exit Do
                End If
                
                If Selection.Style <> Selection.Paragraphs(1).Style Then
                    Selection.End = Selection.Start
                    Selection.TypeText vbCr
                    Selection.End = Selection.Start - 1
                    Exit Do
                End If
                
                Selection.Start = Selection.End
            Loop
                    
            If StatusDigitFound Then
                rStatusDigit.Text = StatusCodes(Val(rStatusDigit.Text))
            End If
            
            rStatusCode.End = Selection.End
            rStatusCode.Text = Trim(rStatusCode.Text)
            rStatusCode.InsertBefore "("
            rStatusCode.InsertAfter ")"
            rStatusCode.Style = ActiveDocument.Styles("rem - File - Status")
                    
            Selection.Start = Selection.End
        End If
    Loop While FoundSTS
    
    If SomeSTSCodeFound = True Then GoTo FinishSub
    
    ' Now search for text in style "rem - File - Status".
    '
    SetStandardFindOptions
    Selection.HomeKey Unit:=wdStory
    Selection.Find.ClearFormatting
    Selection.Find.Replacement.ClearFormatting

    With Selection.Find
        .Style = ActiveDocument.Styles("rem - File - Status")
        .Text = ""
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindStop
    End With

    Do
        FoundSTS = Selection.Find.Execute
    
        If FoundSTS Then
            SomeSTSCodeFound = True
            Set rStatusCode = Selection.Range.Duplicate
            Selection.Collapse
            StatusDigitFound = False
            NonDigitFound = False
            
            ' Put the status item on a line by itself.
            '
            If Selection.Start > 1 Then
                Selection.Start = Selection.Start - 1
                
                If Selection.Text <> vbCr Then
                    Selection.Collapse Direction:=wdCollapseEnd
                    Selection.TypeText vbCrLf
                    rStatusCode.Start = Selection.Start
                End If

                Selection.Start = Selection.End
            End If
            
            ' look for a Status Digit or space or paragraph mark or character style
            ' If a char style found,
            '   put a CRLF before it.
            ' If a space or hyphen found,
            '   don't look for Status Digits any more.
            ' When CRLF found,
            '   exclude it from rStatusCode and stop looking.
            '
            Do While ActiveDocument.Range.End - Selection.End > 2
                Selection.End = Selection.End + 1
                
                If StatusDigitFound Or NonDigitFound Then
                    ' do nothing
                Else
                    If Selection.Text Like "[1-4]" Then
                        StatusDigitFound = True
                        Set rStatusDigit = Selection.Range.Duplicate
                    Else
                        NonDigitFound = True
                    End If
                End If
                
                If Selection.Text = vbCr Then
                    Selection.End = Selection.Start
                    rStatusCode.End = Selection.End
                    Exit Do
                End If
                
                If Selection.Style <> Selection.Paragraphs(1).Style Then
                    Selection.End = Selection.Start
                    Selection.TypeText vbCr
                    Selection.End = Selection.Start - 1
                    Exit Do
                End If
                
                Selection.Start = Selection.End
            Loop
                    
            If StatusDigitFound Then
                rStatusDigit.Text = StatusCodes(Val(rStatusDigit.Text))
                If rStatusCode.End <= rStatusDigit.Start Then
                    rStatusCode.End = rStatusDigit.End
                End If
            End If
            
            rStatusCode.Text = Trim(rStatusCode.Text)
            rStatusCode.InsertBefore "("
            rStatusCode.InsertAfter ")"
                    
            rStatusCode.Select
            Selection.Start = Selection.End
            Selection.MoveDown Unit:=wdParagraph, Count:=1
        End If
    Loop While FoundSTS
    
FinishSub:
    If SomeSTSCodeFound Then
        VariableSet Name:="StatusCode", Value:="done"
    Else
        VariableSet Name:="StatusCode", Value:="none"
    End If
    
    SetStandardFindOptions
End Sub
Sub FormattingDoneReport(Starting As Boolean)
    ' To report when the requested formatting has been finished,
    ' with a timeout so the user doesn't have to click OK
    ' to close the dialog.
    '
    ' Call with Starting:=True at start of formatting operation,
    '    & with Starting:=False at end.
    ' If static Level comes down to zero,
    '   display the Finished report.
    '
    Static Level As Integer
    Const Delay As Integer = 5
    
    If Starting = True Then
        Level = Level + 1
    Else
        Level = Level - 1
    End If
    
    If Level <= 0 Then
'        dbMsgBoxWithTimeout.dbMsgBoxWithTimeout_Initialize
        MsgBox _
            Prompt:="Formatting is finished.", _
            Buttons:=vbOKOnly + vbInformation, _
            Title:="SILAS Formatting Done Report"
    End If
End Sub
Sub GetTextWidth(ByRef TextWidth As Single)
' Calculate the text width at the selectionn
' so Word 97 can place pictures nicely.
'
    Dim ColumnSpacing As Single
    
    With Selection.Sections(1).PageSetup
        TextWidth = .PageWidth - .LeftMargin - .RightMargin - .gutter
        
        If .TextColumns.Count = 2 Then
            ColumnSpacing = .TextColumns.Spacing
            TextWidth = (TextWidth - ColumnSpacing) / 2
        End If
    End With
End Sub
Sub HideIDetc()

    ' Make sure that Identification style(s) and BookName styles
    ' are hidden.
    '
    Dim Found As Boolean
    Dim IDStyle As Boolean
    Dim BookStyle As Boolean
    Dim ObsoletePeriphStyle As Boolean
    Dim JmpStyle As Boolean     ' for hypertext links out of Paratext
    Dim BarPosn As Integer      ' where the link details start
    Dim StyleName As String
    Dim i As Long
    
    If VariableGet("IDLines") = "hidden" Then Exit Sub
    
    SetStandardFindOptions
    WordBasic.ShowAll 1
    ZoomSet
    ActiveDocument.ActiveWindow.View.Type = wdNormalView
        
    Application.StatusBar = "               Hiding ID lines etc -- please wait."
    Application.ScreenUpdating = False
    
    Selection.Find.ClearFormatting
    
    With Selection.Find
        .Text = ""
        .Replacement.Text = ""
        .Format = True
        .Forward = True
        .Wrap = wdFindContinue
    End With
    
    For i = 1 To ActiveDocument.Styles.Count
        StyleName = ActiveDocument.Styles(i)
        IDStyle = StyleName Like "id*File*"
        BookStyle = StyleName Like "h*Header"
        ObsoletePeriphStyle = StyleName Like "OBSOLETE *Peripherals - *"
        JmpStyle = StyleName Like "jmp...*"
        
        If IDStyle Or BookStyle Or ObsoletePeriphStyle Then
            Application.ScreenUpdating = True
            Application.StatusBar = "  Hiding lines in style '" & StyleName & "', please wait."
            Selection.Find.Style = ActiveDocument.Styles(i)
            
            Selection.HomeKey Unit:=wdStory
            Application.ScreenUpdating = False
    
            Found = Selection.Find.Execute
            
            If Found Then
                If ActiveDocument.Styles(i).Font.Hidden = True Then
                    ActiveDocument.Styles(i).Font.Hidden = False
                End If
                
                With ActiveDocument.Styles(i).Font
                    .Size = 1
                    .ColorIndex = HiddenColor
                End With
                With ActiveDocument.Styles(i).ParagraphFormat
                    .LineSpacingRule = wdLineSpaceSingle
                    .SpaceAfter = 0
                    .SpaceBefore = 0
                End With
                
                Selection.Style = ActiveDocument.Styles(i)
            End If
        End If
        
        ' If the file might have Paratext-style hypertext links in it,
        '   hide the mechanism for getting to the target.
        '
        If JmpStyle Then
            Application.ScreenUpdating = True
            Application.StatusBar = "  Hiding details of link targets in style '" & StyleName & "', please wait."
            Selection.Find.Style = ActiveDocument.Styles(i)
            
            Selection.HomeKey Unit:=wdStory
            Application.ScreenUpdating = False
    
            Do
                Found = Selection.Find.Execute
            
                If Found Then
                    ' Hide from the first vertical bar to the end of the selection.
                    '
                    BarPosn = InStr(Selection.Text, "|")
                    
                    If BarPosn > 0 Then
                        Selection.Start = Selection.Start + (BarPosn - 1)
                        Selection.Range.Font.Hidden = True
                    End If
                End If
            Loop While Found
        End If
    Next i
    
    SetStandardFindOptions
    SetDefaultView
    Application.StatusBar = "                                                     "
    Application.ScreenUpdating = True
    WordBasic.ShowAll 0
    VariableSet "IDLines", "hidden"
End Sub
Sub HideSelection()
'
' HideSelection Macro
' Macro recorded 6/8/2006 by Jim Henderson
'
    With Selection.Font
        .Hidden = True
    End With
End Sub
Sub HideFigToggle()
Dim CurrentFigStyle As String
   With ActiveDocument.Styles("fig...fig* - Auxiliary - Figure/Illustration/Map").Font
      CurrentFigStyle = .Hidden
   End With
   
   If CurrentFigStyle = "0" Then
      CurrentFigStyle = "-1"
   Else: CurrentFigStyle = "0"
   End If
   
    With ActiveDocument.Styles("fig...fig* - Auxiliary - Figure/Illustration/Map").Font
      .Hidden = CurrentFigStyle
   End With
  
End Sub
Sub Hyphens2SpacesInternal(ByRef rngStory As Range)
'
' Hyphenetc Macro
' Macro recorded 07/04/2003 by Nico Daams
' modified by Jim.Henderson44@Gmail.com in May 2005, Mar 2007
    Dim rngThisStory As Range
    Dim FoundVerseStyle As Boolean
    Dim FoundXRefStyle As Boolean
    Dim StyleName As String
    Dim ThisStyle As Style
    Dim i As Long
    
    On Error GoTo MissingStoryRange
    
    Application.StatusBar = "         Changing hyphens to thin fixed spaces --  please wait."
    Application.ScreenUpdating = False
    Set rngThisStory = rngStory.Duplicate
    rngThisStory.Select

    SetStandardFindOptions
    rngThisStory.Select

    ' Change "=" characters to no-break spaces?
    '
    If ChangeEqualsToNoBreakSpace Then  ' Global constant
        Selection.Find.ClearFormatting
        With Selection.Find
            .Text = "="
            .Replacement.Text = "^s"
            .Forward = True
        End With
        Selection.Find.Execute Replace:=wdReplaceAll
    End If
    
    ' Now change no-break hyphens to ordinary hyphens
    ' in case hyphens were changed to no-break hyphens in formatting for proof printouts
    '
    SetStandardFindOptions
    Selection.Collapse
    With Selection.Find
        .Text = "^~"
        .Replacement.Text = "-"
    End With
    Selection.Find.Execute Replace:=wdReplaceAll
    
    ' Change any hyphen between verse numbers into a non-breaking hyphen.
    ' (This is done as part of default formatting,
    '  but was undone just above, so we'll do it again.)
    '
    SetStandardFindOptions
    rngThisStory.Select
    Selection.Start = Selection.End
    Selection.Find.ClearFormatting
    Selection.Find.Replacement.ClearFormatting
    With Selection.Find
        .Text = "([0-9a-dA-D])-([0-9])"
        .Replacement.Text = "\1^~\2"
        .MatchWildcards = True
        .Forward = True
        .Wrap = wdFindContinue
        .Format = False
    End With
    Selection.Find.Execute Replace:=wdReplaceAll

    ' Change any other hyphens to small no-break spaces
    '
    ' Shoddy Wd 2010 crashes if we do this in footnotes in Normal view
    '
    If Val(Application.Version) >= Wd2010 Then
        ActiveWindow.ActivePane.View.Type = PageOrPrintView
    End If
    
    rngThisStory.Select
    Selection.Find.ClearFormatting
    Selection.Find.Replacement.ClearFormatting
    With Selection.Find
        .MatchWildcards = True
        .Text = "([!0-9])-([!0-9])"
        .Replacement.Text = "\1^s\2"
        .Forward = True
        .Wrap = wdFindContinue
        .Format = True
    End With
    
    Do While Selection.Find.Execute(Replace:=wdReplaceOne)
        Selection.Characters(2).Font.Scaling = ThinSpaceScaling
        Selection.Start = Selection.End
    Loop
    
    Selection.WholeStory
    If ActiveWindow.View.SplitSpecial <> wdPaneNone Then
        If ActiveWindow.Panes.Count > 1 Then ActiveWindow.Panes(2).Close
    End If
    
MissingStoryRange:
    On Error GoTo 0
    SetStandardFindOptions
End Sub
Sub IncreaseRightMargin()
    Dim RightMargin As Single
    Dim TextWidth As Long
    
    ' in case different sections have different margins,
    '   get the margin of the section where the selection is.
    '
    With Selection.Sections(1).PageSetup
        RightMargin = .RightMargin
        TextWidth = .PageWidth - .RightMargin - -.LeftMargin - .gutter
    End With
    
    If TextWidth > InchesToPoints(4#) Then
        With ActiveDocument.PageSetup
            .RightMargin = RightMargin + InchesToPoints(1#)
        End With
    End If
    
    If Not FormattingInProgress Then
        OptionalLineBreaks
    End If
End Sub
Sub InitAfterAttachingTemplate()
    Dim tempName As String
    
    tempName = VariableGet("LanguageName")
    
    If tempName = "" Or tempName = " " Then
        LangData.LoadLanguageData
        SetDefaultPaperSize
    End If
    ' ReplaceAlmostEverywhere iResetFont

End Sub
Function InsertBookMarks()
Dim DroppedChapter
Dim a$
Dim FSdlg As Object
Dim b$
Dim rHere As Range

Dim ChapterStyle$

' At the end of each chapter a bookmark is inserted
' The names of the bookmarks are roman numerals indicating the next chapter

' InsertBookMarks = 1

ChapterStyle$ = StdChapterStyle
' Delete "z" Chapter Drop style to enable search for original style
' If it does not exist, create style first in order to avoid error
' WordBasic.FormatStyle Name:="z" + ChapterStyle$, Type:=1, Define:=1
' WordBasic.FormatStyle Name:="z" + ChapterStyle$, Delete:=1

WordBasic.StartOfDocument
WordBasic.EditFindClearFormatting
WordBasic.EditFindStyle ChapterStyle$
WordBasic.EditFind "", Format:=1, Direction:=0
If Not WordBasic.EditFindFound() Then
    WordBasic.MsgBox " Chapter Style " + Chr(34) + ChapterStyle$ + Chr(34) + " is not found!", 16
    GoTo Abort
Else
    If WordBasic.Val(WordBasic.[Selection$]()) = 0 Then
        DroppedChapter = 0
        ' New "z" Chapter Drop style is a character style
        ' while original Chapter Drop style is supposed to be a paragraph style for non-dropped chapters
        ChapterStyle$ = "z" + ChapterStyle$
        WordBasic.FormatStyle Name:=ChapterStyle$, Type:=1, Define:=1
    Else
        DroppedChapter = -1
    End If
End If
While WordBasic.EditFindFound()
    WordBasic.CharLeft 1, 1
    
    While Selection.Text Like "* "
        WordBasic.CharLeft 1, 1
    Wend
    
    If DroppedChapter = 0 Then
        WordBasic.CharRight
        WordBasic.WordLeft 1, 1
        WordBasic.FormatStyle Name:=ChapterStyle$, Apply:=1
    End If
    a$ = WordBasic.[Selection$]()
    WordBasic.PrintStatusBar "Inserting bookmark at start of chapter " + a$ + " . . ."
    
    Set rHere = Selection.Range.Duplicate
    
    If WordBasic.Val(a$) = 1 Then
        ' Insert bookmark for first chapter at beginning of document
        ' WordBasic.StartOfDocument
        
        ' Allow multiple sections
        '
        Selection.Sections(1).Range.Select
        Selection.Collapse
    Else
        WordBasic.StartOfLine
    End If
    WordBasic.InsertFieldChars
    WordBasic.Insert "= " + a$ + " \* Roman"
    WordBasic.CharRight 2
    WordBasic.CharLeft 1, 1
    WordBasic.UpdateFields
    WordBasic.UnlinkFields
    b$ = WordBasic.[Selection$]()
    WordBasic.WW6_EditClear
    WordBasic.EditBookmark b$, Add:=1
    
    rHere.Select
    Selection.Collapse Direction:=wdCollapseEnd
    WordBasic.RepeatFind
Wend

InsertBookMarks = 1
Abort: End Function
Sub InsertTablesInFooters()
    ZoomSet
    CreateIDFootersInternal _
        IncludeTable:=True, MakeSectionBreaks:=True
    SetDefaultView
End Sub
Sub InsertTextBox()
'
' InsertTextBox Macro
' Macro recorded 6/23/2006 by Jim Henderson
'
    Selection.Collapse
    ActiveDocument.Shapes.AddTextbox(msoTextOrientationHorizontal, 189#, _
        176.35, 156#, 156#).Select
    Selection.ShapeRange.TextFrame.TextRange.Select
    Selection.Collapse
End Sub
Function InternationalCDbl(ByVal Value As String) As Double
' Convert the string argument to a double,
' even where the locale is other than English,
' and the decimal separator is not "."
'
Dim DecimalChar As String
Dim DecimalPosn As Long
Dim Adjusted As String

DecimalChar = Application.International(wdDecimalSeparator)
DecimalPosn = InStr(1, Value, ".")

If DecimalPosn > 0 Then
    Adjusted = Left(Value, (DecimalPosn - 1)) & DecimalChar & _
                Right(Value, (Len(Value) - DecimalPosn))
Else
    Adjusted = Value
End If

InternationalCDbl = CDbl(Adjusted)

End Function
Sub JoinParagraphs()
    ' Change all paragraph marks in the selection to spaces,
    ' then reduce multiple spaces to one each.
    '
    ' First, make sure the final paragraph mark is not deleted,
    ' because that is where the style for the selection is stored.
    '
    If Selection.Characters(Selection.Characters.Count) = vbCr Then
        Selection.End = Selection.End - 1
    End If
    
    SetStandardFindOptions
    
    Selection.Find.ClearFormatting
    With Selection.Find
        .Text = "^p"
        .Replacement.Text = " "
        .MatchWildcards = False
        .Wrap = wdFindStop
        .Execute Replace:=wdReplaceAll
    End With

    ReduceMultipleSpaces
End Sub
Sub LayOutForDoubleSidedPrinting()
   With ActiveDocument.PageSetup
       .OddAndEvenPagesHeaderFooter = True
       .DifferentFirstPageHeaderFooter = True
       .MirrorMargins = True
   End With
End Sub
Sub LayOutForSingleSidedPrinting()
   With ActiveDocument.PageSetup
       .OddAndEvenPagesHeaderFooter = False
       .DifferentFirstPageHeaderFooter = True
       .MirrorMargins = False
   End With
End Sub
Sub MakeCheckingTableName(CheckingTableName As String)
' Work out a filename for the checking table for this language, made from:
'   "Checking table_"
'   number part of contents of variable "LanguageNumber"
'   underscore
'   first 20 characters of variable "LanguageName"
'   ".doc"
'       (eg. "Checking table_4_East Papua Front Tra.doc")
'       and put that in CheckingTableName.
'
    Dim LanguageNumber As String
    Dim LanguageName As String
    Dim Posn As Long
    Const NameSize As Integer = 20
    
    LanguageNumber = VariableGet("LanguageNumber")
    Posn = InStr(1, LanguageNumber, "_")
    LanguageNumber = Right(LanguageNumber, Len(LanguageNumber) - Posn)
    LanguageName = VariableGet("LanguageName")
    
    If Len(LanguageName) > NameSize Then
        LanguageName = Left(LanguageName, NameSize)
    End If
    
    CheckingTableName = "Checking table_" & LanguageNumber & "_" & LanguageName
End Sub
Sub MakeFrontPages()
    Dim Months(12) As String
    Dim StyleName As String
    Dim TitleCopied As Boolean
    Dim PassageRef As String
    Dim Village As String
    Dim rStartOfText As Range
    Dim Province As String
    Dim Country As String
    
    Months(1) = "January"
    Months(2) = "February"
    Months(3) = "March"
    Months(4) = "April"
    Months(5) = "May"
    Months(6) = "June"
    Months(7) = "July"
    Months(8) = "August"
    Months(9) = "September"
    Months(10) = "October"
    Months(11) = "November"
    Months(12) = "December"
    
    If VariableGet("FrontMatter") = "done" Then
        Gen.RemoveFrontMatter
    End If
    
    Selection.HomeKey Unit:=wdStory
    
    ' Set up for booklets, clear footers and put centred page number on first page footer.
    ZoomSet
    ActiveWindow.ActivePane.View.Type = wdNormalView
    
    PassageRef = VariableGet("ScripturePassage")   'from dbBookletStyle
    Village = VariableGet("VillageName")
    If Village = " " Then Village = ""

    ActiveDocument.Styles("_Footer").Font.Italic = True
    
    ' Find Main Title lines and mark them for copying to the Title Page
    '
    Selection.HomeKey Unit:=wdStory
    TitleCopied = False
    
    Do
        StyleName = Selection.Paragraphs(1).Style
        If StyleName Like "p*" _
        Or StyleName Like "q*" Then
            If Not StyleName Like "pb*" _
            And Not StyleName Like "*pub *" _
            And Not StyleName Like "*pref *" _
            And Not StyleName Like "p[12]*" Then
                Exit Do     ' break out if no Main Title
            End If
        End If
        
        If Not StyleName Like "mt*" Then
            Selection.MoveDown Unit:=wdParagraph, Count:=1
            
            If (ActiveDocument.Range.End - Selection.Start) < 2 Then
                Exit Do
            End If
        Else    ' Here is the first Main Title line.
            Set rStartOfText = Selection.Range
            
            Do
                StyleName = Selection.Paragraphs(1).Style
                If StyleName Like "mt*" Then
                    Selection.MoveDown Unit:=wdParagraph, Count:=1
                   
                    If (ActiveDocument.Range.End - Selection.Start) < 2 Then
                        Exit Do
                    End If
                 Else
                    Exit Do
                End If
            Loop While True
            
            'Here is the start of the next line after Main Title lines.
            '
            rStartOfText.End = Selection.Start
            rStartOfText.Select
            Selection.Copy
            TitleCopied = True
            Exit Do
        End If
    Loop While True
    
    ' Now start making the front matter.
    '
    Selection.HomeKey Unit:=wdStory
    Selection.InsertBreak Type:=wdSectionBreakNextPage
    Selection.MoveLeft Unit:=wdCharacter, Count:=1
    Selection.Style = ActiveDocument.Styles(wdStyleNormal)
    Selection.HomeKey Unit:=wdStory
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.MoveLeft Unit:=wdCharacter, Count:=1

    ' Copy Main Title lines to start of file.
    '
    If TitleCopied Then
        Selection.Paste
    Else
        Selection.Style = ActiveDocument.Styles("mt - Title - Major Title Level 1")
        Selection.TypeText Text:="No \mt Main Title found"
    End If
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.MoveLeft Unit:=wdCharacter, Count:=1
        
    Selection.Style = ActiveDocument.Styles("_Spread_items")
    Selection.TypeText Text:=VariableGet("LanguageName") & " Language" & vbTab & "Scripture booklet"
    Selection.TypeText Text:=Chr(11)
    Selection.InsertBreak Type:=wdPageBreak
    Selection.Style = ActiveDocument.Styles("_Paragraph_Centred")
    Selection.TypeParagraph
    Selection.TypeText Text:="This is the copyright page -- delete this line."
    Selection.TypeParagraph
    Selection.TypeText Text:="and any other lines you don't need."
    Selection.TypeParagraph
    Selection.TypeText Text:="Illustrations on pages X, Y and Z are by Horace Knowles"
    Selection.TypeParagraph
    Selection.TypeText Text:="and are used by permission of"
    Selection.TypeParagraph
    Selection.TypeText Text:=Chr(169) & " "
    Selection.Start = Selection.Start - 2
    Selection.End = Selection.End - 1
    Selection.Range.Font.Name = "Times New Roman"
    Selection.Start = Selection.Start + 2
    Selection.TypeText Text:="The British and Foreign Bible Society,"
    Selection.TypeParagraph
    Selection.TypeText Text:="1954, 1967, 1972"
    Selection.TypeParagraph
    Selection.TypeText Text:="Additions and amendments by Louise Bass"
    Selection.TypeParagraph
    Selection.TypeText Text:=Chr(169) & " "
    Selection.Start = Selection.Start - 2
    Selection.End = Selection.End - 1
    Selection.Range.Font.Name = "Times New Roman"
    Selection.Start = Selection.Start + 2
    Selection.TypeText Text:="The British and Foreign Bible Society, 1994"
    Selection.TypeParagraph
    Selection.TypeParagraph
    ' Selection.TypeParagraph
    Selection.TypeText Text:="Illustrations on pages A, B, C and D"
    Selection.TypeParagraph
    Selection.TypeText Text:="are used by permission of"
    Selection.TypeParagraph
    Selection.TypeText Text:="of Cook Communications Ministries International"
    Selection.TypeParagraph
    Selection.TypeText Text:=Chr(169) & " "
    Selection.Start = Selection.Start - 2
    Selection.End = Selection.End - 1
    Selection.Range.Font.Name = "Times New Roman"
    Selection.Start = Selection.Start + 2
    Selection.TypeText Text:="1978, David C. Cook Publishing Co."
    Selection.TypeParagraph
    ' Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.TypeText Text:=Chr(169) & " "
    Selection.Start = Selection.Start - 2
    Selection.End = Selection.End - 1
    Selection.Range.Font.Name = "Times New Roman"
    Selection.Start = Selection.Start + 2
    Selection.TypeText Text:=Months(Month(Date)) & " " & Year(Date) & ", WPS"
    Selection.TypeParagraph
    Selection.TypeText Text:="# copies"
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.TypeText Text:="Trial translation for testing/revision of " & PassageRef
    Selection.TypeParagraph
    Selection.TypeText Text:="in the " & VariableGet("LanguageName") & " language,"
    Selection.TypeParagraph
    
    Province = VariableGet("LanguageProvince")
    
    If Province <> "" And Province <> " " Then
        Selection.TypeText Text:=Province & " Province,"
        Selection.TypeParagraph
    End If
    
    Country = VariableGet("LanguageCountry")
    
    If Country <> "" And Country <> " " Then
        Selection.TypeText Text:=Country
        Selection.TypeParagraph
    End If
    
    If Village <> "" Then Selection.TypeText Text:="printed at " & Village
    Selection.TypeParagraph
    

    Selection.HomeKey Unit:=wdStory
    Selection.TypeParagraph
    Selection.TypeParagraph
    ActiveWindow.ActivePane.View.ShowAll = False

    ' Now remove any space put before the BookName bookmark.
    RemoveSpaceBeforeBookName
    
    ' Set up page headers and footers.
    '
    ActiveDocument.Sections(1).Footers(wdHeaderFooterFirstPage).Range.Style = "_Footer"
    ActiveDocument.Sections(1).Footers(wdHeaderFooterPrimary).Range.Style = "_Footer"
    ActiveDocument.Sections(1).Footers(wdHeaderFooterEvenPages).Range.Style = "_Footer"
    
    ' First page of text
    '
    With ActiveDocument.Sections(2).Headers(wdHeaderFooterFirstPage)
        .LinkToPrevious = False
        .Range.Text = ""
    End With

    ' Break the link to other pages of section 2
    '
    ActiveDocument.Sections(2).Headers(wdHeaderFooterEvenPages).LinkToPrevious = False
    ActiveDocument.Sections(2).Headers(wdHeaderFooterPrimary).LinkToPrevious = False

    ' Now clear the headers of the front pages
    '
    ActiveDocument.Sections(1).Headers(wdHeaderFooterFirstPage).Range.Text = ""
    ActiveDocument.Sections(1).Headers(wdHeaderFooterEvenPages).Range.Text = ""
    ActiveDocument.Sections(1).Headers(wdHeaderFooterPrimary).Range.Text = ""

    ' Now the footers -- First page of text
    '
    ActiveDocument.Sections(2).Footers(wdHeaderFooterFirstPage).LinkToPrevious = False
    ActiveDocument.Sections(2).Footers(wdHeaderFooterEvenPages).LinkToPrevious = False
    ActiveDocument.Sections(2).Footers(wdHeaderFooterPrimary).LinkToPrevious = False

    ' and clear footers of front pages
    '
    ActiveDocument.Sections(1).Footers(wdHeaderFooterFirstPage).Range.Text = ""
    ActiveDocument.Sections(1).Footers(wdHeaderFooterEvenPages).Range.Text = ""
    ActiveDocument.Sections(1).Footers(wdHeaderFooterPrimary).Range.Text = ""
    
    ActiveWindow.View.ShowHiddenText = False
    VariableSet "FrontMatter", "done"
    
    SetStandardFindOptions
    SetDefaultView
    End Sub
Sub MakeHeaders(Optional MakeSectionBreaks As Boolean)
'
' MakeHeaders Macro
' Macro recorded 24/05/04 by Jim Henderson
'
    Dim Found As Boolean
    Dim FoundStyle As Boolean
    Dim IDFound As Boolean
    Dim i As Integer
    Dim StyleName As String
    Dim BookNameStyle As String
    Dim BookIDLen As Long
    Dim rHeader As Range
    Dim HeaderStart As Long
    Dim NrSections As Long
    Dim TitlePageStyleFound As Boolean
    Dim rNewPages As Range
    Dim SectionNumberForHeaders As Long
'    Dim OldBooknameStyleExists As Boolean
    
    Dim NrHeaderFields As Integer
    Dim Cs$     ' Chapter style
    Dim rEvenPageHeader As Range
    Dim rOddPageHeader As Range
    Dim rng As Range
    
    CopyStyle "_Header"
    CopyStyle "_Footer"

    HeadingStyles(1) = "h1 - File - Header"
    HeadingStyles(2) = "h2 - File - Left Header"
    HeadingStyles(3) = "h3 - File - Right Header"
    HeadingStyles(4) = "h - File - Header"
    
    If DefaultSettings <> True Then Exit Sub ' unless done already
    
    RemoveFrontMatter           ' unless done already
    RemoveSectionBreaks
    ActiveDocument.ActiveWindow.View.Type = wdNormalView
    
    ActiveDocument.Styles("_Header").Font.Italic = True
        
    ' Show hidden text but not field codes.
    WordBasic.ToolsOptionsView Hidden:=1, ShowAll:=0, FieldCodes:=0
    
    ' Look for text in the style "h - File - Header"
    ' and others like it,
    ' and remember what's found, to make page headers.
    ' If none found, use the bookname part of the id line.
    '
    Found = False
    SetStandardFindOptions
    Selection.Find.ClearFormatting
    BookNameStyle = ""
    
    With Selection.Find
        .Text = ""
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindContinue
        .Format = True
    End With
    
    For i = 1 To 5
        Selection.HomeKey Unit:=wdStory
        If i < 5 Then
            Selection.Find.Style = ActiveDocument.Styles(HeadingStyles(i))
        
            HeadingFound(i) = Selection.Find.Execute
    
            If HeadingFound(i) Then
                Found = True
            End If
        End If
    Next i
    
    If Not Found Then
        ' If no bookname field found, use a bit of the ID line
        '
        '   but first, in case this macro has been run already,
        '       remove any space put before the bookmark we are about to make.
        '
        RemoveSpaceBeforeBookName
        Selection.HomeKey Unit:=wdStory
        Selection.Find.Style = ActiveDocument.Styles("id - File - Identification")
 
        With Selection.Find
            .Text = ""
            .Replacement.Text = ""
            .Format = True
            .Forward = True
            .Wrap = wdFindContinue
            .MatchCase = False
            .MatchWholeWord = False
            .MatchWildcards = False
            .MatchSoundsLike = False
            .MatchAllWordForms = False
        End With
        IDFound = Selection.Find.Execute
        
        If IDFound Then
            ' step back off the paragraph mark
            '
            Selection.MoveEnd Unit:=wdCharacter, Count:=-1
            
            BookIDLen = InStr(1, Selection.Text, " ", vbTextCompare) - 1
            
            If BookIDLen > 0 Then
                Selection.End = Selection.Start + BookIDLen
            Else
                Selection.End = Selection.Start
            End If
        End If
        
        ' The BookName bookmark will contain the first part of the ID line or nothing.
        ' But first, put a space before the bookmark,
        ' so the bookmark won't stretch to include all the front matter!
        '
        Selection.InsertBefore (" ")
        Selection.Start = Selection.Start + 1

        With ActiveDocument.Bookmarks
            .Add Range:=Selection.Range, Name:="BookName"
            .DefaultSorting = wdSortByName
            .ShowHidden = True
        End With
    End If
    
    With ActiveDocument.PageSetup
        .OddAndEvenPagesHeaderFooter = True
        .DifferentFirstPageHeaderFooter = True
    End With
    
    ' point to the end of the document
    ' or the end of the first Scripture text section
    ' so we can make sure there are at least 3 pages in the section,
    ' so we can make all 3 kinds of headers.
    '
    NrSections = ActiveDocument.Sections.Count
    ' If NrSections is 1 then go to end of document
    ' elseif any text in a title page style then go to the end of section 2
    ' else go to the end of section 1.
    '
    SectionNumberForHeaders = 1
    
    If NrSections = 1 Then
        Selection.EndKey Unit:=wdStory
    Else
        Selection.HomeKey Unit:=wdStory
        Selection.Find.Style = ActiveDocument.Styles("_Paragraph_Centred")
 
        With Selection.Find
            .Text = ""
            .Replacement.Text = ""
            .Format = True
            .Forward = True
            .Wrap = wdFindStop
            .MatchCase = False
            .MatchWholeWord = False
            .MatchWildcards = False
            .MatchAllWordForms = False
        End With
        TitlePageStyleFound = Selection.Find.Execute
        
        If TitlePageStyleFound Then
            SectionNumberForHeaders = 2
            ActiveDocument.Sections(SectionNumberForHeaders).Range.Select
            Selection.Start = Selection.End
            Selection.Start = Selection.Start - 2
            Selection.Collapse
        Else
            ActiveDocument.Sections(1).Range.Select
            Selection.Start = Selection.End
            Selection.Start = Selection.Start - 2
            Selection.Collapse
        End If
    End If
    
    Selection.TypeParagraph
    Selection.Start = Selection.Start - 1
    Set rNewPages = Selection.Range.Duplicate
    Selection.Start = Selection.End
    
    On Error GoTo -1:  On Error GoTo error1
    
    ' Make sure there are enough pages in this section
    ' so we can make the headers
    '
    Selection.InsertBreak Type:=wdPageBreak
    Selection.InsertBreak Type:=wdPageBreak
    
    ' Make sure each header contains only one line
    '
    Selection.Sections(1).Headers(wdHeaderFooterFirstPage).Range.Text = ""
    Selection.Sections(1).Headers(wdHeaderFooterEvenPages).Range.Text = "even"
    Selection.Sections(1).Headers(wdHeaderFooterPrimary).Range.Text = "odd"
 
    On Error Resume Next
    
    If ActiveWindow.View.SplitSpecial <> wdPaneNone Then
        If ActiveWindow.Panes.Count > 1 Then ActiveWindow.Panes(2).Close
    End If
    
    On Error GoTo error1
    
    ' Set the rng variable to the even page header then the odd one,
    ' and make an appropriate header in each one.
    '
    Set rng = Selection.Sections(1).Headers(wdHeaderFooterEvenPages).Range
    
    ' Insert a string of characters which will become fields later:
    '
    MakeHeadersBookAndChapters rng, LeftPage:=True, Found:=Found
    rng.Select
    Selection.Style = ActiveDocument.Styles("_Header")
    
    ' Now to convert the text to fields:
    '
    If PageHeaderChapterNumbersSimple = "yes" Then
        ConvertTextIntoFields NrFields:=9
    Else
        If Application.International(wdDecimalSeparator) = _
            Application.International(wdListSeparator) Then
            NrHeaderFields = 9
        Else
            NrHeaderFields = 14
        End If
        ConvertTextIntoFields NrFields:=NrHeaderFields
    End If
    
    ' Now for the odd pages . . .
    '
    Set rng = Selection.Sections(1).Headers(wdHeaderFooterPrimary).Range
    MakeHeadersBookAndChapters rng, LeftPage:=False, Found:=Found
    rng.Select
    Selection.Style = ActiveDocument.Styles("_Header")
    
    If PageHeaderChapterNumbersSimple = "yes" Then
        ConvertTextIntoFields NrFields:=9
    Else
        ConvertTextIntoFields NrFields:=14
    End If
    
    With ActiveWindow
        If .Panes.Count > 1 Then .ActivePane.Close
    End With

    GoTo end1
    
    
error1:
    MsgBox "Error happened making page headers: " & Err.Description & vbCr & _
        "We'll continue anyway."
    On Error GoTo 0
    ' fallthrough to remove extra pages added to this section.
    '
end1:
    On Error GoTo 0
    ' Remove the things we added
    '
    rNewPages.Select
    Selection.Delete
    Selection.EndKey Unit:=wdLine, Extend:=wdExtend
    Selection.Delete Unit:=wdCharacter, Count:=1
    Selection.Delete
    
    If MakeSectionBreaks Then
        AddSectionBreaks
        
        ' If PageHeaderChapterNumbersSimple <> "yes" Then
        '     n = InsertBookMarks ' put in the bookmarks the page headers refer to.
        ' End If
    End If
   
    SetStandardFindOptions
    WordBasic.ToolsOptionsView Hidden:=0, ShowAll:=0, FieldCodes:=0
    Selection.HomeKey Unit:=wdStory
End Sub
Sub MakeHeadersBookAndChapters(ByRef rng As Range, LeftPage As Boolean, Found As Boolean)
    ' Makes the page header for a left page or for a right page.
    '
    ' The publication standard of September 2005 says:
    ' Usually the book name and chapter is on the outside margin
    ' and the page number is on the inside margin or centred.
    '
    ' The Sample Scripture Specification Form in that document asks:
    '   Does the book name go in the center [_]   or outside margin [_]?
    '   Does the page number go in the center [_] or inside margin [_]?
    '
    ' I think it would be better to ask the questions on the form this way:
    ' The page headers can have two components:
    '     1. book name and chapter range for this page
    '     2. Page Number
    ' Which component goes at the outside margin --
    '     1. book name and chapter range [_] or 2. page number [_]?
    ' Where does the other component go --
    '     in the center [_] or inside margin [_]?
    '
    Dim sHeaderOutside As String        ' "BookChapter" or "PageNumber"
    Dim sHeaderOutsideContent As String ' BookChapter string or PageNumber
    Dim sHeaderOther As String          ' "inside" or "center"
    Dim sHeaderOtherContent As String   ' BookChapter string or PageNumber
    Dim sBookChapterContent As String   ' BookChapter string
    Dim sPreOtherTab As String
    
    Dim HeadingStyleNumber As Integer
    Dim ChapterFields As String
    Dim TextWidth As Long
    Dim BookNameStyle As String
    
    sHeaderOutside = VariableGet("HeaderOutside")
    
    If sHeaderOutside = "" _
    Or sHeaderOutside = vbCr Then
        sHeaderOutside = "PageNumber"    ' requested at CTC 2006
    End If
    
    sHeaderOther = VariableGet("HeaderOther")
    If sHeaderOther = "" _
    Or sHeaderOther = vbCr Then
        sHeaderOther = "center"  ' requested at CTC 2006
    End If
    
    ' If the Other item is in the centre,
    '   add a tab stop for it.
    '
    If sHeaderOther = "center" Then
        With Selection.Sections(1).PageSetup
            TextWidth = .PageWidth - .LeftMargin - .RightMargin - .gutter
        End With
        
        ActiveDocument.Styles("_Header").ParagraphFormat.TabStops.Add _
            Position:=TextWidth / 2, Alignment:=wdAlignTabCenter, _
            Leader:=wdTabLeaderSpaces
        
        sPreOtherTab = vbTab
    Else
        sPreOtherTab = ""
    End If
    
    If LeftPage Then
        HeadingStyleNumber = 2
    Else
        HeadingStyleNumber = 3
    End If
    
    ChapterFields = MakeHeadersChapters
    
    ' If there is a heading style,
    '   use it
    ' else use bookmark BookName.
    '
    If Found Then
        If HeadingFound(HeadingStyleNumber) Then
            BookNameStyle = HeadingStyles(HeadingStyleNumber)
        ElseIf HeadingFound(1) Then
            BookNameStyle = HeadingStyles(1)
        ElseIf HeadingFound(4) Then
            BookNameStyle = HeadingStyles(4)
        ElseIf HeadingFound(5) Then
            BookNameStyle = HeadingStyles(5)
        End If
        
        sBookChapterContent = _
            "{StyleRef " & Chr(34) & BookNameStyle & Chr(34) & _
                " \* MERGEFORMAT}" & _
                Chr(160) & ChapterFields    ' Chr 160 is no-break space, for Word 2007
    Else    ' no \h* field found
        If ActiveDocument.Bookmarks.Exists("BookName") = True Then
            sBookChapterContent = _
                "{Ref BookName \* MERGEFORMAT}" & _
                Chr(160) & ChapterFields
        Else
            sBookChapterContent = _
                "[No ""h - File - Header"" (\h)] " & _
                Chr(160) & ChapterFields
        End If
    End If
    
    If sHeaderOutside = "BookChapter" Then
        sHeaderOutsideContent = sBookChapterContent
        sHeaderOtherContent = "{Page \* MERGEFORMAT}"
    Else
        sHeaderOutsideContent = "{Page \* MERGEFORMAT}"
        sHeaderOtherContent = sBookChapterContent
    End If
        
    If LeftPage Then
        rng.Text = sHeaderOutsideContent & vbTab & sHeaderOtherContent
    Else
        rng.Text = sPreOtherTab & sHeaderOtherContent & vbTab & sHeaderOutsideContent
    End If
End Sub
Private Function MakeHeadersChapters() As String
Dim Cf$
Dim n As Integer
Dim Cs$
Dim ChapterFields As String

' No need to worry about ordinary chapter number style
' or dropcap chapter style
' because we keep the ordinary numbers in the file, small and white.
'
Cs$ = "_CurrentChapter"

Cf$ = " \* arabic"

' Make a string of characters which will become fields later:
'
If PageHeaderChapterNumbersSimple = "yes" Then
    Cs$ = " " & Cs$ & " "
    'WordBasic.Insert "{IF {PageRef {StyleRef " + ChapterStyle$ + " \l \* Roman}}<{Page} " + Chr(34) + "{StyleRef " + ChapterStyle$ + ChapterFormat$ + "}" + Chr(34) + " " + Chr(34) + "{IF {PageRef {StyleRef " + ChapterStyle$ + " \* Roman}}<{Page} " + Chr(34) + "{StyleRef " + ChapterStyle$ + ChapterFormat$ + "}{IF {StyleRef " + ChapterStyle$ + "}<{={StyleRef " + ChapterStyle$ + " \l }-1} " + Chr(34) + "-{StyleRef " + ChapterStyle$ + " \l" + ChapterFormat$ + "}" + Chr(34) + " " + Chr(34) + ", {StyleRef " + ChapterStyle$ + " \l" + ChapterFormat$ + "}" + Chr(34) + "}" + Chr(34) + " " + Chr(34) + "{={StyleRef " + ChapterStyle$ + " }-1" + ChapterFormat$ + "}{IF {StyleRef " + ChapterStyle$ + "}<{StyleRef " + ChapterStyle$ + " \l } " + Chr(34) + "-{StyleRef " + ChapterStyle$ + " \l" + ChapterFormat$ + "}" + Chr(34) + " " + Chr(34) + ", {StyleRef " + ChapterStyle$ + " \l" + ChapterFormat$ + "}" + Chr(34) + "}" + Chr(34) + "}" + Chr(34) + "}"
    
    ChapterFields = _
        "{IF {StyleRef " & Cs$ & " }" & _
        "= {StyleRef " & Cs$ & "\l }" & _
            " " & Chr(34) & "{StyleRef " & Cs$ & "}" & Chr(34) & _
            " " & Chr(34) & "{StyleRef " & Cs$ & "}" & _
            ", " & "{StyleRef " & Cs$ & " \l" & "}" & Chr(34) & _
        "}"
Else
    Cs$ = Chr(34) + Cs$ + Chr(34)
    
    ' if language settings have the same character for
    '   decimal point and list separator,
    '       Word will give up and refuse to do things like
    '           if (X - Y) > 1
    '   so do simple separation between chapter numbers
    '   such as 4 - 5 or 7 - 9
    ' else do more complicated separation
    '   such as 4, 5 or 7-9
    '
        If Application.International(wdDecimalSeparator) = _
            Application.International(wdListSeparator) Then
            ChapterFields = _
                "{StyleRef " & Cs$ & " \* MERGEFORMAT}" & _
            "{if {STYLEREF " & Cs$ & "\l}<>{STYLEREF " & Cs$ & "}" & _
                Chr(34) & "-{STYLEREF " & Cs$ & "\l}" & Chr(34) & "}"
        Else
            ChapterFields = _
                "{StyleRef " & Cs$ & " \* MERGEFORMAT}" & _
            "{if{={STYLEREF " & Cs$ & "\l}-{STYLEREF " & Cs$ & "}}> 1 " & _
                Chr(34) & "-" & Chr(34) & "}" & _
            "{if{={STYLEREF " & Cs$ & "\l}-{STYLEREF " & Cs$ & "}}= 1 " & _
                Chr(34) & ", " & Chr(34) & "}" & _
            "{if {STYLEREF " & Cs$ & "\l}<>{STYLEREF " & Cs$ & "}" & _
                Chr(34) & "{STYLEREF " & Cs$ & "\l}" & Chr(34) & "}"
        End If
    End If
' MsgBox "ChapterFields is " & ChapterFields

MakeHeadersChapters = ChapterFields

End Function
Sub MoveChapterAfterIDLines()
    ' When you copy just a few chapters into Pt,
    ' it helps to have extra ID, \h and \mt* lines after the \c,
    ' but this messes up formatting.
    '
    ' So this macro will find this situation and fix it
    ' by moving any chapter paragraphs below adjacent id lines and main titles.
    '
    Dim Found As Boolean
    Dim rIDline As Range
    Dim StyleName As String
    
    If VariableGet("ChaptersMoved") = "done" Then
        Exit Sub
    End If
    
    ActiveWindow.ActivePane.View.Type = wdNormalView
    Selection.HomeKey Unit:=wdStory

    Application.StatusBar = "               Moving some chapter codes -- please wait."
    Application.ScreenUpdating = False
    
    Selection.Find.ClearFormatting
    Selection.Find.Style = ActiveDocument.Styles("id - File - Identification ")

    With Selection.Find
        .Text = ""
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindStop
        .Format = True
    End With

    Do
        Found = Selection.Find.Execute

        If Found Then
            Set rIDline = Selection.Range.Duplicate
            
            ' Go up where there might be a chapter number
            '
            Selection.MoveUp Unit:=wdParagraph, Count:=2
            
            If Selection.Paragraphs(1).Style = StdChapterStyle Then
                Selection.EndKey Unit:=wdLine, Extend:=wdExtend
                Selection.Cut
                rIDline.Select
                
                ' Now step down past ID lines, main title lines & intro lines
                ' and paste the chapter before the first Scripture Text line.
                '
                Do
                    StyleName = Selection.Paragraphs(1).Style
                    If StyleName Like "[hi]*" _
                    Or StyleName Like "toc*" _
                    Or StyleName Like "re*" _
                    Or StyleName Like "mt*" Then
                        Selection.MoveDown Unit:=wdParagraph, Count:=1
                    
                        If (ActiveDocument.Range.End - Selection.Start) < 2 Then
                            Exit Do
                        End If
                    Else        ' we have gone below heading lines, so
'                        rIDLine.Select
                        
                        If (ActiveDocument.Range.End - Selection.Start) < 2 Then
                            GoTo Finished
                        End If
                        
                        Exit Do
                    End If
                Loop While True
                
                Selection.Paste ' put chapter number above current line
            End If
            
            rIDline.Select
            
            'If (ActiveDocument.Range.End - Selection.Start) < 2 Then
            Selection.Start = Selection.End
            
            If (ActiveDocument.Range.End - Selection.Start) < 2 Then
                GoTo Finished
            End If
        End If
    Loop While Found
    
Finished:
    VariableSet "ChaptersMoved", "done"
End Sub
Sub NoAutoStyles()
    With AutoCorrect
        .CorrectInitialCaps = False
        .CorrectSentenceCaps = False
    End With
    With Options
        .AutoFormatAsYouTypeFormatListItemBeginning = False
        .AutoFormatAsYouTypeDefineStyles = False
        .TabIndentKey = False
        .CheckSpellingAsYouType = False
        .CheckGrammarAsYouType = False
    End With
End Sub
Sub NonBreakingHyphens(ByRef rngStory As Range)
'
' NonBreakingHyphens Macro
' Macro recorded 9/05/2005 by Jim Henderson
'
Dim rngThisStory As Range
Dim Found As Boolean

    Set rngThisStory = rngStory.Duplicate
    rngThisStory.Select
    
    Application.StatusBar = "               Making hyphens non-breaking -- please wait."
    Application.ScreenUpdating = False
    
    SetStandardFindOptions
    Selection.Find.ClearFormatting
    Selection.Find.Replacement.ClearFormatting
    With Selection.Find
        .Text = "-"
        .Replacement.Text = "^~"
        .Forward = True
        .Wrap = wdFindContinue
        .Format = False
    End With
    Selection.Find.Execute Replace:=wdReplaceAll
End Sub
Sub OptionalLineBreaks()
' To check on "//" sequences each time the margins change,
' and make them into manual line breaks if necessary.

' Display hidden text, in case "//" sequences hidden already.
' Search for "//" sequences and
' for each one
'   extend selection of // to include any following space
'   apply the style "_OptionalLineBreak" to the "// "
'   if it has ^l before it, remove it.
'   make sure the // has a space before it
'   Hide hidden text again.
'   Select current paragraph
'   if current paragraph occupies more than one line,
'     put ^l code before //.
'   Step after the //, display hidden text and search again.
' Hide hidden text again.
'
    Dim rLineBreak As Range
    Dim LineNr1 As Long
    Dim LineNr2 As Long
    Dim Found As Boolean
    Dim iView As Integer
    
    SetStandardFindOptions
    Selection.HomeKey Unit:=wdStory
    iView = ActiveDocument.ActiveWindow.View.Type
    ActiveDocument.ActiveWindow.View.Type = wdNormalView
    Application.StatusBar = "    Processing // codes for optional line breaks -- please wait."
    Application.ScreenUpdating = False
    
    Do
FindAgain:              ' to come back here when the style of text has been changed.
        ActiveWindow.View.ShowHiddenText = True ' in case // hidden already
        Found = False   ' to cope with a bug in Word
        Selection.Find.ClearFormatting
        With Selection.Find
            .Text = "//"
            .Forward = True
            .Wrap = wdFindStop
        End With
        
        Found = Selection.Find.Execute
        
        If Not Found Then Exit Do
        
        Set rLineBreak = Selection.Range.Duplicate
        
        ' Add any space after // to rLineBreak
        '
        If (ActiveDocument.Range.End - Selection.End) > 1 Then
            Selection.Start = Selection.End
            Selection.End = Selection.End + 1
            If Selection.Characters(1) = " " Then
                rLineBreak.End = Selection.End
            Else
                rLineBreak.InsertAfter " "
            End If
        End If
        
        rLineBreak.Style = ActiveDocument.Styles("_OptionalLineBreak")
        rLineBreak.Select
        
        ' Look at character before //
        ' if any "^l" before the //, remove it.
        ' if no space before //, put one there.
        ' end with rLineBreak including the space before the //.
        '
        Do While Selection.Start > 2
            Selection.Start = Selection.Start - 1
            
            If Selection.Characters(1) = Chr(11) Then
                Selection.Characters(1).Delete
            Else
                If Selection.Characters(1) = " " Then
                    rLineBreak.Start = Selection.Start
                    Exit Do
                Else
                    rLineBreak.InsertBefore " "
                    Exit Do
                End If
            End If
        Loop
        
        ' Make sure the space before the // is not in the style "_OptionalLineBreak",
        ' so it won't get hidden.
        '
        rLineBreak.Characters(1).Font.Reset
        
        ' Now find out how many lines this paragraph occupies
        '
        ActiveWindow.View.ShowHiddenText = False    ' to hide the "// "
        LineNr1 = rLineBreak.Information(wdFirstCharacterLineNumber)
        Selection.Paragraphs(1).Range.Select
        Selection.Start = Selection.End - 1
        LineNr2 = Selection.Information(wdFirstCharacterLineNumber)
        
        If LineNr2 <> LineNr1 Then
            rLineBreak.Start = rLineBreak.Start + 1
            rLineBreak.InsertBefore Chr(11)
        End If

        Selection.End = rLineBreak.End
    Loop While Found

    SetStandardFindOptions
    ActiveDocument.ActiveWindow.View.Type = iView
    Application.StatusBar = "        "
    Application.ScreenUpdating = True
    ActiveWindow.View.ShowHiddenText = False
    Selection.HomeKey Unit:=wdStory
End Sub
Sub PageSetupHalfPageBooklets()
'
' PageSetup Macro
' Macro recorded 27/11/2003 by Jim Henderson
' "Does File->Page Setup for you.
'
Dim Answer As String
Dim gutter As Integer
Dim Outside As Integer
Dim Top As Integer
Dim Bottom As Integer
Dim Head As Integer
Dim Foot As Integer
Dim TextWidth As Long
Dim NrSections As Long
Dim SectionNr As Long
Dim PagesInSection As Long
Dim HeaderFooterNr As Integer
Dim rng As Range

    ' This string of tests is repeated,
    ' so that the data input and reporting for approval
    ' can happen before any formatting is done.
    '
    ' Don't rearrange this, or booklet formatting for Vwanji will hang.
    
    ' cut or folded
    Answer = VariableGet("BookletStyle")
    If Answer = "cut" Then
        gutter = 13
        Outside = 13
    ElseIf VariableGet("BookletStyle") = "folded" Then
        gutter = 3
        Outside = 13
    End If

    ' margin trim
    Answer = VariableGet("MarginTrim")
    If Answer = "yes" Then
        Top = 22
        Head = 15
        Bottom = 23
        Foot = 15
        Outside = Outside + 5
    Else
        Top = 17
        Head = 9
        Bottom = 15
        Foot = 8
    End If
    
    ' justified text
    Answer = VariableGet("Justified")
    If Answer = "yes" Then
        With ActiveDocument.Styles("_BodyText_Base")
            .ParagraphFormat.Alignment = wdAlignParagraphJustify
        End With
    Else:
        With ActiveDocument.Styles("_BodyText_Base")
            .ParagraphFormat.Alignment = wdAlignParagraphLeft
        End With
    End If
    'fallthrough to SetSize
SetSize:
    ActiveWindow.ActivePane.View.Type = wdNormalView
'    NoAutoUpdate
    SingleSpacingNew
    SetDefaultPaperSize
    If CancelFormatting Then Exit Sub
    
    Selection.HomeKey Unit:=wdStory
    
    NrSections = ActiveDocument.Sections.Count
    
    If VariableGet("DefaultPaperSize") = "A4" Then
    
        With ActiveDocument.PageSetup
            .LineNumbering.Active = False
            .Orientation = wdOrientPortrait
            .TopMargin = MillimetersToPoints(Top)
            .BottomMargin = MillimetersToPoints(Bottom)
            .LeftMargin = MillimetersToPoints(13)
            .RightMargin = MillimetersToPoints(Outside)
            .gutter = MillimetersToPoints(gutter)
            .HeaderDistance = MillimetersToPoints(Head)
            .FooterDistance = MillimetersToPoints(Foot)
            .PageWidth = MillimetersToPoints(148)
            .PageHeight = MillimetersToPoints(210)
            .FirstPageTray = wdPrinterDefaultBin
            .OtherPagesTray = wdPrinterDefaultBin
            .SectionStart = wdSectionNewPage
            .OddAndEvenPagesHeaderFooter = True
            .DifferentFirstPageHeaderFooter = True
            .VerticalAlignment = wdAlignVerticalTop
            .SuppressEndnotes = True
            .MirrorMargins = True
        End With
    
        With ActiveDocument.PageSetup
            TextWidth = .PageWidth - .LeftMargin - .RightMargin - .gutter
        End With
        
    ElseIf VariableGet("DefaultPaperSize") = "Letter" Then
        With ActiveDocument.PageSetup
            .LineNumbering.Active = False
            .Orientation = wdOrientPortrait
            .TopMargin = MillimetersToPoints(Top)
            .BottomMargin = MillimetersToPoints(Bottom)
            .LeftMargin = MillimetersToPoints(13)
            .RightMargin = MillimetersToPoints(Outside)
            .gutter = MillimetersToPoints(gutter)
            .HeaderDistance = MillimetersToPoints(Head)
            .FooterDistance = MillimetersToPoints(Foot)
            .PageWidth = InchesToPoints(5.5)
            .PageHeight = InchesToPoints(8.5)
            .FirstPageTray = wdPrinterDefaultBin
            .OtherPagesTray = wdPrinterDefaultBin
            .SectionStart = wdSectionNewPage
            .OddAndEvenPagesHeaderFooter = True
            .DifferentFirstPageHeaderFooter = True
            .VerticalAlignment = wdAlignVerticalTop
            .SuppressEndnotes = True
            .MirrorMargins = True
        End With
        
        With ActiveDocument.PageSetup
            TextWidth = .PageWidth - .LeftMargin - .RightMargin - MillimetersToPoints(gutter)
        End With
        
    Else
        If MsgBox("Please choose the default paper size first, in the next " & _
                "window, so that I can properly set up your half-page booklet.", _
                vbOKCancel) = vbCancel Then
            CancelFormatting = True
            Exit Sub
        End If
        SetDefaultPaperSize
        GoTo SetSize
    End If
    
    With ActiveDocument.Styles("_Spread_items").ParagraphFormat.TabStops
        .ClearAll
        .Add _
            Position:=TextWidth, _
            Alignment:=wdAlignTabRight, _
            Leader:=wdTabLeaderSpaces
    End With

    With ActiveDocument.Styles("_Header")
        .AutomaticallyUpdate = False
        .ParagraphFormat.TabStops.ClearAll
    End With
    
    With ActiveDocument.Styles("_Footer").Font
        .Italic = False
    End With
    
    ' Page numbers no longer put in the centre,
    ' because long booknames will mess up.
    '
    ActiveDocument.Styles("_Footer").ParagraphFormat.TabStops.Add Position:= _
        TextWidth / 2, Alignment:=wdAlignTabCenter, Leader:= _
        wdTabLeaderSpaces
    ActiveDocument.Styles("_Header").ParagraphFormat.TabStops.Add Position:= _
        TextWidth, Alignment:=wdAlignTabRight, Leader:= _
        wdTabLeaderSpaces
    
    ' Because we don't want ID footers in half-page booklets,
    ' remove the border line above the footers.
    '
    With ActiveDocument.Styles("_Footer").ParagraphFormat
        .Borders(wdBorderLeft).LineStyle = wdLineStyleNone
        .Borders(wdBorderRight).LineStyle = wdLineStyleNone
        .Borders(wdBorderTop).LineStyle = wdLineStyleNone
        .Borders(wdBorderBottom).LineStyle = wdLineStyleNone
    End With
    
    ' Also set the first page footer & clear any other footers that are there already.
    '

    For SectionNr = 1 To NrSections
        Set rng = ActiveDocument.Sections(SectionNr).Footers(wdHeaderFooterFirstPage).Range
        rng.Text = vbTab
        rng.Collapse wdCollapseEnd
        rng.Fields.Add rng, wdFieldPage
        rng.Style = ActiveDocument.Styles("_Footer")
        
        ' Now clear the other footers in the section.
        '
        ActiveDocument.Sections(SectionNr).Footers(wdHeaderFooterEvenPages).Range.Text = ""
        ActiveDocument.Sections(SectionNr).Footers(wdHeaderFooterPrimary).Range.Text = ""
    Next SectionNr
        
        ActiveWindow.ActivePane.View.Type = PageOrPrintView
    WordBasic.ShowAll 1
    
    WordBasic.StartOfDocument
End Sub
Sub PictureAtLeftMargin()
'
' PictureAtLeftMargin Macro
' Macro recorded 3/16/2006 by Jim Henderson
'
    If PictureTextBoxSetup Then ' Set main characteristics of text box
        Selection.ShapeRange.Left = 0
    End If
End Sub
Sub PictureAtRightMargin()
'
' PictureAtRightMargin Macro
' Macro recorded 3/16/2006 by Jim Henderson
'
    Dim TextWidth As Single
    Dim ColumnSpacing As Single
    
    If PictureTextBoxSetup Then ' Set main characteristics of text box
        If Val(Application.Version) >= Wd2000 Then
            ' Place the right edge at the right margin.
            '
            PicAtRight
        Else
            ' Word 97 doesn't have a single value to do that.
            '
            GetTextWidth TextWidth
            Selection.ShapeRange.Left = TextWidth - Selection.ShapeRange.Width
        End If
    End If
End Sub
Sub PictureAtTopMargin()
'
' Macro recorded 17/05/2005 by NTC
'
    Dim TextWidth As Single
    
    If PictureTextBoxSetup Then ' Set main characteristics of text box
        If Val(Application.Version) >= Wd2000 Then
            ' Centre the text box.
            '
            PicAtCenter
        Else
            ' Word 97 doesn't have a single value to do that.
            '
            GetTextWidth TextWidth
            Selection.ShapeRange.Left = (TextWidth - Selection.ShapeRange.Width) / 2
        End If
        
        Selection.ShapeRange.RelativeVerticalPosition = _
            wdRelativeVerticalPositionMargin
        Selection.ShapeRange.Top = CentimetersToPoints(0)
        Selection.ShapeRange.WrapFormat.Type = wdWrapTopBottom
    End If
End Sub
Function PictureTextBoxSetup() As Boolean
'
' First you should set Word options to make it easier to handle pictures
' by doing   SetUpForPicturePlacement
' Then insert a text box where you want the picture,
' then insert the picture in the text box,
' then run this while the text box is still selected.
' It will remove the border and inner margins,
'
' This is called by other macros that modify text boxes so that they are
' at the top of the page, or with text above and below the text box etc.
'
Dim CountOfShapeRanges As Long
Dim NrColumns As Long

    ' Complain and quit if no text box selected.
    '
    On Error GoTo NoTextBoxSelected
    CountOfShapeRanges = Selection.ShapeRange.Count
    
    If CountOfShapeRanges <> 1 _
    Or Selection.ShapeRange.Type <> msoTextBox Then
NoTextBoxSelected:
    On Error GoTo 0
        MsgBox "You need to have a text box selected before you run this macro." & vbCr & vbCr & _
            "Please click inside a picture, then hit an arrow (cursor) key to make" & vbCr & _
            "the fuzzy border around the text box appear," & vbCr & _
            "then run the Picture... macro again.", vbCritical, "ERROR -- No text box selected"
        PictureTextBoxSetup = False
        Exit Function
    End If
    
    On Error GoTo -1
    
    NrColumns = Selection.Sections(1).PageSetup.TextColumns.Count
    
    ' Now to format the text box.
    '
    With Selection.ShapeRange
        .Select
        .Fill.Visible = msoTrue
        .Fill.Solid
        .Fill.ForeColor.RGB = RGB(255, 255, 255)
        .Fill.Transparency = 0#
        .Line.Weight = 0.75
        .Line.DashStyle = msoLineSolid
        .Line.Style = msoLineSingle
        .Line.Transparency = 0#
        .Line.Visible = msoFalse
        .LockAspectRatio = msoFalse
        
        If Val(Application.Version) >= Wd2000 Then
            BoxRelativeToLine
            ' ie. wdRelativeVerticalPositionLine
        Else
            .RelativeVerticalPosition = wdRelativeVerticalPositionParagraph
            ' Can't do it by Line in Wd 97.
        End If
        
        ' Word 97 will crash PositionColumn used in single column.
        ' and Word XP does it relative to page instead of to margin
        '
        If NrColumns > 1 Then
            .RelativeHorizontalPosition = _
                wdRelativeHorizontalPositionColumn
        Else
            .RelativeHorizontalPosition = _
                wdRelativeHorizontalPositionMargin
        End If

        .LockAnchor = False
        .TextFrame.MarginLeft = 0#
        .TextFrame.MarginRight = 0#
        .TextFrame.MarginTop = 0#
        .TextFrame.MarginBottom = 0#
        .WrapFormat.Type = wdWrapSquare
        .WrapFormat.Side = wdWrapLargest
        .WrapFormat.DistanceTop = CentimetersToPoints(0)
        .WrapFormat.DistanceBottom = CentimetersToPoints(0)
        .WrapFormat.DistanceLeft = CentimetersToPoints(0.2)
        .WrapFormat.DistanceRight = CentimetersToPoints(0.2)
    End With
    PictureTextBoxSetup = True
End Function
Sub PictureWithTextAboveAndBelow()
'
' PictureWithTextAboveAndBelow Macro
' Macro recorded 11/05/2005 by Jim Henderson
'
    Dim TextWidth As Single
    
    If PictureTextBoxSetup Then ' Set main characteristics of text box
        If Val(Application.Version) >= Wd2000 Then
            ' Centre the text box.
            '
            PicAtCenter
        Else
            ' Word 97 doesn't have a single value to do that.
            '
            GetTextWidth TextWidth
            Selection.ShapeRange.Left = (TextWidth - Selection.ShapeRange.Width) / 2
        End If
    
        Selection.ShapeRange.WrapFormat.Type = wdWrapTopBottom
        Selection.ShapeRange.WrapFormat.DistanceTop = CentimetersToPoints(0.2)
        Selection.ShapeRange.WrapFormat.DistanceBottom = CentimetersToPoints(0.2)
    End If
End Sub
Sub PictureWithTextBesideIt()
'
' PictureWithTextBesideIt Macro
' "&chr(10)&"Macro recorded 20/02/02 by Jim Henderson
'
' First you should set Word options to make it easier to handle pictures
' by doing   SetUpForPicturePlacement
' Then insert a text box where you want the picture,
' then insert the picture in the text box,
' then run this while the text box is still selected.
' It will remove the border and inner margins,
' then set the properties of the text box so that the text flows around it
' on the side with more space, so you can drag the text box to a margin.
'
' This is called by other macros that modify text boxes so that they are
' at the top of the page, or with text above and below the text box.
'
    If PictureTextBoxSetup Then ' Set main characteristics of text box
        With Selection.ShapeRange
            .RelativeHorizontalPosition = _
                wdRelativeHorizontalPositionColumn
            .WrapFormat.Type = wdWrapSquare
            .WrapFormat.Side = wdWrapLargest
        End With
    End If
End Sub
Sub PseudoHideSelectedParagraph(StyleName As String)
    With ActiveDocument.Styles(StyleName).Font
        .Size = 1
        .ColorIndex = HiddenColor
    End With
    With ActiveDocument.Styles(StyleName).ParagraphFormat
        .LineSpacingRule = wdLineSpaceSingle
        .SpaceAfter = 0
        .SpaceBefore = 0
    End With
    
    Selection.Style = ActiveDocument.Styles(StyleName)
End Sub
Sub Quotes_MakeSmart()
' ConvertToSmartQuotes, Created by Nico Doelman, Suriname Branch
' Version 1.0 for WinWord 6.0: December 16, 1993
'  Summer Institute of Linguistics
' See also AdjustHardReturns macro
' autoformat
' Run Tools->Autoformat to turn ordinary quotes into smart quotes.
'
    Dim toafdlg As Object
    With Options
        .AutoFormatReplaceQuotes = True
    End With
    
    Set toafdlg = WordBasic.DialogRecord.WW7_ToolsOptionsAutoFormat(False)
    WordBasic.CurValues.WW7_ToolsOptionsAutoFormat toafdlg
    WordBasic.WW7_ToolsOptionsAutoFormat PreserveStyles:=0, ApplyStylesHeadings:=0, ApplyStylesLists:=0, ApplyStylesOtherParas:=0, AdjustParaMarks:=0, AdjustTabsSpaces:=0, AdjustEmptyParas:=0, ReplaceQuotes:=1, ReplaceSymbols:=0, ReplaceBullets:=0
    WordBasic.FormatAutoFormat
    WordBasic.WW7_ToolsOptionsAutoFormat toafdlg
End Sub
Sub ReduceMultipleSpaces()
    ' Reduce multiple spaces in the selection to one
    '
    SetStandardFindOptions
    
    With Selection.Find
        ' .Text = " {1,99}"
        .Text = " {1" & Application.International(wdListSeparator) & "99}"

        .Replacement.Text = " "
        .MatchWildcards = True
        .Wrap = wdFindStop
        .Execute Replace:=wdReplaceAll
    End With
    
    SetStandardFindOptions
End Sub
Sub RemoveFrontMatter()
    If VariableGet("FrontMatter") = "done" Then
        If ActiveDocument.Sections.Count > 1 Then
            ActiveDocument.Sections(1).Range.Delete
        End If
        VariableSet "FrontMatter", ""
    End If
End Sub
Sub RemoveSectionBreaks()
    ' So page headers and footers can be remade
    '
    ActiveDocument.StoryRanges(wdMainTextStory).Select
    Selection.HomeKey Unit:=wdStory
    
    If ActiveWindow.View.SplitSpecial = wdPaneNone Then
        ActiveWindow.ActivePane.View.Type = wdNormalView
    Else
        ActiveWindow.View.Type = wdNormalView
    End If
    
    SetStandardFindOptions
    With Selection.Find
        .Text = "^b"
    End With
    
    Selection.Find.Execute Replace:=wdReplaceAll
    
    ' Now remove "_ibr ..." lines
    '
    Selection.Find.Style = ActiveDocument.Styles( _
        "_ibr - Introduction - Blank Line - Rule")
    Selection.Find.Replacement.ClearFormatting
    
    With Selection.Find
        .Text = ""
        .Format = True
    End With
    Selection.Find.Execute Replace:=wdReplaceAll
    
    Selection.HomeKey Unit:=wdStory
End Sub
Sub RemoveSpaceBeforeBookName()
    ' Remove the space between the front matter and the BookName bookmark.
    '
    Dim ThisBookmark As Bookmark

    For Each ThisBookmark In ActiveDocument.Bookmarks
        If ThisBookmark.Name = "BookName" Then
            ThisBookmark.Select
            Selection.Collapse
            If Selection.Start > 0 Then
                Selection.Start = Selection.Start - 1
                If Selection.Text = " " Then Selection.Delete
                Exit For
            End If
        End If
    Next ThisBookmark
End Sub
Sub RemoveTocLines()
'
' RemoveTocLines Macro
' Macro recorded 28-Jan-2017 by Jim
'
' For each paragraph,
'   if its style begins with "toc",
'      remove the paragraph.
'
    Dim para As Paragraph
    Dim rPara As Range

    Application.StatusBar = "Removing paragraphs in toc styles -- please wait."
    
    ActiveWindow.View.Type = wdNormalView
    Application.ScreenUpdating = False
    
    For Each para In ActiveDocument.Paragraphs
        If para.Style = "toc1 - File - Long Table of Contents Text" _
        Or para.Style = "toc2 - File - Short Table of Contents Text" _
        Or para.Style = "toc3 - File - Book Abbreviation" Then
            para.Range.Delete
        End If
    Next para
    
    SetDefaultView
    Application.ScreenUpdating = True
    Application.StatusBar = " "
End Sub
Sub ResetParaMarks()
'
' ResetParaMarks Macro
' Macro recorded 5/31/2010 by Jim Henderson
'
' For each paragraph mark,
'   remove any character style on it.
'
    Dim para As Paragraph
    Dim rPara As Range

    Application.StatusBar = "Removing character styles from paragraph marks -- please wait."
    
    For Each para In ActiveDocument.Paragraphs
        Set rPara = para.Range.Duplicate
        rPara.Start = rPara.End - 1
        
        If rPara.Style <> rPara.Paragraphs(1).Style Then
            rPara.Font.Reset
        End If
    Next para
    
    Application.StatusBar = " "
End Sub
Sub ReplaceAllInRange( _
    ByRef rngStory As Range, _
    sFrom As String, sTo As String, _
    sStatusMessage As String)
'
' Throughout the rngStory,
'   Change sFrom to sTo
'   while displaying sStatusMessage.
'
    Dim rngThisStory As Range
    
    Application.StatusBar = sStatusMessage
    Application.ScreenUpdating = False
    SetStandardFindOptions
    
    Set rngThisStory = rngStory.Duplicate
    rngThisStory.Select

    With Selection.Find
        .Text = sFrom
        .Replacement.Text = sTo
    End With
    Selection.Find.Execute Replace:=wdReplaceAll
End Sub
 Sub RemDirFmt()
    ReplaceAlmostEverywhere iResetFont
End Sub
Sub ReplaceAlmostEverywhere(Job As Integer)
' Step through each of the story types, to change things that need to be changed throughout.
' Depends on global constants to know which function to call in each story.
'
' Code is adapted from http://word.mvps.org/faQs/Customization/ReplaceAnywhere.htm
' assuming that people won't have text boxes in page headers.
'
  Dim rngStory As Word.Range
  Dim lngJunk As Long
  
  ActiveWindow.View.Type = wdNormalView
  Application.ScreenUpdating = True
  'Fix the skipped blank Header/Footer problem as provided by Peter Hewett
  lngJunk = ActiveDocument.Sections(1).Headers(1).Range.StoryType
  'Iterate through all story types in the current document
  '
  For Each rngStory In ActiveDocument.StoryRanges
    ' Don't do page headers and footers,
    ' because they are just fields like PAGE and STYLEREF.
    '
    If rngStory.StoryType > wdTextFrameStory Then Exit For
    
    Do
        Select Case Job
            Case iFixQuotes
            FixQuotesInternal rngStory
        
        Case iNoBreakHyphens
            NonBreakingHyphens rngStory
        
        Case iHyphens2Spaces
            Hyphens2SpacesInternal rngStory
        
        Case iBrackets2HalfBrackets
            ReplaceAllInRange rngStory, _
                "[", ChrW(763), _
                "Changing brackets to half brackets -- please wait."

            ReplaceAllInRange rngStory, _
                "]", ChrW(764), _
                "Changing brackets to half brackets -- please wait."

        Case iSpecialCharacters
            FixSpecialCharacters rngStory
            
        Case iResetFont
            ClearDirectFormatting rngStory, "Removing direct formatting -- please wait."
            ' If Application.Version >= Wd2007 Then
            '     ClrChrDirFmt rngStory
            ' End If
            ' rngStory.Font.Reset ' Remove any explicit formatting.
            
        End Select

      'Get next linked story (if any)
      Set rngStory = rngStory.NextStoryRange
    Loop Until rngStory Is Nothing
  Next
  
  Set rngStory = ActiveDocument.StoryRanges(wdMainTextStory)
  rngStory.Select
  Selection.HomeKey Unit:=wdStory
  SetStandardFindOptions
  Application.ScreenUpdating = True
  Application.StatusBar = " "
End Sub
Sub ReportProblem()
    ' Prepare a document in which the user can write an error report
    '
    Dim FileName As String
    
    ' Get the language, project or code for the current document first.
    '
    F_FindLanguageOrProjectName FileSaveOps.ErrorReportName
    FileSaveOps.ErrorReportName = "[SILAS] " & FileSaveOps.ErrorReportName
    
    ' Now make and open the report document.
    '
    Documents.Add
    
    Selection.TypeText Text:="Problem report with Silas,"
    Selection.TypeParagraph
    Selection.TypeText Text:="  Smart Interactive Layout Assistant for Scripture"
    Selection.TypeParagraph
    Selection.TypeText Text:="=================================================="
    Selection.TypeParagraph
    Selection.TypeText Text:="From:"
    Selection.TypeParagraph
    Selection.TypeText Text:="  Name: "
    Selection.TypeParagraph
    Selection.TypeText Text:="  Email address: "
    Selection.TypeParagraph
    
    Selection.TypeParagraph
    Selection.TypeText Text:="Please answer the following questions:"
    Selection.TypeParagraph
    Selection.TypeText Text:="What did you do and what happened?"
    Selection.TypeParagraph
    Selection.TypeText Text:="  (eg. Exported text from Paratext and used the Tools menu item to attach Silas.)"
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.TypeText Text:="What did you expect to happen?"
    Selection.TypeParagraph
    Selection.TypeText Text:="  (eg. Silas should have recognised the language name from the ID line.)"
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.TypeText Text:="Please be very specific about error messages -- "
        Selection.TypeParagraph
    Selection.TypeText Text:="the exact wording or a screen shot help a lot."
    Selection.TypeParagraph
    Selection.TypeText Text:="To send a screenshot, operate the Print Screen key"
    Selection.TypeParagraph
    Selection.TypeText Text:="(which sometimes requires that you hold down a key called 'Fn' at the seme time.)"
    Selection.TypeParagraph
    Selection.TypeText Text:="This will put a screenshot in the clipboard."
    Selection.TypeParagraph
    Selection.TypeText Text:="Do START->Run->mspaint, then in that program, do Ctrl-V to paste in the screenshot."
    Selection.TypeParagraph
    Selection.TypeText Text:="Do File->Save As and choose the type PNG (Portable Network Graphics)"
    Selection.TypeParagraph
    Selection.TypeText Text:="and attach that file to your email message along with this document."
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.TypeText Text:="Word reports the computer operating system as " & _
        System.OperatingSystem & "  " & _
        System.Version
    Selection.TypeParagraph
    Selection.TypeText Text:="Version of MS Word reported as "
    Selection.TypeParagraph
    Selection.TypeText Text:="    " & _
        Application.Name & " " & _
        Application.Version & " Build: " & _
        Application.Build & " "
    
    If Application.Version >= Wd2000 Then VBA9plus.AddProductCode
    
    Selection.TypeParagraph
    Selection.TypeText Text:="Version of Silas reported as " & _
        GenSilasVersion & "-" & _
        GenSilasBuild
    Selection.TypeParagraph
    
    ' Save the file as a text document.
    '
    F_FileSaveAsEither AsDoc:=False, IsError:=True
    
    Selection.EndKey Unit:=wdStory
    Selection.TypeParagraph
    Selection.TypeText Text:="(We saved this form with the filename"
    Selection.TypeParagraph
    Selection.TypeText Text:="    " & FileSaveOps.DataDocumentName
    Selection.TypeParagraph
    Selection.TypeText Text:="in the folder"
    Selection.TypeParagraph
    Selection.TypeText Text:="    " & ActiveDocument.Path & ")"
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.TypeText Text:="When you have filled in this form, hold down the Ctrl key and hit S to save it,"
    Selection.TypeParagraph
    Selection.TypeText Text:="then please send it to BOTH of:"
    Selection.TypeParagraph
    Selection.TypeText Text:="kblewett@Gmail.com; Jim.Henderson44@Gmail.com"
    Selection.TypeParagraph
    Selection.TypeParagraph
    Selection.TypeText Text:="If this computer has access to email, you can do "
    Selection.TypeParagraph
    Selection.TypeText Text:="File->Send to->Mail recipient (as attachment),"
    Selection.TypeParagraph
    Selection.TypeText Text:="otherwise find this file at the location above and send it from another computer."
    Selection.TypeParagraph
    
    ActiveDocument.Save
    
    ' Restore filename used by USFM2Styles.
    '
    FileSaveOps.DataDocumentName = FileSaveOps.DataDocumentNameRsv

    SetStandardFindOptions
    With Selection.Find
        .Text = "Problem report with Silas"
        .Replacement.Text = ""
        .Forward = False
        .Wrap = wdFindStop
    End With
    Selection.Find.Execute
End Sub
Sub RevealIDetc()

    ' Reveal Identification style(s), and BookName styles and \glo styles
    ' for editing.
    '
    Dim Found As Boolean
    Dim IDStyle As Boolean
    Dim BookStyle As Boolean
    Dim ObsoletePeriphStyle As Boolean
    Dim JmpStyle As Boolean     ' for hypertext links out of Paratext
    Dim StyleName As String
    Dim i As Long
    
    If VariableGet("IDLines") = "revealed" Then Exit Sub
    
    SetStandardFindOptions
    ZoomSet
    ActiveDocument.ActiveWindow.View.Type = wdNormalView
    WordBasic.ShowAll 1
        
    Application.StatusBar = "               Revealing ID lines etc -- please wait."
    Application.ScreenUpdating = False
    
    Selection.Find.ClearFormatting
    
    With Selection.Find
        .Text = ""
        .Replacement.Text = ""
        .Format = True
        .Forward = True
        .Wrap = wdFindContinue
    End With
    
    For i = 1 To ActiveDocument.Styles.Count
        StyleName = ActiveDocument.Styles(i)
        IDStyle = StyleName Like "id*File*"
        BookStyle = StyleName Like "h*Header"
        ObsoletePeriphStyle = StyleName Like "OBSOLETE *Peripherals - *"
        JmpStyle = StyleName Like "jmp - link"
        
        If IDStyle Or BookStyle Or ObsoletePeriphStyle Then
            Selection.Find.Style = ActiveDocument.Styles(i)
            
            Selection.HomeKey Unit:=wdStory
    
            Found = Selection.Find.Execute
            
            If Found Then
                If ActiveDocument.Styles(i).Font.Hidden = True Then
                    ActiveDocument.Styles(i).Font.Hidden = False
                End If
                
                With ActiveDocument.Styles(i).Font
                    .Size = 12
                    .ColorIndex = wdBlack
                End With
                
                Selection.Style = ActiveDocument.Styles(i)
            End If
        End If
        
        ' If the file might have Paratext-style hypertext links in it,
        '   reveal the mechanism for getting to the target.
        '
        If JmpStyle Then
            Application.ScreenUpdating = True
            Application.StatusBar = "  Revealing details of link targets in style '" & StyleName & "', please wait."
            Selection.Find.Style = ActiveDocument.Styles(i)
            
            Selection.HomeKey Unit:=wdStory
            Application.ScreenUpdating = False
    
            Do
                Found = Selection.Find.Execute
            
                If Found Then
                    ' Reveal from the first vertical bar to the end of the selection.
                    '
                    Selection.Range.Font.Hidden = False
                End If
            Loop While Found
        End If
    Next i
    
    SetStandardFindOptions
    SetDefaultView
    Application.StatusBar = "                                                     "
    Application.ScreenUpdating = True
    WordBasic.ShowAll 0
    VariableSet "IDLines", "revealed"
End Sub
Sub SetDefaultFont()
Dim IniFont As String
Dim FileFont As String
Dim ChangeFont As String
Dim tmpLanguageName As String
Dim Msg As String, Title As String, Style As Long, Response As Integer

    'First check to see if they've set "Don't Ask" for PTNoFontChange
    ChangeFont = VariableGet("PTNoFontChange")
    If ChangeFont = "True" Then GoTo EndNoChange

    IniFont = VariableGet("LanguageFont")
    FileFont = VariableGet("PTFont")
    If FileFont = "" Then FileFont = ActiveDocument.Styles("_Vernacular_Base").Font.Name
    tmpLanguageName = VariableGet("LanguageName")
    
    If IniFont = "" Or IniFont = " " Then
        VariableSet "LanguageFont", FileFont
        Msg = "The font you are using in " & VariableGet("ExportedFrom") & _
            "for this Scripture text is:" & FileFont & "." & _
            vbCrLf & "Is this the font you want to use for printouts in " & tmpLanguageName & " language?" & vbCrLf & _
            "If you select 'No' here, please choose a font and check your other language settings in the Language Information window, next."
        Style = vbYesNo
        Title = "Language Font Setup 1"
        Response = MsgBox(Msg, Style, Title)
        If Response = vbNo Then ShowLanguageInfoForm
    
    
    ElseIf IniFont = FileFont Or FileFont = "" Then
        GoTo EndNoChange
    
    Else
        With dbPTFont
            .txtPTFont = FileFont
            .txtLang1 = tmpLanguageName
            .txtLang2 = tmpLanguageName
            .txtLangFont1 = IniFont
            .txtLangFont2 = IniFont
            .cbDontAskFont = False
            .Show
        End With
        
    End If
EndNoChange:
    VariableSet "PTFont", ""
End Sub
Sub SetDefaultPaperSize()
' first check to see if Language is set
'
Dim tmpCountry As String
Dim TextWidth As Single
Dim Msg As String, Style As Long, Title As String, Result As Integer

If VariableGet("InstallWarning") = "True" Then Exit Sub
If VariableGet("NoLanguageSet") = "True" Then GoTo FindSize

' then check to see if value PaperSize:Size is in Languages.ini
Dim tmpPaperSize As String
    
    SetIniFile  ' also sets PtSettingsDir
    ActiveWindow.ActivePane.View.Type = wdNormalView
    
'Check Inifile for paper size setting
    Set fs = CreateObject("Scripting.FileSystemObject")
    If Not fs.FileExists(IniFile) Then
        'Ask if they want Inifile created automatically
        '
        Msg = "I can't find the file: " & LanguageFileName & " in " & vbCrLf & _
        vbTab & PtSettingsDir & vbCrLf & vbCrLf & _
        "Shall I create it, or do you want to search for this file?" & vbCrLf & _
        "It must be placed in your Paratext settings folder." & vbCrLf & _
        "If you click OK I'll set up " & LanguageFileName & " in " & PtSettingsDir _
        & vbCrLf & vbCrLf & "If you know that you have settings for several languages already in this file," & vbCrLf & _
        "you may want to find the file yourself or manually restore a backup. If so, click Cancel."
        Style = vbCritical + vbOKCancel
        Title = "Languages.ini Not Found (SetDefPaperSize)"
    
        'if they say no then exit this sub
        Result = MsgBox(Msg, Style, Title)
        
        If Result = vbCancel Then Exit Sub
    
        'if they say OK, then create Ini file
        If Result = vbOK Then LangData.CreateIniFile
    End If
    
' Get papersize from Country in Inifile
        tmpCountry = VariableGet("LanguageCountry")
        tmpPaperSize = System.PrivateProfileString( _
            FileName:=IniFile, Section:="DefaultPaperSize", _
            Key:=tmpCountry)
        VariableSet "DefaultPaperSize", tmpPaperSize
        GoTo FindSize
    
FindSize:
    If tmpPaperSize = "" Or tmpPaperSize = " " Then
        If VariableGet("NoLanguageSet") = "True" Then
            Msg = "You must first assign a language to this Scripture text file. To do this, " & _
                "please open the " & Chr(34) & "ScrLanguages" & Chr(34) & _
                " menu and do the job called " & Chr(34) & "Set Language Project For This Text." & Chr(34)
            Title = "No Language Set (SetDefPaperSize)"
            Result = MsgBox(Msg, , Title)
                CancelFormatting = True
            GoTo EndSub2
        End If
        If tmpCountry = "" Or tmpCountry = " " Then
        
            MsgBox "It looks as if the settings for " & Chr(34) & VariableGet("LanguageName") & _
                Chr(34) & " Language Project " & _
                " has nothing in the " & Chr(34) & "Country" & Chr(34) & " setting box. " & _
                "Please open the " & Chr(34) & "ScrLanguages" & Chr(34) & _
                " menu and do the job called " & Chr(34) & "Change Settings For A Language Project." & Chr(34)
                CancelFormatting = True
                GoTo EndSub2

        Else:
            If MsgBox("I can't find a default paper size associated with the country " & tmpCountry & _
                 "." & vbCrLf & "In the next box, please select the standard paper size used in this country.", _
                 vbOKCancel) = vbCancel Then
               CancelFormatting = True
               GoTo EndSub2
            End If
        End If
        ShowDbPaperSize
        
        tmpPaperSize = System.PrivateProfileString( _
            FileName:=IniFile, Section:="DefaultPaperSize", _
            Key:=tmpCountry)
    End If
    
    VariableSet "DefaultPaperSize", tmpPaperSize

    If VariableGet("DefaultPaperSize") = "A4" Then
        With ActiveDocument.PageSetup
            .LineNumbering.Active = False
            .Orientation = wdOrientPortrait
            .TopMargin = InchesToPoints(0.6)
'            .BottomMargin = InchesToPoints(0.6)
            .BottomMargin = InchesToPoints(DefaultBottomMargin) ' allow space for footer, without checking table
            .LeftMargin = InchesToPoints(1#)
            .RightMargin = InchesToPoints(1#)
            .gutter = InchesToPoints(0)
            .HeaderDistance = InchesToPoints(0.3)
            .FooterDistance = InchesToPoints(0.6)
            .PageWidth = InchesToPoints(8.27)
            .PageHeight = InchesToPoints(11.69)
            .FirstPageTray = wdPrinterDefaultBin
            .OtherPagesTray = wdPrinterDefaultBin
            .SectionStart = wdSectionNewPage
            .OddAndEvenPagesHeaderFooter = True
            .DifferentFirstPageHeaderFooter = True
            .VerticalAlignment = wdAlignVerticalTop
            .SuppressEndnotes = True
            .MirrorMargins = True
        End With
'        Exit Sub
        
    ElseIf VariableGet("DefaultPaperSize") = "Letter" Then

        With ActiveDocument.PageSetup
            .LineNumbering.Active = False
            .Orientation = wdOrientPortrait
            .TopMargin = InchesToPoints(0.6)
'            .BottomMargin = InchesToPoints(0.6)
            .BottomMargin = InchesToPoints(DefaultBottomMargin) ' allow space for footer, without checking table
            .LeftMargin = InchesToPoints(1#)
            .RightMargin = InchesToPoints(1#)
            .gutter = InchesToPoints(0)
            .HeaderDistance = InchesToPoints(0.3)
            .FooterDistance = InchesToPoints(0.6)
            .PageWidth = InchesToPoints(8.5)
            .PageHeight = InchesToPoints(11)
            .FirstPageTray = wdPrinterDefaultBin
            .OtherPagesTray = wdPrinterDefaultBin
            .SectionStart = wdSectionNewPage
            .OddAndEvenPagesHeaderFooter = True
            .DifferentFirstPageHeaderFooter = True
            .VerticalAlignment = wdAlignVerticalTop
            .SuppressEndnotes = True
            .MirrorMargins = True
        End With
    End If
    
    ' Footer should have only one tab stop, at the right margin.
    CancelFormatting = False
    ActiveWindow.ActivePane.View.Type = PageOrPrintView
    With ActiveDocument.PageSetup
        TextWidth = .PageWidth - .LeftMargin - .RightMargin - .gutter
    End With

    With ActiveDocument.Styles("_Footer")
        .AutomaticallyUpdate = False
        .ParagraphFormat.TabStops.ClearAll
    End With
    
    ActiveDocument.Styles("_Footer").ParagraphFormat.TabStops.Add _
        Position:=TextWidth, Alignment:=wdAlignTabRight, _
        Leader:=wdTabLeaderSpaces

    ' Header the same, so there is plenty of room for long booknames.
    '
    With ActiveDocument.Styles("_Header")
        .AutomaticallyUpdate = False
        .ParagraphFormat.TabStops.ClearAll
    End With
    
    ActiveDocument.Styles("_Header").ParagraphFormat.TabStops.Add _
        Position:=TextWidth, Alignment:=wdAlignTabRight, _
        Leader:=wdTabLeaderSpaces
EndSub2:
    If Not FormattingInProgress Then
        OptionalLineBreaks
    End If
End Sub
Sub SetDefaultView()
'
' SetDefaultView Macro
' Macro recorded 6/12/2006 by Jim Henderson
'
    If ActiveWindow.View.SplitSpecial = wdPaneNone Then
        ActiveWindow.ActivePane.View.Type = PageOrPrintView
    Else
        ActiveWindow.View.Type = PageOrPrintView
    End If
    ZoomRecall
    Selection.HomeKey Unit:=wdStory
End Sub
Sub SetDropCapSize()
    Dim Found As Boolean
    Dim DropcapDigitPointSize As Integer
    Dim DropcapPosition As Integer
    
    If VariableGet("ChapterStyle") = "drop" Then
        Application.StatusBar = "               Resetting the sizes of dropcaps -- please wait."
        Application.ScreenUpdating = False
        Selection.HomeKey Unit:=wdStory
        
        SetStandardFindOptions
        
        ' Get the line spacing for sizing dropcap chapter numbers properly.
        '
        With ActiveDocument.Styles("_BodyText_Base").ParagraphFormat
            DropcapDigitPointSize = .LineSpacing * 2.2    ' Two lines for dropcap
            
            If .LineSpacingRule = wdLineSpaceExactly Then
                DropcapPosition = -2
            Else
                DropcapPosition = 0
            End If
        End With
        
        Selection.Find.ClearFormatting
        
        With Selection.Find
            .Format = True
            .Wrap = wdFindStop
        End With
        
        Do
            Selection.Find.Style = "_Chapter_Drop"
            Found = Selection.Find.Execute
            
            If Found _
            And Selection.Text Like "[0-9]*" Then
                With Selection.Paragraphs(1).DropCap
                    .LinesToDrop = 2#
                End With
            
                ' Now to set the pointsize of the digits.
                '
                With Selection.Font
                    .Size = DropcapDigitPointSize
                    .Position = DropcapPosition
                End With
            
            End If
        Loop While Found
        
        'reset "c - Chapter Number" to LineSpacing=0
        '
        With ActiveDocument.Styles(StdChapterStyle).ParagraphFormat
           .LineSpacingRule = wdLineSpaceExactly
           .LineSpacing = 1 ' minimum in Word 2007 is 0.7, but 1 better for new space-before method
           .SpaceAfter = 0
           .SpaceBefore = 0
        End With

        Application.StatusBar = "                                                     "
        SetDefaultView
        Application.ScreenUpdating = True
        WordBasic.ShowAll 0
    End If
    
    SetStandardFindOptions
End Sub
Public Sub SetIniFile()
    If PtSettingsDir = "" Then
        If CheckPTPath = "bad" Then
            Lib1.EndMacro
        End If
    End If
    IniFile = PtSettingsDir & LangIniFile
End Sub
Public Function SetPathVariables() As Boolean
    Dim fs As Object
    Dim TemplateLocation As String
    
    SetPathVariables = False    ' assume the worst
    Set fs = CreateObject("Scripting.FileSystemObject")
    IniPT = NormalTemplate.Path & Application.PathSeparator & IniPTname
    
    If Not fs.FileExists(IniPT) Then
        MsgBox _
            Title:="'ParatextPath.ini' missing", _
            Prompt:="I expected to find a file called '" & _
                IniPTname & "' in the folder" & strSpaces & vbCr & _
                "where Word looks for 'User Templates' " & vbCr & _
                "('" & NormalTemplate.Path & "').                 " & vbCr & vbCr & _
                strGeneralReinstall
        Exit Function
    End If
    
    PtSettingsFolderFromIni = System.PrivateProfileString(FileName:=IniPT, _
                Section:="PTProjects", Key:="Path")
    CheckPath PtSettingsFolderFromIni
    InstallFolder = System.PrivateProfileString(FileName:=IniPT, _
                Section:="template", Key:="InstallFolder")
    CheckPath InstallFolder

GetPtPathFromReg:
    On Error GoTo 0
    PtSettingsPathFromReg = System.PrivateProfileString("", _
        "HKEY_LOCAL_MACHINE\SOFTWARE\ScrChecks\1.0\Settings_Directory", "")
    CheckPath PtSettingsPathFromReg
    
    If CheckPTPath = "bad" Then Exit Function
    
    If CheckForNewVersionB = True Then
        If UseUserTemplates Then
            TemplateLocation = "Paratext settings"
        Else
            TemplateLocation = "templates"
        End If

        MsgBox "It looks as if you have recently copied a new version of the Scripture template " & strSpaces & vbCrLf & _
         "into your " & TemplateLocation & " folder, or else your languages settings file, " & vbCrLf & _
         vbTab & IniFile & vbCrLf & "has been deleted or corrupted." & vbCrLf & vbCrLf & _
         "You need to install this template properly or it won't work for you. " & vbCrLf & vbCrLf & _
         strGeneralReinstall, _
         vbOKOnly, "New Version Install"
        VariableSet "InstallWarning", "True"
    Else
        SetPathVariables = True
    End If
End Function
Sub SetStandardFindOptions()
    Selection.Find.ClearFormatting
    Selection.Find.Replacement.ClearFormatting
    With Selection.Find
        .Text = ""
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindContinue
        .Format = False
        .MatchCase = False
        .MatchWholeWord = False
        .MatchWildcards = False
        .MatchSoundsLike = False
        .MatchAllWordForms = False
        
        If Val(Application.Version) >= WdXP Then
            AdvancedSearchOptions
        End If
    End With
    Application.Browser.Target = wdBrowsePage
End Sub
Sub SetUpForInsertingPictures()
    ActiveWindow.ActivePane.View.Type = PageOrPrintView
    
    With ActiveWindow
        With .View
            .ShowPicturePlaceHolders = False
            .ShowFieldCodes = False
            .ShowBookmarks = False
            .FieldShading = wdFieldShadingWhenSelected
            .ShowDrawings = True
            .ShowObjectAnchors = True
            .ShowTextBoundaries = True
        End With
    End With
    
    With Options
        .GridDistanceHorizontal = MillimetersToPoints(0.5)
        .GridDistanceVertical = MillimetersToPoints(0.5)
    End With
End Sub
Sub SetUpForViewingText()
    With ActiveWindow
        With .View
            .ShowTextBoundaries = False
        End With
    End With
End Sub
Sub ShowCodesInSelection()
    Dim rWhole As Range
    Dim n As Long
    Dim Char As String
    Dim Msg As String
    Dim EndPoint As Long

    EndPoint = Selection.End - Selection.Start
    
    If EndPoint > 40 Then
        EndPoint = 40
        Msg = "Number of characters to show limited to 40." & vbCrLf & vbCrLf
    End If
    
    Msg = Msg & "Char" & vbTab & "Code" & vbTab & "Style" & vbCrLf
    
    For n = 1 To Selection.Characters.Count
        Char = Selection.Characters(n)
        
        If AscW(Char) >= 0 And AscW(Char) < 32 Then
            Char = "Ctrl-" & Chr(64 + Asc(Char))
        End If
        
        Msg = Msg & "_" & Char & "_" & vbTab & _
            Hex(AscW(Selection.Characters(n))) & vbTab & Selection.Characters(n).Style & vbCrLf
    Next n
    
    MsgBox _
        Prompt:=Msg, _
        Buttons:=vbInformation, _
        Title:="SILAS macro 'ShowCodesInSelection'"
End Sub
Private Sub ShowDbPaperSize()

    If VariableGet("LanguageCountry") <> "" Then
        dbPaperSize.Show
    Else:
        MsgBox "This Scripture text does not have an associated language, " & _
            "or else 'Country' is missing from the settings file." & vbCrLf & _
            "Please open the 'ScrLanguages' menu and select a language from " & _
            "'Set Language Project For This Text' to choose a language " & _
            "or add a new one. Make sure the language you choose " & _
            "has a country in its settings.", vbCritical
        VariableSet "NoLanguageSet", "True"
        Exit Sub
    End If
End Sub
Sub ShowAddPicturesDoc()
    If SetPathVariables = False Then Exit Sub
    Documents.Open _
        FileName:=InstallFolder & "Adding pictures to Scripture files in Word.doc", _
        ReadOnly:=True
End Sub
Sub ShowBookletMacrosDoc()
    Dim WordMacrosDoc As String

   If SetPathVariables <> True Then Exit Sub
    
    WordMacrosDoc = _
        "Booklet_Macros\Word Booklet Macros" & InstallingBookletVersion & ".doc"
    
    On Error GoTo -1: On Error GoTo CantOpenBookletMacros
    Documents.Open _
        FileName:=InstallFolder & WordMacrosDoc, _
        ReadOnly:=True
    Documents(InstallFolder & WordMacrosDoc).Activate
    On Error GoTo 0
    Exit Sub
    
CantOpenBookletMacros:
    MsgBox _
        Title:=strShowBookletMacrosDoc_Title & strSpaces, _
        Prompt:=strShowBookletMacrosDoc_Prompt_1 & strSpaces & _
            InstallFolder & WordMacrosDoc & vbCr & _
            strShowBookletMacrosDoc_Prompt_2 & _
            vbCr & Err.Description & vbCr & vbCr & _
            strGeneralReinstall, _
        Buttons:=vbExclamation
    On Error GoTo 0
End Sub
Sub ShowDocumentVariables()
Dim List As String
Dim v As Variable
    For Each v In ActiveDocument.Variables
        List = List & v.Name & vbTab & "'" & v & "'" & vbCrLf
        Next v
    MsgBox ActiveDocument.Variables.Count & " variables in " & ActiveDocument.Name & vbCrLf & vbCrLf & _
        List, vbOKOnly, "Current variables in this document are"
End Sub
Sub ShowInfoDoc()
    If SetPathVariables = False Then Exit Sub
    Documents.Open _
        FileName:=InstallFolder & "Using Silas.doc", _
        ReadOnly:=True
End Sub
Sub ShowTemplateVersion()
    MsgBox "Silas -- Smart Interactive Layout Assistant for Scripture, version  " & _
        SilasVersion & ", build " & SilasBuild & "." & vbCrLf & _
        "Based on USFM version " & LatestUSFMVersion & vbCr & _
        "The version number of Word is " & Application.Version
End Sub
Sub ShowThisDocumentVariables()
Dim List As String
Dim myvar As Variable
    For Each myvar In ThisDocument.Variables
        List = List & myvar.Name & vbTab & "'" & myvar & "'" & vbCrLf
    Next myvar
    MsgBox ThisDocument.Variables.Count & " variables in " & ThisDocument.Name & vbCrLf & vbCrLf & _
        List, vbOKOnly, "Current variables in this document are"
End Sub
Sub SplitChapter(ByRef rngCl As Range)
    ' In case chapter is like Psalm 23
    '   point cl range argument to the "Psalm " part
    '   and Selection to the "23" part.
    '
    ' Called with whole chapter paragraph selected,
    ' perhaps except for any whitespace after the chapter number.
    '
    ' Returns with Selection on the 23 part,
    ' rngCl on "Psalm " or empty.
    '
    Dim ChapterNameLength As Long
    Dim rWholeChapterPara As Range
    
    Set rngCl = Selection.Range.Duplicate
    Set rWholeChapterPara = Selection.Range.Duplicate
    
    Selection.Start = Selection.End
    
    ' Ignore final whitespace
    '
    Do
        Selection.Start = Selection.Start - 1
        
        If Selection.Text = " " _
        Or Selection.Text = vbCr Then
            Selection.Collapse
            rWholeChapterPara.End = Selection.End
        Else
            Exit Do
        End If
    Loop While Selection.Start > rngCl.Start
    
    Selection.Start = rngCl.Start
    ChapterNameLength = Selection.Characters.Count
    
    If Selection.Text Like "[!0-9]*" _
    And Selection.Characters(ChapterNameLength) Like "[0-9)]" Then
        ' It's like Psalm 23, so get the digits.
        '
        Selection.Start = Selection.End
        
        Do
            Selection.Start = Selection.End - 1
            
            If Not Selection.Text Like "[0-9 ()]" Then
                Selection.Start = Selection.End
                Exit Do
            Else
                Selection.Collapse
            End If
        Loop While Selection.Start > rngCl.Start
    Else
        rngCl.Collapse
    End If
    
    If Selection.Characters(1) = " " Then Selection.Start = Selection.Start + 1
    rngCl.End = Selection.Start
    Selection.End = rWholeChapterPara.End
End Sub
Sub Table_AddTabsToRow(NumCols As Integer)
' Called with a table row in \tr style selected.
' Step through the selection character by character,
' putting a tab character between each field and the next
'
' (While the number in the stylename (such as the 3 in tc3) is higher than expected,
'   add an extra tab.)

' Return with the selection at the end of the original selection
'
Dim NrChars As Long
Dim ColsThisLine As Integer
Dim ColFound As Integer
Dim ColWritten As Integer
Dim i As Long   ' counts characters in the line
Dim ThisCharStyle As String
Dim PrevCharStyle As String

ColsThisLine = 1
ColWritten = 1  ' Assuming the first column is tc1 or similar.
NrChars = Selection.Characters.Count

If NrChars = 1 Then Exit Sub

' PrevCharStyle = Selection.Characters(1).Style
i = 1

Do
    ThisCharStyle = Selection.Characters(i).Style
    
    If ThisCharStyle <> PrevCharStyle Then
        If ThisCharStyle Like "t[ch][0-9] *" Then
            ColFound = Mid(ThisCharStyle, 3, 1)
        ElseIf ThisCharStyle Like "t[ch]r[0-9] *" Then
            ColFound = Mid(ThisCharStyle, 4, 1)
        End If
    
        PrevCharStyle = ThisCharStyle
    End If
    
    Do While ColWritten < ColFound
        Selection.Characters(i).InsertBefore (vbTab)
        i = i + 1
        ColWritten = ColWritten + 1
        ColsThisLine = ColsThisLine + 1
    Loop
        
    i = i + 1
Loop While i < Selection.End - Selection.Start

If ColsThisLine > NumCols Then NumCols = ColsThisLine

End Sub
Sub Table_ConvertRunsOfTableRows()
' This is just the beginning of a macro to format tables nicely.
'
' The final algorithm should be something like this:
'
' Start at the beginning of the file.
' Search for text in \tr paragraph style
' If you find some
' and if it's not part of a table already,
'   mark the start of it
'   step down by paragraphs till you find the end of it and mark that,
'       counting the number of rows as you go, in RowCount.
'   Select all the text you've just moved over
'   Now go through each line, and wherever there is a change in character style
'       (such as from \tc1 to \tc2)
'       put a tab at the change point, counting the columns in NumCols.
'       (While the number in the stylename (such as the 3 in tc3) is higher than expected,
'           add an extra tab.)
'   Reselect the whole table and call Table_ConvertSelectionToTable
'   with number of rows and columns as parameters.
'
    Dim NumCols As Integer
    Dim RowCount As Integer
    Dim RowFound As Boolean
    Dim rTableFound As Range
    Dim StartOfTable As Long
    
    SetStandardFindOptions
    Selection.HomeKey Unit:=wdStory
    StartOfTable = 0
    
    Selection.Find.ClearFormatting
    Selection.Find.Style = ActiveDocument.Styles("tr - Table - Row")
    With Selection.Find
        .Text = ""
        .Replacement.Text = ""
        .Forward = True
        .Wrap = wdFindStop
        .Format = True
    End With
    
    Do
        RowFound = Selection.Find.Execute
        
        If RowFound Then
            If Selection.Information(wdWithInTable) Then
                Selection.Tables(1).Select
                Selection.Start = Selection.End
                GoTo TryNextRow
            End If
        Else
            Exit Do
        End If
        
        If StartOfTable = 0 Then
            StartOfTable = Selection.Start
            NumCols = 0
            RowCount = 0
        End If
        
        Do
            If Selection.Characters.Count = 1 Then
                Selection.EndOf Unit:=wdParagraph, Extend:=wdExtend
            End If
            
            Table_AddTabsToRow NumCols
            Selection.MoveDown Unit:=wdParagraph, Count:=1
            RowCount = RowCount + 1
            If Selection.End >= ActiveDocument.Range.End - 1 Then Exit Do
        Loop While Selection.Paragraphs(1).Style = "tr - Table - Row"

        ' Now select the table we have just moved over
        ' and convert it to a Word table.
        '
        Selection.MoveUp Unit:=wdParagraph, Count:=1
        Selection.EndOf Unit:=wdParagraph, Extend:=wdExtend
        Selection.Start = StartOfTable
        Table_ConvertSelectionToTable NumCols, RowCount
        Selection.Start = Selection.End
        StartOfTable = 0
        
TryNextRow:
        If Selection.End >= ActiveDocument.Range.End - 1 Then Exit Sub
    Loop While RowFound
End Sub
Sub Table_ConvertSelectionToTable(NumCols As Integer, RowCount As Integer)
'
' ConvertSelectionToTable Macro
' Macro recorded 10/14/2006 by Jim Henderson
'
Dim ThisCell As Cell
Dim CellStyle As String

    If RowCount = 0 Or NumCols = 0 Then Exit Sub
    
    Selection.ConvertToTable Separator:=wdSeparateByTabs, NumColumns:=NumCols, _
        NumRows:=RowCount, AutoFit:=True
    
    If Val(Application.Version) >= Wd2003 Then
        Table_SetStyleOptions
    End If
    
    With Options
        .DefaultBorderLineStyle = wdLineStyleSingle
        .DefaultBorderLineWidth = wdLineWidth050pt
        .DefaultBorderColorIndex = wdBlack
    End With
    
    Selection.Tables(1).Columns.AutoFit
    
    With Selection.Tables(1)
        .Borders(wdBorderLeft).LineStyle = wdLineStyleNone
        .Borders(wdBorderRight).LineStyle = wdLineStyleNone
        .Borders(wdBorderTop).LineStyle = wdLineStyleNone
        .Borders(wdBorderBottom).LineStyle = wdLineStyleNone
        .Borders(wdBorderHorizontal).LineStyle = wdLineStyleNone
        .Borders(wdBorderVertical).LineStyle = wdLineStyleNone
        ' .Borders(wdBorderDiagonalDown).LineStyle = wdLineStyleNone
        ' .Borders(wdBorderDiagonalUp).LineStyle = wdLineStyleNone
        .Borders.Shadow = False
    End With
    
    ' Set right justification on cells that need it
    '
    For Each ThisCell In Selection.Tables(1).Range.Cells
        CellStyle = ThisCell.Range.Characters(1).Style.NameLocal
        
        If CellStyle Like "t[ch]r[0-9] *" Then
            ThisCell.Range.ParagraphFormat.Alignment = wdAlignParagraphRight
        End If
    Next ThisCell
End Sub
Sub XcopyHeader()
    Selection.WholeStory
    Selection.Copy
    ActiveWindow.ActivePane.View.PreviousHeaderFooter
    Selection.WholeStory
    Selection.Paste
    Selection.TypeBackspace
End Sub
Sub ZoomRecall()
Dim PgCols As String
PgCols = VariableGet("ZoomView")
If PgCols = "" Then PgCols = "PageWidth"

With ActiveDocument.ActiveWindow.View
   .Type = PageOrPrintView
   If PgCols = "TwoPages" Then
         .Zoom.PageColumns = 2
         .Zoom.PageRows = 1
   ElseIf PgCols = "OnePage" Then
         .Zoom.PageColumns = 1
         .Zoom.PageRows = 1
   Else:
      .Zoom.PageFit = wdPageFitBestFit
   End If
End With
End Sub
Sub ZoomSet()
Dim PgCols As String
With ActiveDocument.ActiveWindow.View
   If .Type <> PageOrPrintView Then
      Exit Sub
   Else:
      If .Zoom.PageColumns = 2 Then
         PgCols = "TwoPages"
      ElseIf .Zoom > 100 Then
         PgCols = "PageWidth"
      Else:
         PgCols = "OnePage"
      End If
   End If
End With

VariableSet "ZoomView", PgCols
End Sub
Sub testdialog()
    dbMsgBoxWithTimeout.dbMsgBoxWithTimeout_Initialize
'    dbMsgBoxWithTimeout.Show
End Sub

